
#!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
#!!!  M O R P H O T A C T I C S  !!!
#!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!


#Unclear if Aor should be tagged as aorist or nothing, because it's sometimes meaningless

#Develop complex tenses with onstraint Grammar TO DO

#TO DO Valency morphology (passive, causative, inchoative)
# Theme-less causatives, maybe tags or columns
# Maybe the nil group shouldnt get a causative version? or cil class
# lexical caus/passives can get their own lexical entry
#via https://wiki.apertium.org/wiki/Word_sense_disambiguation and Lexical Selection

# TO DO use postdix (post-generator) to figure out how to get ci -> c'
#https://wiki.apertium.org/wiki/Post-generator
#Could use post-dix to do indicative too

#TO DO: gbcil is an incomplete paradigm in Boyacioglu but it has more entries in EANC
#TO DO check for paradigm leveling of irregular nil/cil in imperatives to get aorist (EANC)


# TO DO double check defectives
#15 Cil prohibtive, negativ present/imperfect,evid,subj
#15 16 difference in Imperative

#Add exlamation mark for imperative TO DO

#Not sure if participles should count as verbal TO DO
#because they feel verbal, but can also act as nouns/adjectives 



########################################
########################################
########################################
##### VERB paradigms
########################################
########################################
########################################
########################################


#Base of causative is root+th
PATTERN CausBase
VerbRootE(1) VZero VerbRootE(2): ThE 
VerbRootE(1) VZero VerbRootE(2): ThA? # Dir/LR   to capture variation in theme-less or with mismatched vowel 
VerbRootI(1) VZero VerbRootI(2): ThE #I'm just changing -i- to -e- directly
VerbRootI(1) VZero VerbRootI(2): ThA? # Dir/LR   to capture variation in theme-less or with mismatched vowel 
VerbRootA(1) VZero VerbRootA(2): ThA 
VerbRootA(1) VZero VerbRootA(2): ThE? # Dir/LR   to capture variation in theme-less or with mismatched vowel  

InchBase InchEmpty  #<inch> marked by either covert or overt -n-

#This is for lexically listed causatives
#Lexical causatives take their own special valency tag. Might need to change this later if 
#it violates some tagging rule TO DO
CausativeVerbRoot VZero [<<tv>:]
#Orthographic variants are separately handled based on the inflection lsot


#Irregulars

#Irregulars
VerbRootNil(1)   VZero VerbRootNil(2):    ThE  # անցնիլ group from Boyacioglu
VerbRootNil(1)   VZero VerbRootNil(2): (EmptyNasal  ThE)?  # Dir/LR   to capture variation in theme-less or infix-less
# VerbRootCilV(1)|VerbRootCilC(1)   VZero VerbRootCilV(2):|VerbRootCilC(2):     # կպչիլ group from Boyacioglu
VerbRootCel(1)   VZero VerbRootCel(2):    ThE # թռչել not in Boyacioglu
VerbRootCel(1)   VZero VerbRootCel(2): (EmptyAffr ThE)? # Dir/LR   to capture variation in theme-less or infix-less
VerbRootNelNale(1)  VZero VerbRootNelNale(2):  ThE  #իջնել իջնալ
VerbRootNelNale(1)  VZero VerbRootNelNale(2): (EmptyNasal ThE)? # Dir/LR   to capture variation in theme-less or infix-less
VerbRootNelNala(1)   VZero VerbRootNelNala(2):   ThA 
VerbRootNelNale(1)  VZero VerbRootNelNale(2): (EmptyNasal ThA)? # Dir/LR   to capture variation in theme-less or infix-less
VerbRootCilIlc(1) EmptyAffr VZero VerbRootCilIlc(2):  ThE  		#դպչիլ դպիլ 
VerbRootCilIlc(1) EmptyAffr VZero VerbRootCilIlc(2): (EmptyAffr ThE)? # Dir/LR   to capture variation in theme-less or infix-less
VerbRootCilIli(1) VZero VerbRootCilIli(2):  ThE 
VerbRootCilIli(1) VZero VerbRootCilIli(2): (EmptyAffr ThE)? # Dir/LR   to capture variation in theme-less or infix-less


VerbRootCry(1) VZero VerbRootCry(2): ThA   # լալ
VerbRootBring(1) VZero VerbRootBring(2): ThE   # բերել
VerbRootBring(1) VZero VerbRootBring(2): ThE? # Dir/LR   to capture variation in theme-les
# skip because listed in CausativeVerbRootOrthographicAffr
#VerbRootTurnPres(1) VZero VerbRootBring(2): ThA Inf # դառնալ
VerbRootSeeA(1)   VZero VerbRootSeeA(2):     # տեսնալ
VerbRootSeeA(1) VZero VerbRootSeeA(2): (EmptyNasal? ThE)? # Dir/LR   to capture variation in theme-less or infix-less
VerbRootSeeE(1)   VZero VerbRootSeeE(2):    # տեսնալ
VerbRootSeeE(1) VZero VerbRootSeeE(2): (EmptyNasal? ThA)? # Dir/LR   to capture variation in theme-less or infix-less
VerbRootPutPres(1) VZero VerbRootPutPres(2): ThE   # դնել
VerbRootHitPast(1) VZero VerbRootHitPast(2): ThE   # զարնել
VerbRootAccept(1) VZero VerbRootAccept(2): ThE   # ընդունիլ
VerbRootFallPast(1) VZero VerbRootFallPast(2): ThA   # իյնալ
VerbRootPlay(1) VZero VerbRootPlay(2):     # խաղալ
VerbRootPlay(1) VZero VerbRootPlay(2): ThA   # Dir/LR խաղալ
VerbRootStay(1) VZero VerbRootStay(2): ThA   # մնալ
VerbRootSit(1) VZero VerbRootSit(2): ThE   # նստիլ
VerbRootEatPast(1) VZero VerbRootEatPast(2):     # ուտել
VerbRootEatPres(1) VZero VerbRootEatPres(2):  ThE   # Dir/LR


#Base of passive is root
PATTERN PassBase
VerbRootE(1) VZero VerbRootE(2): 
VerbRootI(1) VZero VerbRootI(2): 
VerbRootA(1) VZero VerbRootA(2): ThA Aor

CausBase CausPass 
#This is for orthographic variants
CausativeVerbRootOrthographicSchwa VZero [<tv>:] CausPass
CausativeVerbRootOrthographicAffr VZero [<tv>:] CausOrthographicAffrPass


#This is for lexically listed passives
#Lexical passives take their own special valency tag. Might need to change this later if 
#it violates some tagging rule TO DO
PassiveVerbRoot VZero [<iv>:] 

#Should I care that this will rarely be traversed? TO DO
InchBase InchEmpty Aor 
  
#Passives of irregular or suppletive verbs
VerbRootNil(1) EmptyNasal VZero VerbRootNil(2):  # անցնիլ group from Boyacioglu
VerbRootNil(1) VZero VerbRootNil(2):  # Dir/LR to capture variation in infix-drop
# VerbRootCilV(1)|VerbRootCilC(1) EmptyAffr VZero VerbRootCilV(2):|VerbRootCilC(2): # կպչիլ group from Boyacioglu
VerbRootCel(1) EmptyAffr VZero VerbRootCel(2):  # թռչել not in Boyacioglu
VerbRootCel(1) 		 	VZero VerbRootCel(2):  # Dir/LR to capture variation in infix-drop
VerbRootNelNale(1) EmptyNasal VZero VerbRootNelNale(2):  #իջնել իջնալ
VerbRootNelNale(1)  VZero VerbRootNelNale(2):  # Dir/LR to capture variation in infix-drop
VerbRootNelNala(1) EmptyNasal VZero VerbRootNelNala(2): 
VerbRootNelNala(1)  VZero VerbRootNelNala(2):  # Dir/LR to capture variation in infix-drop
VerbRootCilIlc(1) EmptyAffr VZero VerbRootCilIlc(2): 		#դպչիլ դպիլ 
VerbRootCilIlc(1) VZero VerbRootCilIlc(2):  # Dir/LR to capture variation in infix-drop
VerbRootCilIli(1) VZero VerbRootCilIli(2): 

VerbRootGivePast(1) VZero VerbRootGivePast(2):  # տալ
VerbRootBring(1) VZero VerbRootBring(2):   # բերել
VerbRootTurnPast(1) VZero VerbRootTurnPast(2):  # դառնալ
VerbRootTake(1) EmptyNasal VZero VerbRootTake(2):  # առնել
VerbRootTake(1)  VZero VerbRootTake(2):  # Dir/LR to capture variation in infix-drop
VerbRootSeeA(1) EmptyNasal VZero VerbRootSeeA(2):  # տեսնալ
VerbRootSeeA(1)  VZero VerbRootSeeA(2):  # Dir/LR to capture variation in infix-drop
VerbRootSeeE(1) EmptyNasal VZero VerbRootSeeE(2):  # տեսնալ
VerbRootSeeE(1)  VZero VerbRootSeeE(2):  # Dir/LR to capture variation in infix-drop
VerbRootOpenPast(1) VZero VerbRootOpenPast(2):  # բանալ
VerbRootPutPast(1) VZero VerbRootPutPast(2):   # դնել
VerbRootHitPres(1) VZero VerbRootHitPres(2):   # զարնել
VerbRootAccept(1) VZero VerbRootAccept(2):   # ընդունիլ
VerbRootDoPast(1) VZero VerbRootDoPast(2):   # ընել
VerbRootSay(1) VZero VerbRootSay(2):  # ըսել
VerbRootBite(1) EmptyNasal VZero VerbRootBite(2):   # խածնել
VerbRootBite(1)  VZero VerbRootBite(2):   # Dir/LR to capture variation in infix-drop
VerbRootPlay(1) VZero VerbRootPlay(2): Aor  # խաղալ
VerbRootEatPres(1) VZero VerbRootEatPres(2):   # ուտել
VerbRootSendPast(1) VZero VerbRootSendPast(2):   # տանիլ
 

#Base of inchoative is root+th or root
PATTERN InchBase
InchRoot(1) VZero InchRoot(2): 
# InchRootTh0(1) VZero InchRootTh0(2): 
# InchRootThA(1) ThA VZero InchRootThA(2):
# InchRootThE(1) ThE VZero InchRootThE(2):

#Infinitival
PATTERN Infinitival
VerbRootE(1) VZero VerbRootE(2): ThE Inf
VerbRootI(1) VZero VerbRootI(2): ThI Inf
VerbRootA(1) VZero VerbRootA(2): ThA Inf

CausBase CausPres ThE Inf
CausativeVerbRootOrthographicSchwa VZero [<tv>:] CausOrthographicSchwaPres ThE Inf
CausativeVerbRootOrthographicAffr VZero [<tv>:] CausOrthographicAffrPres ThE Inf


PassBase Pass ThI Inf

InchBase InchNasal ThA Inf

#Archaic/obsolete
VerbRootUv(1) VZero VerbRootUv(2): ThU Inf # արտահեղուլ 
VerbRootUc(1) VZero VerbRootUc(2): ThU Inf # թողուլ 
VerbRootUcanel(1) VZero VerbRootUcanel(2): ThA EmptyNasal ThE Inf  # Dir/LR # ծանուցանել , preferably part of RootE class
VerbRootNulV(1) EmptyNasal VZero VerbRootNulV(2): ThU Inf # երդնուլ 
VerbRootNulC(1) EmptyNasal VZero VerbRootNulC(2): ThU Inf # զբօսնուլ

#Irregulars
VerbRootNil(1) EmptyNasal VZero VerbRootNil(2): ThI Inf # անցնիլ group from Boyacioglu
# VerbRootCilV(1)|VerbRootCilC(1) EmptyAffr VZero VerbRootCilV(2):|VerbRootCilC(2): ThI Inf # կպչիլ group from Boyacioglu
VerbRootCel(1) EmptyAffr VZero VerbRootCel(2): ThE Inf # թռչել not in Boyacioglu
VerbRootNelNale(1) EmptyNasal VZero VerbRootNelNale(2): ThE Inf #իջնել իջնալ
VerbRootNelNala(1) EmptyNasal VZero VerbRootNelNala(2): ThA Inf 
VerbRootRiseEPres(1) VZero VerbRootRiseEPres(2): ThE Inf #ելլել ելլալ
VerbRootRiseAPres(1) VZero VerbRootRiseAPres(2): ThA Inf 
VerbRootCilIlc(1) EmptyAffr VZero VerbRootCilIlc(2): ThI Inf 		#դպչիլ դպիլ 
VerbRootCilIli(1) VZero VerbRootCilIli(2): ThI Inf 		 

VerbRootCanLong(1) EmptyNasal VZero VerbRootCanLong(2): ThA Inf #կարենալ, not կրնալ
VerbRootCanShort(1) EmptyNasal VZero VerbRootCanLong(2): ThA Inf #Dir/LR կրնալ, exists in rare speech as inflected forms չկրնալով

VerbRootAppear(1) VZero VerbRootAppear(2): ThI Inf # Defective երեւիլ, only indicative non-perfect, no subj in boyacioglu, but I disagree
VerbRootBeLongPres(1) VZero VerbRootBeLongPres(2): ThA Inf # Irregular ըլլալ
VerbRootBeLongPresEastern(1) VZero VerbRootBeLongPresEastern(2): ThE Inf # Irregular լինել
VerbRootComePres(1) VZero VerbRootComePres(2): ThA Inf # irregular գալ եկայ
VerbRootCry(1) VZero VerbRootCry(2): ThA Inf # լալ
VerbRootGivePres(1) VZero VerbRootGivePres(2): ThA Inf # տալ
VerbRootBring(1) VZero VerbRootBring(2): ThE Inf # բերել
VerbRootTurnPres(1) VZero VerbRootBring(2): ThA Inf # դառնալ
VerbRootTake(1) EmptyNasal VZero VerbRootTake(2): ThE Inf # առնել
VerbRootSeeA(1) EmptyNasal VZero VerbRootSeeA(2): ThA Inf # տեսնալ
VerbRootSeeE(1) EmptyNasal VZero VerbRootSeeE(2): ThE Inf # տեսնալ
VerbRootOpenPres(1) VZero VerbRootOpenPres(2): ThA Inf # բանալ
VerbRootPutPres(1) VZero VerbRootPutPres(2): ThE Inf # դնել
VerbRootGoPres(1) VZero VerbRootGoPres(2): ThA Inf # երթալ
VerbRootHitPres(1) VZero VerbRootHitPres(2): ThE Inf # զարնել
VerbRootYield(1) VZero VerbRootYield(2): ThI Inf # զիջիլ
VerbRootAccept(1) VZero VerbRootAccept(2): ThI Inf # ընդունիլ
VerbRootDoPres(1) VZero VerbRootDoPres(2): ThE Inf # ընել
VerbRootSay(1) VZero VerbRootSay(2): ThE Inf # ըսել
VerbRootSpitOldPres(1) EmptyNasal VZero VerbRootSpitOldPres(2): ThE Inf # թքնել
VerbRootFallPres(1) VZero VerbRootFallPres(2): ThA Inf # իյնալ
VerbRootBite(1) EmptyNasal VZero VerbRootBite(2): ThE Inf # խածնել
VerbRootPlay(1) VZero VerbRootPlay(2): ThA Inf # խաղալ
VerbRootStay(1) VZero VerbRootStay(2): ThA Inf # մնալ
VerbRootSit(1) VZero VerbRootSit(2): ThI Inf # նստիլ
VerbRootEatPres(1) VZero VerbRootEatPres(2): ThE Inf # ուտել
VerbRootSendPres(1) VZero VerbRootSendPres(2): ThI Inf # տանիլ

#Following defective verbs lack infinitival for Boyacioglu, but they have one in Kouyoumdjian
VerbRootHaveE(1) VZero VerbRootHaveE(2): ThE Inf # ունել, but inflected as ունիմ
VerbRootHaveI(1) VZero VerbRootHaveI(2): ThI Inf # ունիլ
VerbRootKnow(1) VZero VerbRootKnow(2): ThE Inf # գիտել 


PATTERN Indicative
#Verbs with prefix
:Indc IndcSubjStem Indc:
#Including irregular monosylabic verbs that take gu-
:IndcShort IndcSubjBaseMonoStem IndcShort:

#Irregulars that lake prefix 
IndicativeIrregular

PATTERN Subjunctive
IndcSubjStem Subj0
IndcSubjBaseMonoStem Subj0

#irregulars that lack prefix in indicative
SubjunctiveIrregular


PATTERN IndcSubjStem
#Stem is base(root+th) and the agreement, no prefix or mood marking
#present
IndcSubjBase Pres0 PresAgr 
#impefect
IndcSubjBase ImpfAgr 


PATTERN IndcSubjBase
#Base of verb for regular Indicative and Subjunctive (without prefix)
#Base is root till theme vowel
#Present
VerbRootE(1) VZero VerbRootE(2): ThE 
VerbRootI(1) VZero VerbRootI(2): ThI 
VerbRootA(1) VZero VerbRootA(2): ThA 

CausBase CausPres ThE
CausativeVerbRootOrthographicSchwa VZero [<tv>:] CausOrthographicSchwaPres ThE
CausativeVerbRootOrthographicAffr VZero [<tv>:] CausOrthographicAffrPres ThE


PassBase Pass ThI 

InchBase InchNasal ThA 


VerbRootUv(1) VZero VerbRootUv(2): ThU	#Unclear which one is preferred because archaic
VerbRootUv(1) VZero VerbRootUv(2): ThE 
VerbRootUcanel(1) VZero VerbRootUcanel(2): ThA EmptyNasal ThE  # Dir/LR

VerbRootNil(1) EmptyNasal VZero VerbRootNil(2): ThI 
VerbRootNelNale(1) EmptyNasal VZero VerbRootNelNale(2): ThE
VerbRootNelNala(1) EmptyNasal VZero VerbRootNelNala(2): ThA
VerbRootRiseEPres(1) VZero VerbRootRiseEPres(2): ThE
VerbRootRiseAPres(1) VZero VerbRootRiseAPres(2): ThA
VerbRootCilIlc(1) EmptyAffr VZero VerbRootCilIlc(2): ThI 
VerbRootCilIli(1) VZero VerbRootCilIli(2): ThI 
VerbRootCel(1) EmptyAffr VZero VerbRootCel(2): ThE    
VerbRootBeLongPres(1) VZero VerbRootBeLongPres(2): ThA 
VerbRootBeLongPresEastern(1) VZero VerbRootBeLongPresEastern(2): ThE
VerbRootBring(1) VZero VerbRootBring(2): ThE
VerbRootTurnPres(1) VZero VerbRootBring(2): ThA
VerbRootTake(1) EmptyNasal VZero VerbRootTake(2): ThE
VerbRootSeeA(1) EmptyNasal VZero VerbRootSeeA(2): ThA
VerbRootSeeE(1) EmptyNasal VZero VerbRootSeeE(2): ThE
VerbRootOpenPres(1) VZero VerbRootOpenPres(2): ThA
VerbRootPutPres(1) VZero VerbRootPutPres(2): ThE
VerbRootGoPres(1) VZero VerbRootGoPres(2): ThA 
VerbRootHitPres(1) VZero VerbRootHitPres(2): ThE 
VerbRootYield(1) VZero VerbRootYield(2): ThI 
VerbRootAccept(1) VZero VerbRootAccept(2): ThI 
VerbRootDoPres(1) VZero VerbRootDoPres(2): ThE
VerbRootSay(1) VZero VerbRootSay(2): ThE
VerbRootSpitOldPres(1) EmptyNasal VZero VerbRootSpitOldPres(2): ThE
VerbRootFallPres(1) VZero VerbRootFallPres(2): ThA
VerbRootBite(1) EmptyNasal VZero VerbRootBite(2): ThE 
VerbRootPlay(1) VZero VerbRootPlay(2): ThA
VerbRootStay(1) VZero VerbRootStay(2): ThA
VerbRootSit(1) VZero VerbRootSit(2): ThI
VerbRootEatPres(1) VZero VerbRootEatPres(2): ThE
VerbRootSendPres(1) VZero VerbRootSendPres(2): ThI

VerbRootAppear(1) VZero VerbRootAppear(2): ThI # boyacioglu had defective երեւիլ defective 
#I think Boyacioglu has an error because the -i- stayed -i- here


PATTERN IndcSubjBaseMonoStem
#Monosyllabic verbs which take the prefix gu-
#stem is base (root+th) and agr
IndcSubjBaseMono Pres0 PresAgr
IndcSubjBaseMono ImpfAgr 

PATTERN IndcSubjBaseMono
#Monosyllabic verbs which take the prefix gu-
#base is root and theme
VerbRootComePres(1) VZero VerbRootComePres(2): ThA 
VerbRootCry(1) VZero VerbRootCry(2): ThA 
VerbRootGivePres(1) VZero VerbRootGivePres(2): ThA 


PATTERN IndicativeIrregular
# I won't generalize them just ot be safe
#Some of these don't take prefix 
#Some of them don't have any subjunctive form
#:Indc VerbRootCilV(1)|VerbRootCilC(1) EmptyAffr VZero VerbRootCilV(2):|VerbRootCilC(2): ThI Pres0 PresAgr Indc: # երկնչիլ defective
#:Indc VerbRootCilV(1) EmptyAffr VZero VerbRootCilV(2): ThI ImpfAgr Indc: 
VerbRootCanShort(1) EmptyNasal VZero VerbRootCanShort(2): ThA Pres0 PresAgr Indc: 
VerbRootCanShort(1) EmptyNasal VZero VerbRootCanShort(2): ThA ImpfAgr Indc:
VerbRootHave(1) VZero VerbRootHave(2): ThI Pres0 PresAgr Indc: # ունիմ defective
VerbRootHave(1) VZero VerbRootHave(2): ThI ImpfAgr Indc:
VerbRootKnow(1) VZero VerbRootKnow(2): ThE Pres0 PresAgr Indc: # գիտեմ defectve
VerbRootKnow(1) VZero VerbRootKnow(2): ThE ImpfAgr Indc:
VerbRootExist(1) VZero VerbRootExist(2): ThA Pres0 PresAgr Indc: # կամ defectve
VerbRootExist(1) VZero VerbRootExist(2): ThA ImpfAgr Indc: 

#imperfect

#U theme vowels behave haphazardly
#For optional variants, can't determine which is preferred form
:Indc VerbRootUc(1) VZero VerbRootUc(2): ThU Pres0 PresAgr Indc: 
:Indc VerbRootUc(1) VZero VerbRootUc(2): ThU|ThE ImpfAgr Indc:
:Indc VerbRootNulV(1) EmptyNasal VZero VerbRootNulV(2): ThU Pres0 PresAgr Indc: 
:Indc VerbRootNulV(1) EmptyNasal VZero VerbRootNulV(2): ThE ImpfAgr Indc: 
:Indc VerbRootNulC(1) EmptyNasal VZero VerbRootNulC(2): ThU Pres0 PresAgr Indc: 
:Indc VerbRootNulC(1) EmptyNasal VZero VerbRootNulC(2): ThE ImpfAgr Indc: 


PATTERN SubjunctiveIrregular
#These have an irregular indicative form that lacks prefix
VerbRootCanShort(1) EmptyNasal VZero VerbRootCanShort(2): ThA Pres0 PresAgr Subj0 #I disagree with Boyacioglu, I have subjunctives
VerbRootCanShort(1) EmptyNasal VZero VerbRootCanShort(2): ThA ImpfAgr Subj0
VerbRootCanLong(1) EmptyNasal VZero VerbRootCanLong(2): ThA Pres0 PresAgr Subj0 
VerbRootCanLong(1) EmptyNasal VZero VerbRootCanLong(2): ThA ImpfAgr Subj0  
VerbRootHave(1) VZero VerbRootHave(2): ThI Pres0 PresAgr Subj0  #I disagree with Boyacioglu, I have subjunctives
VerbRootHave(1) VZero VerbRootHave(2): ThI ImpfAgr Subj0 
VerbRootKnow(1) VZero VerbRootKnow(2): ThE Pres0 PresAgr Subj0  #I disagree with Boyacioglu, I have subjunctives
VerbRootKnow(1) VZero VerbRootKnow(2): ThE ImpfAgr Subj0 
VerbRootBeLongPres(1) VZero VerbRootBeLongPres(2): ThA Pres0 PresAgr Subj0
VerbRootBeLongPres(1) VZero VerbRootBeLongPres(2): ThA ImpfAgr Subj0
VerbRootBeLongPresEastern(1) VZero VerbRootBeLongPresEastern(2): ThE  Pres0 PresAgr Subj0
VerbRootBeLongPresEastern(1) VZero VerbRootBeLongPresEastern(2): ThE ImpfAgr Subj0

VerbRootExist(1) VZero VerbRootExist(2): ThA Pres0 PresAgr Subj0	#I disagree with Boyacioglu, I have subjunctives
VerbRootExist(1) VZero VerbRootExist(2): ThA ImpfAgr Subj0	


 
 #U theme vowels with C-initial segment behave haphazardly
#For optional variants, can't determine which is preferred form in some cases
#I let it overgeneralize the -e- form to negation even though the grammar only shows -u-
VerbRootUc(1) VZero VerbRootUc(2): ThU|ThE Pres0 PresAgr Subj0
VerbRootUc(1) VZero VerbRootUc(2): ThU|ThE ImpfAgr Subj0
VerbRootNulV(1) EmptyNasal VZero VerbRootNulV(2): ThU Pres0 PresAgr Subj0
VerbRootNulV(1) EmptyNasal VZero VerbRootNulV(2): ThU|ThE ImpfAgr Subj0 #Overgeneralize because no negative form, and not clear about -e- in complex tenses
VerbRootNulC(1) EmptyNasal VZero VerbRootNulC(2):  ThU  Pres0 PresAgr Subj0
VerbRootNulC(1) EmptyNasal VZero VerbRootNulC(2):  ThE ImpfAgr Subj0



PATTERN Past
#Past
VerbRootE(1) VZero VerbRootE(2): ThE Aor PastPrimAgr Indc0
VerbRootI(1) VZero VerbRootI(2): ThI Aor PastSecAgr Indc0 
VerbRootA(1) VZero VerbRootA(2): ThA Aor PastPrimAgr Indc0 

CausBase CausPast Aor PastPrimAgr Indc0
CausativeVerbRootOrthographicSchwa VZero [<tv>:] CausPast Aor PastPrimAgr Indc0
CausativeVerbRootOrthographicAffr VZero [<tv>:] CausOrthographicAffrPast Aor PastPrimAgr Indc0


PassBase Pass ThI Aor PastSecAgr Indc0

InchBase InchEmpty Aor PastSecAgr Indc0


VerbRootUv(1) VZero VerbRootUv(2): ThU Aor PastPrimAgr Indc0 
VerbRootUc(1) VZero VerbRootUc(2): ThU Aor PastPrimAgr Indc0 
VerbRootUc(1) VZero VerbRootUc(2): PastPrimAgr Indc0 #   This form can't b negated but I'll let it over-generalize
VerbRootUcanel(1) VZero VerbRootUcanel(2): PastPrimAgr Indc0  # Dir/LR
VerbRootNulV(1) VZero VerbRootNulV(2): ThU PastSecAgr
VerbRootNulV(1) VZero VerbRootNulV(2): PastSecAgr
VerbRootNulC(1) EmptyNasal VZero VerbRootNulC(2): ThE Aor PastPrimAgr #Obsolete so the preference is unknown


VerbRootNil(1) VZero VerbRootNil(2): PastSecAgr Indc0
# VerbRootCilV(1)|VerbRootCilC(1) VZero VerbRootCilV(2):|VerbRootCilC(2): PastSecAgr Indc0
VerbRootCel(1) VZero VerbRootCel(2): PastSecAgr Indc0
VerbRootNelNale(1)|VerbRootNelNala(1) VZero VerbRootNelNale(2):|VerbRootNelNala(2): PastSecAgr Indc0
VerbRootRiseEPast(1)|VerbRootRiseAPast(1) VZero VerbRootRiseEPast(2):|VerbRootRiseAPast(2): PastSecAgr Indc0
VerbRootCilIlc(1)|VerbRootCilIli(1) VZero VerbRootCilIlc(2):|VerbRootCilIli(2): PastSecAgr Indc0
VerbRootCanShort(1) VZero VerbRootCanShort(2): Aor PastSecAgr Indc0
VerbRootBeLongPast(1) VZero VerbRootBeLongPast(2): PastSecAgr Indc0
VerbRootComePast(1) VZero VerbRootComePast(2): PastSecAgr Indc0
VerbRootCry(1) VZero VerbRootCry(2): ThA Aor PastSplitAgr Indc0
VerbRootGivePast(1) VZero VerbRootGivePast(2): PastSplitAgr Indc0
VerbRootBring(1) VZero VerbRootBring(2): PastSplitAgr Indc0
VerbRootTurnPast(1) VZero VerbRootTurnPast(2): PastSecAgr Indc0
VerbRootTake(1) VZero VerbRootTake(2): PastSplitAgr Indc0
VerbRootSeeA(1)|VerbRootSeeE(1) VZero VerbRootSeeA(2):|VerbRootSeeE(2): PastSecAgr Indc0
VerbRootOpenPast(1) VZero VerbRootOpenPast(2): PastSplitAgr Indc0
VerbRootPutPast(1) VZero VerbRootPutPast(2): PastSplitAgr Indc0
VerbRootGoPastC(1) VZero VerbRootGoPastC(2): ThA Aor PastAgrGoNo3Sg Indc0
VerbRootGoPast3(1) VZero VerbRootGoPast3(2): ThA Aor PastAgrGoYes3Sg Indc0
VerbRootHitPast(1) VZero VerbRootHitPast(2): PastSplitAgr Indc0
VerbRootYield(1) VZero VerbRootYield(2): PastSecAgr Indc0
VerbRootAccept(1) VZero VerbRootAccept(2): ThI Aor PastPrimAgr Indc0 
VerbRootAccept(1) VZero VerbRootAccept(2): ThI Aor PastSecAgr Indc0 # Dir/LR
VerbRootDoPast(1) VZero VerbRootDoPast(2): PastSplitAgr Indc0
VerbRootSay(1) VZero VerbRootSay(2): PastSplitAgr Indc0
VerbRootSpitOldPres(1) VZero VerbRootSpitOldPres(2): PastSecAgr Indc0
VerbRootSpitOldPres(1) VZero VerbRootSpitOldPres(2): PastSplitAgr Indc0 # Dir/LR
VerbRootFallPast(1) VZero VerbRootFallPast(2): PastSecAgr Indc0
VerbRootBite(1) VZero VerbRootBite(2): PastSecAgr Indc0
VerbRootBite(1) VZero VerbRootBite(2): PastSplitAgr  Indc0 # Dir/LR
VerbRootPlay(1) VZero VerbRootPlay(2): Aor PastSecAgr Indc0
VerbRootPlay(1) VZero VerbRootPlay(2): ThA Aor PastPrimAgr Indc0 # Dir/LR
VerbRootStay(1) VZero VerbRootStay(2): ThA Aor PastPrimAgr Indc0
VerbRootSit(1) VZero VerbRootSit(2): PastSecAgr Indc0
VerbRootSit(1) VZero VerbRootSit(2): ThI Aor PastSecAgr  Indc0 # Dir/LR
VerbRootEatPast(1) VZero VerbRootEatPast(2): PastSecAgr Indc0
VerbRootSendPast(1) VZero VerbRootSendPast(2): PastSplitAgr Indc0

PATTERN Imperative
#Optionality is handled via Dir/LR
#Imperatives sg
VerbRootE(1) VZero VerbRootE(2): ThE ImpSg
VerbRootI(1) VZero VerbRootI(2): ThI ImpSg # Dir/LR
VerbRootI(1) VZero VerbRootI(2): ImpSgIR
VerbRootA(1) VZero VerbRootA(2): ThA ImpSg 

PassBase Pass ThI ImpSg # Dir/LR
PassBase Pass ImpSgIR

CausBase CausPast ImpSgR
CausativeVerbRootOrthographicSchwa VZero [<tv>:] CausPast ImpSgR
CausativeVerbRootOrthographicAffr VZero [<tv>:] CausOrthographicAffrPast ImpSgR


InchBase InchEmpty Aor ImpSgIR

VerbRootUv(1)|VerbRootUc(1) VZero VerbRootUv(2):|VerbRootUc(2): ImpSgInternalMark #Unclear which one is preferred because archaic/obsolete
VerbRootUv(1)|VerbRootUc(1) VZero VerbRootUv(2):|VerbRootUc(2): ThU ImpSgInternalMark
VerbRootUv(1)|VerbRootUc(1) VZero VerbRootUv(2):|VerbRootUc(2): ImpSgUR
VerbRootUcanel(1) VZero VerbRootUcanel(2): ImpSgUR  # Dir/LR
VerbRootNulV(1) VZero VerbRootNulV(2): ThU ImpSgIR
VerbRootNulC(1) EmptyNasal VZero VerbRootNulC(2): ThE ImpSg
VerbRootNulC(1) VZero VerbRootNulC(2): ImpSgIR	#Unclear which one is preferred because archaic/obsolete

VerbRootNil(1) VZero VerbRootNil(2): ImpSgIR
# VerbRootCilC(1) VZero VerbRootCilC(2): ImpSgIR
# VerbRootCilV(1) VZero VerbRootCilV(2): ThI ImpSg # Dir/LR
# VerbRootCilV(1) VZero VerbRootCilV(2): ImpSgIR
VerbRootNelNale(1)|VerbRootNelNala(1) VZero VerbRootNelNale(2):|VerbRootNelNala(2): ImpSgIR
VerbRootRiseEPast(1)|VerbRootRiseAPast(1) VZero VerbRootRiseEPast(2):|VerbRootRiseAPast(2): ImpSg # Dir/LR
VerbRootRiseEPast(1)|VerbRootRiseAPast(1) VZero VerbRootRiseEPast(2):|VerbRootRiseAPast(2): ImpSgIR
VerbRootCilIlc(1)|VerbRootCilIli(1) VZero VerbRootCilIlc(2):|VerbRootCilIli(2): EmptyAffr? ThI ImpSg # Dir/LR դպչէ was found  in the bible
VerbRootCilIlc(1)|VerbRootCilIli(1) VZero VerbRootCilIlc(2):|VerbRootCilIli(2): ImpSgIR
VerbRootCel(1) VZero VerbRootCel(2): ThI ImpSg # Dir/LR   թռի, not թռէ
VerbRootCel(1) VZero VerbRootCel(2): ImpSgIR
VerbRootCanShort(1) VZero VerbRootCanShort(2): Aor ImpSgIR
VerbRootBeLongPast(1) VZero VerbRootBeLongPast(2): ImpSgIR
VerbRootComePast(1) VZero VerbRootComePast(2): ImpSgUR
VerbRootCry(1) VZero VerbRootCry(2): ThA :ImpSgInternalMark Aor ImpSgInternalMark: # Force the metathesis because aorist boundary causes problems
VerbRootGivePres(1) VZero VerbRootGivePres(2): ImpSgUR
VerbRootBring(1) VZero VerbRootBring(2): ImpSgInternalMark 
VerbRootTurnPast(1) VZero VerbRootTurnPast(2): ImpSgIR 
VerbRootTake(1) VZero VerbRootTake(2): ImpSgInternalMark 
VerbRootSeeA(1)|VerbRootSeeE(1) VZero VerbRootSeeA(2):|VerbRootSeeE(2): ImpSgInternalMark 
VerbRootOpenPast(1) VZero VerbRootOpenPast(2): ImpSgInternalMark 
VerbRootPutImpSg(1) VZero VerbRootPutImpSg(2): ImpSgIR 
VerbRootGoPast3(1) VZero VerbRootGoPast3(2): ThA ImpSg
VerbRootHitPast(1) VZero VerbRootHitPast(2): ImpSgInternalMark 
VerbRootYield(1) VZero VerbRootYield(2): ThI ImpSg # Dir/LR
VerbRootYield(1) VZero VerbRootYield(2): ImpSgIR
VerbRootAccept(1) VZero VerbRootAccept(2): ThI ImpSg # Dir/LR
VerbRootAccept(1) VZero VerbRootAccept(2): ImpSgIR
VerbRootDoPast(1) VZero VerbRootDoPast(2): ThE ImpSg
VerbRootSay(1) VZero VerbRootSay(2): ThE ImpSg
VerbRootSpitOldPres(1) VZero VerbRootSpitOldPres(2): ThE ImpSg
VerbRootSpitOldPres(1) VZero VerbRootSpitOldPres(2): ImpSgIR # Dir/LR
VerbRootSpitOldImpSg(1) VZero VerbRootSpitOldImpSg(2): ImpSgInternalMark # Dir/LR
VerbRootFallPast(1) VZero VerbRootFallPast(2): ImpSgIR
VerbRootBite(1) VZero VerbRootBite(2): ImpSgInternalMark
VerbRootBite(1) VZero VerbRootBite(2): ImpSgIR # Dir/LR
VerbRootPlay(1) VZero VerbRootPlay(2): Aor ImpSgIR 
VerbRootPlay(1) VZero VerbRootPlay(2): ThA ImpSg # Dir/LR
VerbRootStay(1) VZero VerbRootStay(2): ThA Aor ImpSgIR
VerbRootStay(1) VZero VerbRootStay(2): ThA ImpSg # Dir/LR
VerbRootSit(1) VZero VerbRootSit(2): ImpSgIR
VerbRootSit(1) VZero VerbRootSit(2): ThI ImpSg # Dir/LR
VerbRootEatPast(1) VZero VerbRootEatPast(2): ImpSgInternalMark
VerbRootSendPast(1) VZero VerbRootSendPast(2): ImpSgInternalMark

#Imperatives pl
VerbRootE(1) VZero VerbRootE(2): ThE Aor ImpPl
VerbRootI(1) VZero VerbRootI(2): ThI Aor ImpPl
VerbRootA(1) VZero VerbRootA(2): ThA Aor ImpPl

CausBase CausPast Aor ImpPl
CausativeVerbRootOrthographicSchwa VZero [<tv>:] CausPast Aor ImpPl
CausativeVerbRootOrthographicAffr VZero [<tv>:] CausOrthographicAffrPast Aor ImpPl

PassBase Pass ThI Aor ImpPl

InchBase InchEmpty Aor ImpPl


VerbRootUv(1)|VerbRootUc(1) VZero VerbRootUv(2):|VerbRootUc(2): ImpPl #Unclear which is preferred
VerbRootUv(1)|VerbRootUc(1) VZero VerbRootUv(2):|VerbRootUc(2): ThE Aor ImpPl
VerbRootUv(1)|VerbRootUc(1) VZero VerbRootUv(2):|VerbRootUc(2): ThU Aor ImpPl
VerbRootUcanel(1) VZero VerbRootUcanel(2): ImpPl  # Dir/LR
VerbRootNulV(1) VZero VerbRootNulV(2): ThU ImpPl
VerbRootNulC(1) EmptyNasal VZero VerbRootNulC(2): ThE Aor ImpPl

VerbRootNil(1) VZero VerbRootNil(2): ImpPl
# VerbRootCilV(1)|VerbRootCilC(1) VZero VerbRootCilV(2):|VerbRootCilC(2): ImpPl
VerbRootNelNale(1)|VerbRootNelNala(1) VZero VerbRootNelNale(2):|VerbRootNelNala(2): ImpPl
VerbRootRiseEPast(1)|VerbRootRiseAPast(1) VZero VerbRootRiseEPast(2):|VerbRootRiseAPast(2): ImpPl
VerbRootCilIlc(1)|VerbRootCilIli(1) VZero VerbRootCilIlc(2):|VerbRootCilIli(2): ImpPl
VerbRootCel(1) VZero VerbRootCel(2): ImpPl
VerbRootCanShort(1) VZero VerbRootCanShort(2): Aor ImpPl
VerbRootBeLongPast(1) VZero VerbRootBeLongPast(2): ImpPl
VerbRootComePast(1) VZero VerbRootComePast(2): ImpPl
VerbRootCry(1) VZero VerbRootCry(2): ThA Aor   ImpPl
VerbRootGivePast(1) VZero VerbRootGivePast(2): ImpPl
VerbRootBring(1) VZero VerbRootBring(2): ImpPl 
VerbRootTurnPast(1) VZero VerbRootTurnPast(2): ImpPl 
VerbRootTake(1) VZero VerbRootTake(2): ImpPl 
VerbRootSeeA(1)|VerbRootSeeE(1) VZero VerbRootSeeA(2):|VerbRootSeeE(2): ImpPl 
VerbRootOpenPast(1) VZero VerbRootOpenPast(2): ImpPl 
VerbRootPutPast(1) VZero VerbRootPutPast(2): ImpPl 
VerbRootGoPastC(1) VZero VerbRootGoPastC(2): ThA Aor ImpPl
VerbRootHitPast(1) VZero VerbRootHitPast(2): ImpPl
VerbRootYield(1) VZero VerbRootYield(2): ImpPl
VerbRootAccept(1) VZero VerbRootAccept(2): ThI Aor ImpPl
VerbRootDoPast(1) VZero VerbRootDoPast(2): ImpPl
VerbRootSay(1) VZero VerbRootSay(2): ImpPl
VerbRootSpitOldPres(1) VZero VerbRootSpitOldPres(2): ImpPl
VerbRootFallPast(1) VZero VerbRootFallPast(2): ImpPl
VerbRootBite(1) VZero VerbRootBite(2): ImpPl
VerbRootPlay(1) VZero VerbRootPlay(2): Aor ImpPl
VerbRootPlay(1) VZero VerbRootPlay(2): ThA Aor ImpPl
VerbRootStay(1) VZero VerbRootStay(2): ThA Aor ImpPl
VerbRootSit(1) VZero VerbRootSit(2): ThI Aor ImpPl
VerbRootSit(1) VZero VerbRootSit(2): ImpPl # Dir/LR
VerbRootEatPast(1) VZero VerbRootEatPast(2): ImpPl
VerbRootSendPast(1) VZero VerbRootSendPast(2): ImpPl

PATTERN Prohibitive
#Prohibitives
VerbRootE(1) VZero VerbRootE(2): ThE [<proh>:] ProhAgr
VerbRootI(1) VZero VerbRootI(2): ThI [<proh>:] ProhAgr
VerbRootA(1) VZero VerbRootA(2): ThA [<proh>:] ProhAgr

CausBase CausPres ThE [<proh>:] ProhAgr
CausativeVerbRootOrthographicSchwa VZero [<tv>:] CausPres ThE [<proh>:] ProhAgr
CausativeVerbRootOrthographicAffr VZero [<tv>:] CausOrthographicAffrPres  ThE [<proh>:] ProhAgr

PassBase Pass ThI [<proh>:] ProhAgr

InchBase InchNasal ThA [<proh>:] ProhAgr

VerbRootUv(1)|VerbRootUc(1) VZero VerbRootUv(2):|VerbRootUc(2): ThU [<proh>:] ProhAgr
#VerbRootUcanel group is defective and lacks prohibitives in Boyacioglu

VerbRootNil(1) EmptyNasal VZero VerbRootNil(2): ThI [<proh>:] ProhAgr
#VerbRootCil(1) EmptyAffr VZero VerbRootCil(2): ThI [<proh>:] ProhAgr #Need to double this accuracy because Boyacioglu didnt have it
VerbRootNelNale(1) EmptyNasal VZero VerbRootNelNale(2): ThE [<proh>:] ProhAgr
VerbRootNelNala(1) EmptyNasal VZero VerbRootNelNala(2): ThA [<proh>:] ProhAgr
VerbRootRiseEPres(1) VZero VerbRootRiseEPres(2): ThE [<proh>:] ProhAgr
VerbRootRiseAPres(1) VZero VerbRootRiseAPres(2): ThA [<proh>:] ProhAgr
VerbRootCilIlc(1) EmptyAffr VZero VerbRootCilIlc(2): ThI [<proh>:] ProhAgr 
VerbRootCilIli(1) VZero VerbRootCilIli(2): ThI [<proh>:] ProhAgr 
VerbRootCel(1) EmptyAffr VZero VerbRootCel(2): ThE [<proh>:] ProhAgr 
VerbRootCanLong(1) EmptyNasal VZero VerbRootCanLong(2): ThA [<proh>:] ProhAgr
VerbRootBeLongPres(1) VZero VerbRootBeLongPres(2): ThA [<proh>:] ProhAgr
VerbRootBeLongPresEastern(1) VZero VerbRootBeLongPresEastern(2): ThE [<proh>:] ProhAgr
VerbRootComePres(1) VZero VerbRootComePres(2): ThA [<proh>:] ProhAgr
VerbRootCry(1) VZero VerbRootCry(2): ThA [<proh>:] ProhAgr
VerbRootGivePres(1) VZero VerbRootGivePres(2): ThA [<proh>:] ProhAgr
VerbRootBring(1) VZero VerbRootBring(2): ThE [<proh>:] ProhAgr
VerbRootTurnPres(1) VZero VerbRootBring(2): ThA [<proh>:] ProhAgr
VerbRootTake(1) EmptyNasal VZero VerbRootTake(2): ThE [<proh>:] ProhAgr 
VerbRootSeeA(1) EmptyNasal VZero VerbRootSeeA(2): ThA [<proh>:] ProhAgr 
VerbRootSeeE(1) EmptyNasal VZero VerbRootSeeE(2): ThE [<proh>:] ProhAgr 
VerbRootOpenPres(1) VZero VerbRootOpenPres(2): ThA [<proh>:] ProhAgr 
VerbRootPutPres(1) VZero VerbRootPutPres(2): ThE [<proh>:] ProhAgr 
VerbRootGoPres(1) VZero VerbRootGoPres(2): ThA [<proh>:] ProhAgr 
VerbRootHitPres(1) VZero VerbRootHitPres(2): ThE [<proh>:] ProhAgr 
VerbRootYield(1) VZero VerbRootYield(2): [<proh>:] ProhAgrIrEk 
VerbRootAccept(1) VZero VerbRootAccept(2): [<proh>:] ProhAgrIrEk 
VerbRootDoPres(1) VZero VerbRootDoPres(2): ThE [<proh>:] ProhAgr 
VerbRootSay(1) VZero VerbRootSay(2): ThE [<proh>:] ProhAgr 
VerbRootSpitOldPres(1) EmptyNasal VZero VerbRootSpitOldPres(2): ThE [<proh>:] ProhAgr 
VerbRootFallPres(1) VZero VerbRootFallPres(2): ThA [<proh>:] ProhAgr 
VerbRootBite(1) EmptyNasal VZero VerbRootBite(2): ThE [<proh>:] ProhAgr 
VerbRootPlay(1) VZero VerbRootPlay(2): ThA [<proh>:] ProhAgr 
VerbRootStay(1) VZero VerbRootStay(2): ThA [<proh>:] ProhAgr 
VerbRootSit(1) VZero VerbRootSit(2): ThI [<proh>:] ProhAgr 
VerbRootEatPres(1) VZero VerbRootEatPres(2): ThE [<proh>:] ProhAgr 
VerbRootSendPres(1) VZero VerbRootSendPres(2): ThI [<proh>:] ProhAgr 

PATTERN SubjectPart
#Subject participle
VerbRootE(1) VZero VerbRootE(2): SubjPart
VerbRootI(1) VZero VerbRootI(2): SubjPart
VerbRootA(1) VZero VerbRootA(2): ThA Aor SubjPart

CausBase CausPres SubjPart
CausativeVerbRootOrthographicSchwa VZero [<tv>:] CausPres SubjPart
CausativeVerbRootOrthographicAffr VZero [<tv>:] CausOrthographicAffrPres SubjPart

PassBase Pass SubjPart

InchBase InchEmpty Aor SubjPart


VerbRootUv(1)|VerbRootUc(1) VZero VerbRootUv(2):|VerbRootUc(2): SubjPart #Unclear which is preferred because obsolete
VerbRootUv(1)|VerbRootUc(1) VZero VerbRootUv(2):|VerbRootUc(2): ThU Aor SubjPart 
VerbRootUcanel(1) VZero VerbRootUcanel(2): ThA EmptyNasal SubjPart  # Dir/LR
VerbRootNulV(1) VZero VerbRootNulV(2): ThU SubjPart
VerbRootNulC(1) VZero VerbRootNulC(2): SubjPart  #Unclear which is preferred because obsolete
VerbRootNulC(1) EmptyNasal VZero VerbRootNulC(2): SubjPart 

VerbRootNil(1) EmptyNasal VZero VerbRootNil(2): SubjPart
#VerbRootCil(1) EmptyAffr VZero VerbRootCil(2): SubjPart #not listed in boyacioglu
VerbRootNelNale(1)|VerbRootNelNala(1) EmptyNasal VZero VerbRootNelNale(2):|VerbRootNelNala(2): SubjPart
VerbRootRiseEPres(1)|VerbRootRiseAPres(1) VZero VerbRootRiseEPres(2):|VerbRootRiseAPres(2): SubjPart
VerbRootCilIlc(1) EmptyAffr VZero VerbRootCilIlc(2): SubjPart
VerbRootCilIli(1) VZero VerbRootCilIli(2): SubjPart
VerbRootCel(1) EmptyAffr VZero VerbRootCel(2): SubjPart
VerbRootCanShort(1) Aor VZero VerbRootCanShort(2):SubjPart
VerbRootBeLongPast(1) VZero VerbRootBeLongPast(2): SubjPart
VerbRootComePast(1) VZero VerbRootComePast(2): SubjPart
VerbRootCry(1) VZero VerbRootCry(2): ThA Aor SubjPart
VerbRootGivePast(1) VZero VerbRootGivePast(2): SubjPart
VerbRootBring(1) VZero VerbRootBring(2): SubjPart
VerbRootTurnPast(1) VZero VerbRootTurnPast(2): SubjPart
VerbRootTake(1) EmptyNasal VZero VerbRootTake(2): SubjPart
VerbRootSeeA(1)|VerbRootSeeE(1) EmptyNasal VZero VerbRootSeeA(2):|VerbRootSeeE(2): SubjPart
VerbRootOpenPast(1) VZero VerbRootOpenPast(2): SubjPart
VerbRootPutPres(1) VZero VerbRootPutPres(2): SubjPart
VerbRootGoPastC(1) VZero VerbRootGoPastC(2): ThA Aor SubjPart
VerbRootHitPres(1) VZero VerbRootHitPres(2): SubjPart
VerbRootYield(1) VZero VerbRootYield(2): SubjPart
VerbRootAccept(1) VZero VerbRootAccept(2): SubjPart
VerbRootDoPres(1) VZero VerbRootDoPres(2): SubjPart
VerbRootSay(1) VZero VerbRootSay(2): SubjPart
VerbRootSpitOldPres(1) EmptyNasal VZero VerbRootSpitOldPres(2): SubjPart
VerbRootFallPast(1) VZero VerbRootFallPast(2): SubjPart
VerbRootFallPres(1) VZero VerbRootFallPres(2): SubjPart # Dir/LR
VerbRootBite(1) EmptyNasal VZero VerbRootBite(2): SubjPart
VerbRootPlay(1) VZero VerbRootPlay(2): Aor SubjPart
VerbRootStay(1) VZero VerbRootStay(2): ThA Aor SubjPart
VerbRootSit(1) VZero VerbRootSit(2): SubjPart
VerbRootEatPres(1) VZero VerbRootEatPres(2): SubjPart
VerbRootSendPres(1) VZero VerbRootSendPres(2): SubjPart

PATTERN PerfectPart
#Perfect participle
VerbRootE(1) VZero VerbRootE(2): PerfPart
VerbRootI(1) VZero VerbRootI(2): PerfPart
VerbRootA(1) VZero VerbRootA(2): ThA Aor PerfPart

CausBase CausPast Aor PerfPart
CausativeVerbRootOrthographicSchwa VZero [<tv>:] CausPast Aor PerfPart
CausativeVerbRootOrthographicAffr VZero [<tv>:] CausOrthographicAffrPast Aor PerfPart

### Arak had հարցուցուած as perfect participle of passivized causative, even though also had հարցուիլ
### Just to be safe, I optionally insert the past+aorist for this construction
## TO DO: Clarify from Arak29 if this is a glitch
CausBase CausPast Aor Pass PerfPart   # Dir/LR
CausativeVerbRootOrthographicSchwa VZero [<tv>:] CausPast Aor Pass PerfPart   # Dir/LR
CausativeVerbRootOrthographicAffr VZero [<tv>:] CausOrthographicAffrPast Pass Aor PerfPart   # Dir/LR



PassBase Pass PerfPart

InchBase InchEmpty Aor PerfPart


VerbRootUv(1)|VerbRootUc(1) VZero VerbRootUv(2):|VerbRootUc(2): PerfPart #Unclear which is preferred because obsolete
VerbRootUv(1)|VerbRootUc(1) VZero VerbRootUv(2):|VerbRootUc(2): ThU Aor PerfPart 
VerbRootUcanel(1) VZero VerbRootUcanel(2): PerfPart  # Dir/LR
VerbRootNulV(1) VZero VerbRootNulV(2): ThU PerfPart
VerbRootNulC(1) VZero VerbRootNulC(2): PerfPart   

VerbRootNil(1) VZero VerbRootNil(2): PerfPart
# VerbRootCilV(1)|VerbRootCilC(1) VZero VerbRootCilV(2):|VerbRootCilC(2): PerfPart
VerbRootNelNale(1)|VerbRootNelNala(1) VZero VerbRootNelNale(2):|VerbRootNelNala(2): PerfPart
VerbRootRiseEPast(1)|VerbRootRiseAPast(1) VZero VerbRootRiseEPast(2):|VerbRootRiseAPast(2): PerfPart
VerbRootCilIlc(1)|VerbRootCilIli(1) VZero VerbRootCilIlc(2):|VerbRootCilIli(2): PerfPart
VerbRootCel(1) VZero VerbRootCel(2): PerfPart
VerbRootCanShort(1) Aor VZero VerbRootCanShort(2):PerfPart
VerbRootBeLongPast(1) VZero VerbRootBeLongPast(2): PerfPart
VerbRootComePast(1) VZero VerbRootComePast(2): PerfPart
VerbRootCry(1) VZero VerbRootCry(2): ThA Aor   PerfPart
VerbRootGivePast(1) VZero VerbRootGivePast(2): PerfPart
VerbRootBring(1) VZero VerbRootBring(2): PerfPart
VerbRootTurnPast(1) VZero VerbRootTurnPast(2): PerfPart
VerbRootTake(1) VZero VerbRootTake(2): PerfPart
VerbRootSeeA(1)|VerbRootSeeE(1) VZero VerbRootSeeA(2):|VerbRootSeeE(2): PerfPart
VerbRootOpenPast(1) VZero VerbRootOpenPast(2): PerfPart
VerbRootPutPast(1) VZero VerbRootPutPast(2): PerfPart
VerbRootGoPastC(1) VZero VerbRootGoPastC(2): ThA Aor PerfPart
VerbRootHitPast(1) VZero VerbRootHitPast(2): PerfPart
VerbRootYield(1) VZero VerbRootYield(2): PerfPart
VerbRootAccept(1) VZero VerbRootAccept(2): PerfPart
VerbRootDoPast(1) VZero VerbRootDoPast(2): PerfPart
VerbRootSay(1) VZero VerbRootSay(2): PerfPart
VerbRootSpitOldPres(1) VZero VerbRootSpitOldPres(2): PerfPart
VerbRootFallPast(1) VZero VerbRootFallPast(2): PerfPart
VerbRootBite(1) VZero VerbRootBite(2): PerfPart
VerbRootPlay(1) VZero VerbRootPlay(2): Aor PerfPart
VerbRootStay(1) VZero VerbRootStay(2): ThA Aor PerfPart
VerbRootSit(1) VZero VerbRootSit(2): PerfPart
VerbRootEatPast(1) VZero VerbRootEatPast(2): PerfPart
VerbRootSendPast(1) VZero VerbRootSendPast(2): PerfPart

PATTERN EvidentialPart
#Evidential participle
VerbRootE(1) VZero VerbRootE(2): EvidPart
VerbRootI(1) VZero VerbRootI(2): EvidPart
VerbRootA(1) VZero VerbRootA(2): ThA Aor EvidPart

CausBase CausPast Aor EvidPart
CausativeVerbRootOrthographicSchwa VZero [<tv>:] CausPast Aor EvidPart
CausativeVerbRootOrthographicAffr VZero [<tv>:] CausOrthographicAffrPast Aor EvidPart

PassBase Pass EvidPart

InchBase InchEmpty Aor EvidPart

VerbRootUv(1)|VerbRootUc(1) VZero VerbRootUv(2):|VerbRootUc(2): EvidPart #Unclear which is preferred because obsolete
VerbRootUv(1)|VerbRootUc(1) VZero VerbRootUv(2):|VerbRootUc(2): ThU Aor EvidPart 
VerbRootUcanel(1) VZero VerbRootUcanel(2): EvidPart  # Dir/LR
VerbRootNulV(1) VZero VerbRootNulV(2): ThU EvidPart
VerbRootNulC(1) VZero VerbRootNulC(2): ThU EvidPart   


VerbRootNil(1) VZero VerbRootNil(2): EvidPart
#VerbRootCil(1) VZero VerbRootCil(2): EvidPart #not listed in boyacioglu
VerbRootNelNale(1)|VerbRootNelNala(1) VZero VerbRootNelNale(2):|VerbRootNelNala(2): EvidPart
VerbRootRiseEPast(1)|VerbRootRiseAPast(1) VZero VerbRootRiseEPast(2):|VerbRootRiseAPast(2): EvidPart
VerbRootCilIlc(1)|VerbRootCilIli(1) VZero VerbRootCilIlc(2):|VerbRootCilIli(2): EvidPart
VerbRootCel(1) VZero VerbRootCel(2): EvidPart
VerbRootCanShort(1) VZero VerbRootCanShort(2): Aor EvidPart
VerbRootBeLongPast(1) VZero VerbRootBeLongPast(2): EvidPart
VerbRootComePast(1) VZero VerbRootComePast(2): EvidPart
VerbRootCry(1) VZero VerbRootCry(2): ThA Aor EvidPart
VerbRootGivePast(1) VZero VerbRootGivePast(2): EvidPart
VerbRootBring(1) VZero VerbRootBring(2): EvidPart
VerbRootTurnPast(1) VZero VerbRootTurnPast(2): EvidPart
VerbRootTake(1) VZero VerbRootTake(2): EvidPart
VerbRootSeeA(1)|VerbRootSeeE(1) VZero VerbRootSeeA(2):|VerbRootSeeE(2): EvidPart
VerbRootOpenPast(1) VZero VerbRootOpenPast(2): EvidPart
VerbRootPutPast(1) VZero VerbRootPutPast(2): EvidPart
VerbRootGoPastC(1) VZero VerbRootGoPastC(2): ThA Aor EvidPart
VerbRootHitPast(1) VZero VerbRootHitPast(2): EvidPart
VerbRootYield(1) VZero VerbRootYield(2): EvidPart
VerbRootAccept(1) VZero VerbRootAccept(2): EvidPart
VerbRootDoPast(1) VZero VerbRootDoPast(2): EvidPart
VerbRootSay(1) VZero VerbRootSay(2): EvidPart
VerbRootSpitOldPres(1) VZero VerbRootSpitOldPres(2): EvidPart
VerbRootFallPast(1) VZero VerbRootFallPast(2): EvidPart
VerbRootBite(1) VZero VerbRootBite(2): EvidPart
VerbRootPlay(1) VZero VerbRootPlay(2): Aor EvidPart
VerbRootStay(1) VZero VerbRootStay(2): ThA Aor EvidPart
VerbRootSit(1) VZero VerbRootSit(2): EvidPart
VerbRootEatPast(1) VZero VerbRootEatPast(2): EvidPart
VerbRootSendPast(1) VZero VerbRootSendPast(2): EvidPart

PATTERN NegativePart
#Negative participle
#Indicatives (negative)
VerbRootE(1) VZero VerbRootE(2): ThE Pres0|PastImpf0 NegPtcp 
VerbRootI(1) VZero VerbRootI(2): ThI Pres0 NegPtcp 
VerbRootI(1) VZero VerbRootI(2): ThE PastImpf0 NegPtcp #For i->e
VerbRootA(1) VZero VerbRootA(2): ThA Pres0|PastImpf0 NegPtcp 

CausBase CausPres ThE Pres0|PastImpf0 NegPtcp 
CausativeVerbRootOrthographicSchwa VZero [<tv>:] CausPres ThE Pres0|PastImpf0 NegPtcp 
CausativeVerbRootOrthographicAffr VZero [<tv>:] CausOrthographicAffrPres ThE Pres0|PastImpf0 NegPtcp 

PassBase Pass ThI Pres0 NegPtcp 
PassBase Pass ThE PastImpf0 NegPtcp #For i->e

InchBase InchNasal ThA Pres0|PastImpf0 NegPtcp 

VerbRootUv(1) VZero VerbRootUv(2): ThE Pres0|PastImpf0 NegPtcp 
VerbRootUc(1) VZero VerbRootUc(2): ThE|ThU Pres0|PastImpf0 NegPtcp 
VerbRootUcanel(1) VZero VerbRootUcanel(2): ThE Pres0|PastImpf0 NegPtcp   # Dir/LR
VerbRootNulV(1) VZero VerbRootNulV(2): ThU ThE Pres0 NegPtcp 
VerbRootNulC(1) VZero VerbRootNulC(2): ThE Pres0 NegPtcp 


VerbRootNil(1) EmptyNasal VZero VerbRootNil(2): ThI Pres0 NegPtcp 
VerbRootNil(1) EmptyNasal VZero VerbRootNil(2): ThE PastImpf0 NegPtcp #For i->e
#VerbRootCil(1) EmptyAffr VZero VerbRootCil(2): ThI Pres0 NegPtcp  #not listed in boyacioglu
#VerbRootCil(1) EmptyAffr VZero VerbRootCil(2): ThE PastImpf0 NegPtcp #For i->e
VerbRootNelNale(1) EmptyNasal VZero VerbRootNelNale(2): ThE Pres0|PastImpf0 NegPtcp 
VerbRootNelNala(1) EmptyNasal VZero VerbRootNelNala(2): ThA Pres0|PastImpf0 NegPtcp 
VerbRootRiseEPres(1) VZero VerbRootRiseEPres(2): ThE Pres0|PastImpf0 NegPtcp 
VerbRootRiseAPres(1) VZero VerbRootRiseAPres(2): ThA Pres0|PastImpf0 NegPtcp 
VerbRootCilIlc(1) EmptyAffr VZero VerbRootCilIlc(2): ThI Pres0 NegPtcp 
VerbRootCilIli(1) VZero VerbRootCilIli(2): ThI Pres0 NegPtcp 
VerbRootCilIlc(1) EmptyAffr VZero VerbRootCilIlc(2): ThE PastImpf0 NegPtcp #For i->e
VerbRootCilIli(1) VZero VerbRootCilIli(2): ThE PastImpf0 NegPtcp #For i->e
VerbRootCel(1) EmptyAffr VZero VerbRootCel(2): ThE Pres0|PastImpf0 NegPtcp 
VerbRootCanShort(1) EmptyNasal VZero VerbRootCanShort(2): ThA Pres0|PastImpf0 NegPtcp 
VerbRootCanLong(1) EmptyNasal VZero VerbRootCanLong(2): ThA Pres0|PastImpf0 NegPtcp 
VerbRootAppear(1) VZero VerbRootAppear(2): ThI Pres0 NegPtcp 
VerbRootAppear(1) VZero VerbRootAppear(2): ThE PastImpf0 NegPtcp #For i->e
VerbRootKnow(1) VZero VerbRootKnow(2): ThE Pres0|PastImpf0 NegPtcp 
VerbRootBeLongPres(1) VZero VerbRootBeLongPres(2): ThA Pres0|PastImpf0 NegPtcp 
VerbRootBeLongPresEastern(1) VZero VerbRootBeLongPresEastern(2): ThE Pres0|PastImpf0 NegPtcp 
VerbRootComePres(1) VZero VerbRootComePres(2): ThA Pres0|PastImpf0 NegPtcp 
VerbRootCry(1) VZero VerbRootCry(2): ThA Pres0|PastImpf0 NegPtcp 
VerbRootGivePres(1) VZero VerbRootGivePres(2): ThA Pres0|PastImpf0 NegPtcp 
VerbRootBring(1) VZero VerbRootBring(2): ThE Pres0|PastImpf0 NegPtcp 
VerbRootTurnPres(1) VZero VerbRootBring(2): ThA Pres0|PastImpf0 NegPtcp 
VerbRootTake(1) EmptyNasal VZero VerbRootTake(2): ThE Pres0|PastImpf0 NegPtcp 
VerbRootSeeA(1) EmptyNasal VZero VerbRootSeeA(2): ThA Pres0|PastImpf0 NegPtcp 
VerbRootSeeE(1) EmptyNasal VZero VerbRootSeeE(2): ThE Pres0|PastImpf0 NegPtcp 
VerbRootOpenPres(1) VZero VerbRootOpenPres(2): ThA Pres0|PastImpf0 NegPtcp 
VerbRootPutPres(1) VZero VerbRootPutPres(2): ThE Pres0|PastImpf0 NegPtcp 
VerbRootGoPres(1) VZero VerbRootGoPres(2): ThA Pres0|PastImpf0 NegPtcp 
VerbRootHitPres(1) VZero VerbRootHitPres(2): ThE Pres0|PastImpf0 NegPtcp 
VerbRootYield(1) VZero VerbRootYield(2): ThI Pres0 NegPtcp 
VerbRootYield(1) VZero VerbRootYield(2): ThE PastImpf0 NegPtcp #For i->e
VerbRootAccept(1) VZero VerbRootAccept(2): ThI Pres0 NegPtcp 
VerbRootAccept(1) VZero VerbRootAccept(2): ThE PastImpf0 NegPtcp #For i->e
VerbRootDoPres(1) VZero VerbRootDoPres(2): ThE Pres0|PastImpf0 NegPtcp 
VerbRootSay(1) VZero VerbRootSay(2): ThE Pres0|PastImpf0 NegPtcp 
VerbRootSpitOldPres(1) VZero VerbRootSpitOldPres(2): ThE Pres0|PastImpf0 NegPtcp 
VerbRootFallPres(1) VZero VerbRootFallPres(2): ThA Pres0|PastImpf0 NegPtcp 
VerbRootBite(1) EmptyNasal VZero VerbRootBite(2): ThE Pres0|PastImpf0 NegPtcp 
VerbRootPlay(1) VZero VerbRootPlay(2): ThA Pres0|PastImpf0 NegPtcp 
VerbRootStay(1) VZero VerbRootStay(2): ThA Pres0|PastImpf0 NegPtcp
VerbRootSit(1) VZero VerbRootSit(2): ThI Pres0 NegPtcp 
VerbRootSit(1) VZero VerbRootSit(2): ThE PastImpf0 NegPtcp #For i->e
VerbRootEatPres(1) VZero VerbRootEatPres(2): ThE Pres0|PastImpf0 NegPtcp
VerbRootSendPres(1) VZero VerbRootSendPres(2): ThI Pres0 NegPtcp 
VerbRootSendPres(1) VZero VerbRootSendPres(2): ThE PastImpf0 NegPtcp #For i->e
 

PATTERN ProspectivePart #Also called future gerund
Infinitival ProsPtcp

PATTERN FuturePart 
Infinitival FutPtcp

PATTERN NegatedChunks
#whatever verbal paradigms that are negated by just adding the neg- prefix
:Neg Infinitival Neg:
:Neg Subjunctive Neg:
:Neg Past Neg:
:Neg SubjectPart Neg:
:Neg PerfectPart Neg:
:Neg ProspectivePart Neg:
:Neg FuturePart Neg:



#Bucket
PATTERNS
Infinitival
Indicative 
Subjunctive 
Past 
Imperative 
Proh [:\ ] Prohibitive
Prohibitive # Dir/LR 
SubjectPart
PerfectPart 
EvidentialPart 
ProspectivePart 
FuturePart 
NegativePart
:Neg NegativePart Neg: # Dir/LR because sometimes see reduction of 3SG չի/չ՚
NegatedChunks 

#Auxiliary 
Aux Pres0 PresAgr Indc0
:Neg Aux  Pres0 PresAgr Indc0 Neg:
Aux ImpfAgr Indc0
:Neg Aux  ImpfAgr Indc0 Neg:

#the special ci- auxiliary in negated present indicdative 3sg
SyntheticNegAux3SGI

#Irregulars that have synthetic indicative negative
:Neg VerbRootHave(1) VZero VerbRootHave(2): ThI Pres0 PresAgr Indc: Neg: 
:Neg VerbRootHave(1) VZero VerbRootHave(2): ThI ImpfAgr Indc: Neg: 
:Neg VerbRootExist(1) VZero VerbRootExist(2): ThA Pres0 PresAgr Indc:  Neg:
:Neg VerbRootExist(1) VZero VerbRootExist(2): ThA ImpfAgr Indc: Neg: 

#Particles
Fut
Prog
Proh
IndcLong



#####################
#####################
###### Verb morphemes 
#####################
#####################

LEXICON Aux # Class 32
է<vaux>:ե

LEXICON SyntheticNegAux3SGI 
<synth_vaux><pres><p3><sg><neg>:չի
<synth_vaux><pres><p3><sg><neg>:չ՚


LEXICON VZero
<v>:

LEXICON Pres0
<pres>:
LEXICON PastImpf0
<past><impf>:


LEXICON Indc0
<indc>:

LEXICON Subj0
<subj>:


LEXICON ThE
:ե
LEXICON ThI
:ի
LEXICON ThA
:ա{ThAյ}
LEXICON ThU
:ու



LEXICON Inf
<ger>:{լ} #To trigger i->e raising in derived words

#Valency affixes
#Allomorphs of causative
LEXICON CausPres
<caus>:ցն
<caus>:ցըն # Dir/LR in case theres orthographic variation for some causative

LEXICON CausPast
<caus>:ցու

LEXICON CausPass
<caus>:ց

#Orthographic variants of causative
LEXICON CausOrthographicSchwaPres
<caus>:ցըն
LEXICON CausOrthographicAffrPres
<caus>:ձն
LEXICON CausOrthographicAffrPast
<caus>:ձու
LEXICON CausOrthographicAffrPass
<caus>:ձ

LEXICON Pass
<pass>:ու

LEXICON InchNasal
<inch>:ն

LEXICON InchEmpty #This is when the inchoative nasal is absent
<inch>:


#Empty affixes
LEXICON EmptyNasal
:ն
LEXICON EmptyAffr
:չ


LEXICON PresAgr
<p1><sg>:մ
<p2><sg>:ս
<p3><sg>:{Pres3SG} #was {է}
<p1><pl>:նք
<p2><pl>:{Pres2PL}ք # was {է}
<p3><pl>:ն

LEXICON ImpfAgr
<past><impf><p1><sg>:{Impf}ի #was {եէ}
<past><impf><p2><sg>:{Impf}իր
<past><impf><p3><sg>:{Impf}ր
<past><impf><p1><pl>:{Impf}ինք
<past><impf><p2><pl>:{Impf}իք
<past><impf><p3><pl>:{Impf}ին

 
LEXICON ProsPtcp
<pros_ptcp>:իք

LEXICON FutPtcp
<fut_ptcp>:ու


LEXICON Aor
<aor>:{Aor}ց #was {ե}

LEXICON PastPrimAgr
<past><pret><p1><sg>:ի
<past><pret><p2><sg>:իր
<past><pret><p3><sg>:
<past><pret><p1><pl>:ինք
<past><pret><p2><pl>:իք
<past><pret><p3><pl>:ին

LEXICON PastSecAgr
<past><pret><p1><sg>:այ
<past><pret><p2><sg>:ար
<past><pret><p3><sg>:աւ
<past><pret><p1><pl>:անք
<past><pret><p2><pl>:աք
<past><pret><p3><pl>:ան

LEXICON PastSplitAgr #For լացի լացաւ
<past><pret><p1><sg>:ի
<past><pret><p2><sg>:իր
<past><pret><p3><sg>:աւ
<past><pret><p1><pl>:ինք
<past><pret><p2><pl>:իք
<past><pret><p3><pl>:ին

LEXICON PastAgrGoNo3Sg #For գացի գնաց
<past><pret><p1><sg>:ի
<past><pret><p2><sg>:իր
<past><pret><p1><pl>:ինք
<past><pret><p2><pl>:իք
<past><pret><p3><pl>:ին
LEXICON PastAgrGoYes3Sg #For գացի գնաց
<past><pret><p3><sg>:


#TO DO make a post-gen/constraint grammar to demand that imperative keep exclamation marker
LEXICON ImpSg
<imp><p2><sg>:{ImpSg} #was {եէ} # had ՛
LEXICON ImpSgR 
<imp><p2><sg>:ր #Don't add the {ImpSg} tag so that don't trigger i->e change, had ՛
LEXICON ImpSgIR 
<imp><p2><sg>:իր # had ՛ 
LEXICON ImpSgUR 
<imp><p2><sg>:ուր # had ՛ 


LEXICON ImpSgInternalMark #Mark is added at end of verb; will later metathesize
<imp><p2><sg>: # had ՛

 
LEXICON ImpPl
<imp><p2><pl>:էք # had ՛

LEXICON ProhAgr
<p2><sg>:ր
<p2><pl>:{ProhPl}ք #Was {է}

LEXICON ProhAgrIrEk #For մի զիջիր մի զիջէք
<p2><sg>:իր
<p2><pl>:էք 

LEXICON Indc
<indc>:կ{G1}{G2}
LEXICON IndcShort
<indc>:կու\ 
LEXICON  IndcLong
#Place this to help for parsing unrecognized verbs
<indc>:կը


LEXICON Proh
մի<pro><proh>:մի #had ՛

LEXICON Prog
կոր<enc><prog>:կոր

LEXICON Fut
պիտի<pro><fut>:պիտի
պիտ<pro><fut>:պիտ # Dir/LR

#Negation-based lexicons
LEXICON NegPtcp
<neg_ptcp>:ր



LEXICON Neg
<neg>:չ

LEXICON SubjPart
<pprs>:ող

LEXICON PerfPart
<perf>:ած

LEXICON EvidPart
<evid>:եր



















###############################
### Lexicon of Verbs

## This is now all imported into the lexicon files
## I keep the lexicon-names here for easier fact-checking

#Simple regular verbs
LEXICON VerbRootE(2) 
երգել:երգ <tv>
քաղել:քաղ <tv>
ուսուցանել:ուսուցան <tv>
ծանուցանել:ծանուցան <tv>

LEXICON VerbRootI(2)
ապրիլ:ապր <iv>
խօսիլ:խօս <iv_tv>
ուսանիլ:ուսան <tv>
սնանիլ:սնան <iv>

LEXICON VerbRootA(2)
աղալ:աղ <tv>
կարդալ:կարդ <iv_tv>

#Derived verbs
LEXICON CausativeVerbRoot
աճեցնել:աճե
մօտեցնել:մօտե

LEXICON CausativeVerbRootOrthographicSchwa
անցընել:ան 
LEXICON CausativeVerbRootOrthographicAffr
դարձնել:դար
վերադարձնել:վերադար



LEXICON PassiveVerbRoot
ածիլուիլ:ածիլ
բուժուիլ:բուժ

#TO DO Allow a system to add nouns and adjectives
#I used to divide inchoatives into groups based on theme vowel
#But it's not useful so collapsing
LEXICON InchRoot(2)
#LEXICON InchRootTh0(2)
ամչնալ:ամչ <iv>
նիհարնալ:նիհար <iv>
#LEXICON InchRootThA(2)
ստանալ:ստա <tv>
#LEXICON InchRootThE(2)
մօտենալ:մօտե <iv>

##############
#Obsolete verbs
LEXICON VerbRootUv(2)  # class 7 8
արտահեղուլ:արտահեղ <iv>
LEXICON VerbRootUc(2)
թողուլ:թող <tv>

LEXICON VerbRootUcanel(2)
# class 11 12
#All of its entries are preferably part of class 1 & 2
ուսուցանել:ուսուց <tv>
ծանուցանել:ծանուց <tv>

LEXICON VerbRootNulV(2) # Class 21 22
երդնուլ:երդ <iv>
LEXICON VerbRootNulC(2)
զբօսնուլ:զբօս <iv>


###########
#Irregulars and defective verbs
LEXICON VerbRootNil(2) # Class 13 14
անցնիլ:անց <iv>
մեռնիլ:մեռ <iv>

#These two classes are defective in Boyacioglu, but conjugated the same (for me) as class 26
#LEXICON VerbRootCilV # Class 15 16
#երկնչիլ:երկն
#LEXICON VerbRootCilC
#կպչիլ:կպ

LEXICON VerbRootNelNale(2) # Class 23 24
իջնել:իջ <iv>
գտնել:գտ <tv>
դպնել:դպ <tv> #This verb is also CilIl
LEXICON VerbRootNelNala(2)
իջնալ:իջ <iv>
գտնալ:գտ <tv>
դպնալ:դպ <tv> #This verb is also CilIl

LEXICON VerbRootSeeE(2) #Incorrectly part of գտնել group but it has bare imperative 2sg like առնել
տեսնել:տես <tv>
LEXICON VerbRootSeeA(2) #Incorrectly part of գտնել group but it has bare imperative 2sg like առնել
տեսնալ:տես <tv>


LEXICON VerbRootRiseEPres(2) # Class 25
ելլել:ելլ	<iv>
LEXICON VerbRootRiseEPast(2)
ելլել:ել	<iv>
LEXICON VerbRootRiseAPres(2)
ելլալ:ելլ	<iv>
LEXICON VerbRootRiseAPast(2)
ելլալ:ել	<iv>

LEXICON VerbRootCilIlc(2) # Class 26
դպչիլ:դպ <tv>
LEXICON VerbRootCilIli(2)
դպիլ:դպ <tv>

### Added this for the lone irregular words which are չել instead of չիլ
LEXICON VerbRootCel(2)
թռչել:թռ <iv>

LEXICON VerbRootCanShort(2)
կրնամ:կր <iv> 
#Apparently կրնալ exists, though mostly as negated form չկրնալով
LEXICON VerbRootCanLong(2)
կարենալ:կարե <iv>
LEXICON VerbRootAppear(2) #Class 28 երեւիլ, has -nal version that's regular
երեւիլ:երեւ <iv>


LEXICON VerbRootHaveE(2) # Class 29 ունիմ, has -enal version that's regular
#Has infinitival form in Kouyoumdjian grammar ունել
ունել:ուն <tv>
LEXICON VerbRootHaveI(2)  # There is also rare infinitval ունիլ
ունիլ:ուն <tv>
ունիմ:ուն <tv>
LEXICON  VerbRootHave(2)
#To merge the infinitivals ունիլ ունել for conjugations besides the infinitival
ունել:ուն <tv>
ունիլ:ուն <tv>
ունիմ:ուն <tv>


LEXICON VerbRootKnow(2) # Class 30 գիտեմ, has -nal version that's regular
#Has infinitival form in Kouyoumdjian grammar
գիտել:գիտ <tv>
գիտեմ:գիտ <tv>

LEXICON VerbRootExist(2) # Class 31 կամ 
կամ:կ <iv>
LEXICON VerbRootBeLongPres(2) # Class 33 ըլլալ եղայ
ըլլալ:ըլլ <iv>
LEXICON VerbRootBeLongPresEastern(2) # Eastern form
լինել:լին <iv>
LEXICON VerbRootBeLongPast(2)
լինել:եղ <iv>
ըլլալ:եղ <iv>
LEXICON VerbRootComePres(2) # Class 34 գալ եկայ
գալ:գ <iv>
LEXICON VerbRootComePast(2)
գալ:եկ <iv>
LEXICON VerbRootCry(2) # Class 35 լալ
լալ:լ <iv>
LEXICON VerbRootGivePres(2) # Class 36 տալ տուի
տալ:տ <tv>
LEXICON VerbRootGivePast(2)
տալ:տու <tv>
LEXICON VerbRootBring(2)  #Class 37 38 արտաբերել բերել
բերել:բեր <tv>
արտաբերել:արտաբեր <tv>
LEXICON VerbRootTurnPres(2)
դառնալ:դառն <iv>
անդրադառնալ:անդրադառն <tv>
վերադառնալ:վերադառն <iv>
ամբառնալ:ամբառն <iv_tv> # Dir/LR  optionally irregular
բառնալ:բառն <tv> # Dir/LR  optionally irregular
համբառնալ:համբառն <iv_tv> # Dir/LR  optionally irregular
LEXICON VerbRootTurnPast(2)
դառնալ:դարձ <iv>
անդրադառնալ:անդրադարձ <tv>
վերադառնալ:վերադարձ <iv>
ամբառնալ:ամբարձ <iv_tv> # Dir/LR  optionally irregular
բառնալ:բարձ <tv> # Dir/LR  optionally irregular
համբառնալ:համբարձ <iv_tv> # Dir/LR  optionally irregular
LEXICON VerbRootTake(2)
առնել:առ <tv>
LEXICON VerbRootOpenPres(2)
բանալ:բան <tv>
LEXICON VerbRootOpenPast(2)
բանալ:բաց <tv>
LEXICON VerbRootPutPres(2)
դնել:դն <tv>
LEXICON VerbRootPutPast(2)
դնել:դր <tv>
LEXICON VerbRootPutImpSg(2)
դնել:դ <tv>
LEXICON VerbRootGoPres(2)
երթալ:երթ <iv>
LEXICON VerbRootGoPastC(2)
երթալ:գ <iv>
LEXICON VerbRootGoPast3(2)
երթալ:գն <iv>
LEXICON VerbRootHitPres(2)
զարնել:զարն <tv>
LEXICON VerbRootHitPast(2)
զարնել:զարկ <tv>
LEXICON VerbRootYield(2) # Class 47 զիջիմ զիջայ մի զիջիր մի զիջէք
զիջիլ:զիջ <iv>
կարեկցիլ:կարեկց <iv>
LEXICON VerbRootAccept(2) # Class 48 ընդունիմ ընդունեցի ընդունեցայ մի ընդունիր մի ընդունէք
ընդունիլ:ընդուն <iv_tv>
LEXICON VerbRootDoPres(2) # Class 49 ընել
ընել:ըն <tv>
LEXICON VerbRootDoPast(2) # ընել
ընել:ըր <tv>
LEXICON VerbRootSay(2) # Class 50 ըսել
ըսել:ըս <tv>
LEXICON VerbRootSpitOldPres(2) # Class 51 թքնել
թքնել:թք <iv>
LEXICON VerbRootSpitOldImpSg(2)
թքնել:թուք <iv>
LEXICON VerbRootFallPres(2) # Class 52 իյնալ
իյնալ:իյն <iv>
LEXICON VerbRootFallPast(2)
իյնալ:ինկ <iv>
իյնալ:ընկ <iv> # Dir/LR
LEXICON VerbRootBite(2)
խածնել:խած <tv>
պագնել:պագ <tv>
LEXICON VerbRootPlay(2) # Class 54 խաղալ խաղցայ խաղացի
խաղալ:խաղ <tv>
LEXICON VerbRootStay(2) # Class 55 մնալ
մնալ:մն <iv>
ջանալ:ջան <iv>
սպառնալ:սպառն <iv>
LEXICON VerbRootSit(2) # Class 56 նստիլ
նստիլ:նստ  <iv>
LEXICON VerbRootEatPres(2) # Class 57 ուտել կերայ
ուտել:ուտ <tv>
LEXICON VerbRootEatPast(2)
ուտել:կեր <tv>
LEXICON VerbRootSendPres(2) # Class 58 տանիլ տարաւ
տանիլ:տան <tv>
LEXICON VerbRootSendPast(2)
տանիլ:տար <tv>

#! Resources:
#! https://wiki.apertium.org/wiki/Starting_a_new_language_with_HFST#Lexicon
#! https://wiki.apertium.org/wiki/Lttoolbox_and_lexc
#! https://wiki.apertium.org/wiki/Apertium-specific_conventions_for_lexc
#! https://kitwiki.csc.fi/twiki/bin/view/KitWiki/HfstHome
#! https://github.com/hfst/
#! http://www.cis.upenn.edu/~cis639/docs/lexc.html
#! https://wiki.apertium.org/wiki/Hfst



