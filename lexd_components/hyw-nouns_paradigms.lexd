
#!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
#!!!  M O R P H O T A C T I C S  !!!
#!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

# I don't use zero affixes for nominative or accusative
# I don't separate syncretic genitive and dative
# Not making եան declension because obsolete
#Not making թեանց class or նեաց class From Hagopian grammar

########################################
########################################
########################################
### NOUN PARADIGMS
########################################
########################################
########################################
########################################

#TO DO: fancy reduced words like սիրով անուան
# TO DO: vowel reduction
#TO DO: remove repetition in output like ամիսը by reorganizing declensions based on Number

#All the main declension systems
PATTERNS
NounDeclRegular
NounDeclRisingClusters
InfinitivalDecl
AdjAdvDeclRegular
NounOtherPOSDeclRegular
NameDecl

#suffix-based declensions
NounDeclUtjunSuffix
NounDeclUmSuffix

#Full-declension patterns
NounDeclIrregMonoWithN
NounDeclRegularU
NounDeclIrregularTimeVan
NounDeclOldOch
NounDeclOldFamilyOAblaut

#Dative only patterns
NounDeclOldAnForm
NounDeclFamilyNkForm
NounDeclOldRandomOAblaut
NounDeclCountryOj

#Irregular words
NounDeclIrregReduce
NounDeclIrregReducePoly

#IrregularBucket
IrregularBucket (Def|Poss)?

PATTERN NounDeclRegular
#Singular
NounReg NZero NouNinflBoundary NSg0 CaseRegI? (Def|Poss)? 
NounReg NZero NouNinflBoundaryRed NSg0 CaseRegI? (Def|Poss)?  # Dir/LR
#Plural
NounReg NZero NouNinflBoundary NPlReg CaseRegU? (Def|Poss)? 
NounReg NZero NouNinflBoundaryRed NPlReg CaseRegU? (Def|Poss)?  # Dir/LR
#Plural possessive (separate line for instrumental)
NounReg NZero NouNinflBoundary NSg0|NPlReg  PossPl CaseRegPostPluralPoss Def|Poss
NounReg NZero NouNinflBoundaryRed NSg0|NPlReg  PossPl CaseRegPostPluralPoss Def|Poss  # Dir/LR
NounReg NZero NouNinflBoundary NSg0|NPlReg  InsReg PossPl  Def|Poss
NounReg NZero NouNinflBoundaryRed NSg0|NPlReg  InsReg PossPl  Def|Poss  # Dir/LR


PATTERN NounDeclRegularU
#This is for verbs that take ու in the genitive/dative
#This don't have high vowels that can reduce
#Singular
NounIrregU NZero NouNinflBoundary NSg0 CaseRegU? (Def|Poss)? 
NounIrregU NZero NouNinflBoundary NSg0 DatGenRegI (Def|Poss)?  # Dir/LR
#Plural
NounIrregU NZero NouNinflBoundary NPlReg CaseRegU? (Def|Poss)? 
#Plural possessive (separate line for instrumental)
NounIrregU NZero NouNinflBoundary NSg0|NPlReg  PossPl CaseRegPostPluralPoss Def|Poss
NounIrregU NZero NouNinflBoundary NSg0|NPlReg  InsReg PossPl  Def|Poss

PATTERN NameDecl
# For names or proper nouns. Don't have the NouNInflBoundary to block glide deletion
#Singular
Name [<np>:] CaseRegI? (Def|Poss)? 
#Plural
Name [<np>:]  NPlReg CaseRegU? (Def|Poss)? 
#Plural possessive (separate line for instrumental)
Name [<np>:] NPlReg?  PossPl CaseRegPostPluralPoss Def|Poss
Name [<np>:] NPlReg?  InsReg PossPl  Def|Poss




PATTERN NounDeclIrregularTimeVan
#optional normal case
#Have to add noun-boundary before definite too in order to trigger glide deletion
#ժամ takes the above and dative ու

NounTimeVan|NounTimeVanU NZero NouNinflBoundary NSg0 CaseTimeVan? NouNinflBoundary (Def|Poss)? 

NounTimeVan|NounTimeVanU NZero NouNinflBoundary NSg0 CaseRegI (Def|Poss)?  # Dir/LR

NounTimeVanU  NZero NouNinflBoundary NSg0 DatGenRegU (Def|Poss)? # Dir/LR

#Plural
NounTimeVan|NounTimeVanU NZero NouNinflBoundary NPlReg CaseRegU? (Def|Poss)? 
#Plural possessive (separate line for instrumental)
NounTimeVan|NounTimeVanU NZero NouNinflBoundary NSg0|NPlReg  PossPl CaseRegPostPluralPoss Def|Poss
NounTimeVan|NounTimeVanU NZero NouNinflBoundary NSg0|NPlReg  InsReg PossPl  Def|Poss



#For irregulars among the time words
#For irregulars derived from adverbs like այսօր
#Bare adverb
NounTimeVanAdv AdvZero
#Without case, but with definite
NounTimeVanAdv AdvZero SubstZero NouNinflBoundary NSg0 NouNinflBoundary (Def|Poss)
#With case
NounTimeVanAdv AdvZero SubstZero NouNinflBoundary NSg0 CaseTimeVan NouNinflBoundary (Def|Poss)? 
NounTimeVanAdv AdvZero SubstZero NouNinflBoundary NSg0 CaseRegI (Def|Poss)?  # Dir/LR
#Plural
NounTimeVanAdv AdvZero SubstZero NouNinflBoundary NPlReg CaseRegU? (Def|Poss)? 
#Plural possessive (separate line for instrumental)
NounTimeVanAdv AdvZero SubstZero NouNinflBoundary NSg0|NPlReg  PossPl CaseRegPostPluralPoss Def|Poss
NounTimeVanAdv AdvZero SubstZero NouNinflBoundary NSg0|NPlReg  InsReg PossPl  Def|Poss

#For irregular հիմակուան
NounTimeVanHima AdvZero
NounTimeVanHima [:կ] AdvZero SubstZero NouNinflBoundary NSg0 CaseTimeVan NouNinflBoundary (Def|Poss)? 
#Plural
NounTimeVanHima [:կուան] AdvZero SubstZero NouNinflBoundary NPlReg CaseRegU? (Def|Poss)? 
#Plural possessive (separate line for instrumental)
NounTimeVanHima [:կուան] AdvZero SubstZero NouNinflBoundary NSg0|NPlReg  PossPl CaseRegPostPluralPoss Def|Poss
NounTimeVanHima [:կուան] AdvZero SubstZero NouNinflBoundary NSg0|NPlReg  InsReg PossPl  Def|Poss


#For reduced forms իրիկուն իրիկուան
#For reduced forms շաբաթ շաբթուան շաբթուն
NounTimeVanReducedBase|NounTimeVanUReducedBase NZero NouNinflBoundary NSg0 (Def|Poss)? 

NounTimeVanReducedBase NZero NouNinflBoundary NSg0 CaseRegI (Def|Poss)? 
NounTimeVanUReducedBase NZero NouNinflBoundary NSg0 DatGenRegU (Def|Poss)? 

NounTimeVanReducedReduced|NounTimeVanUReducedReduced NZero NouNinflBoundary NSg0 CaseTimeVan NouNinflBoundary (Def|Poss)? 
#Plural
NounTimeVanReducedBase|NounTimeVanUReducedBase NZero NouNinflBoundary NPlReg CaseRegU? (Def|Poss)? 
#Plural possessive (separate line for instrumental)
NounTimeVanReducedBase|NounTimeVanUReducedBase NZero NouNinflBoundary NSg0|NPlReg  PossPl CaseRegPostPluralPoss Def|Poss
NounTimeVanReducedBase|NounTimeVanUReducedBase NZero NouNinflBoundary NSg0|NPlReg  InsReg PossPl  Def|Poss
	

PATTERN NounDeclIrregReducePoly
#For nouns which reduce when they take irregular inflection
#But don't reduce when take normal inflection
#ամիս ամսու ամիսի, գլուխ
#I take the -u form as less prefered
#Singular
NounIrregUReducePoly NZero NSg0 NouNinflBoundary CaseRegI? (Def|Poss)? 
NounIrregUReducePoly NZero NSg0 NouNinflBoundaryRed CaseRegI? (Def|Poss)?   # Dir/LR
NounIrregUReducePoly NZero NouNinflBoundaryRed NSg0 CaseRegU (Def|Poss)?   # Dir/LR
#Because polysyllabic, no need for reduction boundary
#Plural
NounIrregUReducePoly NZero  NouNinflBoundary NPlReg CaseRegU? (Def|Poss)? 
#Plural possessive (separate line for instrumental)
NounIrregUReducePoly NZero  NouNinflBoundary NSg0|NPlReg  PossPl CaseRegPostPluralPoss Def|Poss
NounIrregUReducePoly NZero  NouNinflBoundary NSg0|NPlReg  InsReg PossPl  Def|Poss




PATTERN InfinitivalDecl
#For declined infinitivals, I assume they're not pluralized
#I don't add the reduction boundary as a safety precaution
#I make bare infinitival get analyezd as just a verb
#Singular caseless definite
Infinitival  NSg0 (Def|Poss)
Infinitival  NSg0 CaseRegU (Def|Poss)?
#Plural possessive (separate line for instrumental)
Infinitival   PossPl CaseRegPostPluralPoss Def|Poss
Infinitival   InsReg PossPl  Def|Poss

PATTERN AdjAdvDeclRegular

#Used as normal adjective/adverb
AdjAdvBase

#Singular definite
#Without case
AdjAdvBase SubstZero NouNinflBoundary NSg0  (Def|Poss)
AdjAdvBase SubstZero NouNinflBoundaryRed NSg0  (Def|Poss) # Dir/LR
#With case, with/without definite
AdjAdvBase SubstZero NouNinflBoundary NSg0 CaseRegI (Def|Poss)? 
AdjAdvBase SubstZero NouNinflBoundaryRed NSg0 CaseRegI (Def|Poss)?  # Dir/LR

#Plural
AdjAdvBase SubstZero NouNinflBoundary NPlReg CaseRegU? (Def|Poss)? 
AdjAdvBase SubstZero NouNinflBoundaryRed NPlReg CaseRegU? (Def|Poss)?  # Dir/LR
#Plural possessive (separate line for instrumental)
AdjAdvBase SubstZero NouNinflBoundary NSg0|NPlReg  PossPl CaseRegPostPluralPoss Def|Poss
AdjAdvBase SubstZero NouNinflBoundaryRed NSg0|NPlReg  PossPl CaseRegPostPluralPoss Def|Poss  # Dir/LR
AdjAdvBase SubstZero NouNinflBoundary NSg0|NPlReg  InsReg PossPl  Def|Poss
AdjAdvBase SubstZero NouNinflBoundaryRed NSg0|NPlReg  InsReg PossPl  Def|Poss  # Dir/LR

PATTERN AdjAdvBase
#Bare adjectives
AdjReg AdjZero
#Bare adverbs
AdvReg AdvZero
#Multi-POS
AdvAdjReg [<adv_adj>:]
AdvAdjOren [<adv_adj>:]

#Derived adverbs
AdjReg [<adv>:օրէն]
AdvAdjReg [<adv>:օրէն]

PATTERN NounOtherPOSDeclRegular

#Singular
NounOtherPOSBase NouNinflBoundary NSg0 CaseRegI? (Def|Poss)?
NounOtherPOSBase NouNinflBoundaryRed NSg0 CaseRegI? (Def|Poss)? # Dir/LR

#Plural
NounOtherPOSBase NouNinflBoundary NPlReg CaseRegU? (Def|Poss)? 
NounOtherPOSBase NouNinflBoundaryRed NPlReg CaseRegU? (Def|Poss)?  # Dir/LR
#Plural possessive (separate line for instrumental)
NounOtherPOSBase NouNinflBoundary NSg0|NPlReg  PossPl CaseRegPostPluralPoss Def|Poss
NounOtherPOSBase NouNinflBoundaryRed NSg0|NPlReg  PossPl CaseRegPostPluralPoss Def|Poss  # Dir/LR
NounOtherPOSBase NouNinflBoundary NSg0|NPlReg  InsReg PossPl  Def|Poss
NounOtherPOSBase NouNinflBoundaryRed NSg0|NPlReg  InsReg PossPl  Def|Poss  # Dir/LR

PATTERN NounOtherPOSBase
AdvNounReg [<adv_noun>:]
NounAdjReg [<noun_adj>:]

PATTERN NounDeclRisingClusters
#This is for nouns like մեղր which can variably take եր ներ
#Because of speaker-variation in syllabification

NounRisingClusters NZero NouNinflBoundary NSg0 CaseRegI (Def|Poss)?
NounRisingClusters NZero NouNinflBoundaryRed NSg0 CaseRegI (Def|Poss)?   # Dir/LR

NounRisingClusters NZero NouNinflBoundary NPlVary CaseRegU? (Def|Poss)? 
NounRisingClusters NZero NouNinflBoundaryRed NPlVary CaseRegU? (Def|Poss)?    # Dir/LR

NounRisingClusters NZero NouNinflBoundary NSg0|NPlVary  PossPl CaseRegPostPluralPoss Def|Poss
NounRisingClusters NZero NouNinflBoundaryRed NSg0|NPlVary  PossPl CaseRegPostPluralPoss Def|Poss   # Dir/LR
NounRisingClusters NZero NouNinflBoundary NSg0|NPlVary  InsReg PossPl  Def|Poss
NounRisingClusters NZero NouNinflBoundaryRed NSg0|NPlVary  InsReg PossPl  Def|Poss   # Dir/LR


PATTERN NounDeclIrregMonoWithN
#These are for nouns which are monosyllabic but take ներ, whether descriptively or prescriptively

#For nouns which take ներ prescriptively but not in practice
#I give a special diacritic in the plural -ner so that it can trigger reduction
#Singular մուկ մուկէ մկնէ 
NounIrregMonoNerOld NZero NouNinflBoundary NSg0 CaseRegI (Def|Poss)?
NounIrregMonoNerOld NZero NouNinflBoundaryRedForcedNer NSg0 [:ն]? CaseRegI (Def|Poss)?   # Dir/LR
#Plural
#մուկեր
NounIrregMonoNerOld NZero NouNinflBoundary NPlReg CaseRegU? (Def|Poss)? 
#մկեր
NounIrregMonoNerOld NZero NouNinflBoundaryRed NPlReg CaseRegU? (Def|Poss)?  # Dir/LR
#մուկներ մկներ
NounIrregMonoNerOld NZero NouNinflBoundary|NouNinflBoundaryRedForcedNer NPlNerOnly CaseRegU? (Def|Poss)?    # Dir/LR
#Plural possessive (separate line for instrumental)
#I manually add the ն for singular forms before the plural possessive
#մուկերնիս
NounIrregMonoNerOld NZero NouNinflBoundary NSg0|NPlReg  PossPl CaseRegPostPluralPoss Def|Poss 
NounIrregMonoNerOld NZero NouNinflBoundary NSg0|NPlReg  InsReg PossPl  Def|Poss

#մկերնիս
NounIrregMonoNerOld NZero NouNinflBoundaryRed NSg0|NPlReg  PossPl CaseRegPostPluralPoss Def|Poss    # Dir/LR
NounIrregMonoNerOld NZero NouNinflBoundaryRed NSg0|NPlReg  InsReg PossPl  Def|Poss   # Dir/LR

#մուկներնիս, I have to force -er 
NounIrregMonoNerOld NZero NouNinflBoundary (NSg0 [:ներ])|NPlNerOnly  PossPl CaseRegPostPluralPoss Def|Poss  # Dir/LR
NounIrregMonoNerOld NZero NouNinflBoundary NSg0|NPlNerOnly  InsReg PossPl  Def|Poss   # Dir/LR

#մկներնիս
NounIrregMonoNerOld NZero NouNinflBoundaryRedForcedNer (NSg0 [:ներ])|NPlNerOnly  PossPl CaseRegPostPluralPoss Def|Poss  # Dir/LR
NounIrregMonoNerOld NZero NouNinflBoundaryRedForcedNer NSg0|NPlNerOnly  InsReg PossPl  Def|Poss   # Dir/LR


#For nouns which take ներ both prescriptively and in practice
#It's just լեռ
#Singular լեռէ լեռնէ
NounIrregMonoNerStill NZero NouNinflBoundary NSg0 CaseRegI (Def|Poss)?
NounIrregMonoNerStill NZero NouNinflBoundary [:ն] NSg0 CaseRegI (Def|Poss)?  # Dir/LR
#Plural
NounIrregMonoNerStill NZero NouNinflBoundary NPlNerOnly CaseRegU? (Def|Poss)? 
#Plural possessive (separate line for instrumental)
#I manually add the ն for singular forms before the plural possessive
# Have to force եր inside the singular
NounIrregMonoNerStill NZero NouNinflBoundary (NSg0 [:ներ])|NPlNerOnly  PossPl CaseRegPostPluralPoss Def|Poss 
NounIrregMonoNerStill NZero NouNinflBoundary NSg0|NPlNerOnly  InsReg PossPl  Def|Poss 


PATTERN NounDeclUtjunSuffix
#paradigm of suffix թիւն
#Singular caseless
NounUtjunSuffix NUtjunSuffix NZero NouNinflBoundary NSg0 (Def|Poss)?  
#Singular archaic case
NounUtjunSuffix NZero NouNinflBoundary NSg0 CaseUtjunArchaic (Def|Poss)? 
#Singular regular case
NounUtjunSuffix NUtjunSuffix NZero NouNinflBoundary NSg0 CaseRegI (Def|Poss)?    # Dir/LR

#Plural 
NounUtjunSuffix NUtjunSuffix  NZero NouNinflBoundary NPlReg CaseRegU? (Def|Poss)? 

#Plural possessive (separate line for instrumental)
NounUtjunSuffix NUtjunSuffix  NZero	 NouNinflBoundary NSg0|NPlReg  PossPl CaseRegPostPluralPoss Def|Poss
NounUtjunSuffix NUtjunSuffix  NZero  NouNinflBoundary NSg0|NPlReg  InsReg PossPl  Def|Poss

PATTERN NounDeclOldAnForm
#These are nouns which can take -ան in the dative
#The -an form is rather antiquated and with only a few words
#The -an then triggers arbitrary reduction
#My cheat is that this pattern only forms the dative
#The words should have a regular version in the regular list
#So this pattern only derives գարուն գարնան

NounOldAnForm NZero NouNinflBoundary NSg0 DatGenArchaicAn  (Def|Poss)? 

PATTERN NounDeclUmSuffix
#pattern for suffix ում

#Singular caseless
NounUmSuffix NUmSuffixBase NZero NouNinflBoundary NSg0 (Def|Poss)? 
#Singular archaic case
NounUmSuffix NUmSuffixReduced NZero NouNinflBoundary NSg0 DatGenArchaicAn (Def|Poss)? 
#Singular regular case
NounUmSuffix NUmSuffixBase NZero NouNinflBoundary NSg0 DatGenRegI (Def|Poss)?   # Dir/LR
NounUmSuffix NUmSuffixBase NZero NouNinflBoundary NSg0 CaseRegOblique (Def|Poss)?  

#Plural (already know it's polysyllabic)
NounUmSuffix NUmSuffixBase NZero NouNinflBoundary NPlReg CaseRegU? (Def|Poss)? 

#Plural possessive (separate line for instrumental)
NounUmSuffix NUmSuffixBase  NZero	 NouNinflBoundary NSg0|NPlReg  PossPl CaseRegPostPluralPoss Def|Poss
NounUmSuffix NUmSuffixBase  NZero  NouNinflBoundary NSg0|NPlReg  InsReg PossPl  Def|Poss

PATTERN NounDeclOldOch
#These are nouns which can take -ոջ in the dative
#The -ոջ form is rather antiquated and with only a few words
#The -ոջ then triggers arbitrary reduction
#My cheat is to use separate reduced stems
#The obliques optionally take մ
#So this pattern   derives կին կնոջ կնոջմէ կնոջէ and regularizedform

#Singular caseless ընկեր կին
NounDecOldOchNominativeStem NZero NouNinflBoundary NSg0 (Def|Poss)? 
#Singular archaic case with oblique forms ընկերոջ ընկերոջմէ կնոջ կնոջմէ
NounDecOldOchObliqueStem NZero NouNinflBoundary NSg0 DatGenArchaicOch (CaseBoundaryM CaseRegOblique)? (Def|Poss)? 
 #without nasal boundary ընկերոջէ կնոջէ
NounDecOldOchObliqueStem NZero NouNinflBoundary NSg0 DatGenArchaicOch CaseRegOblique (Def|Poss)?  # Dir/LR
#Singular regular case
NounDecOldOchNominativeStem  NZero NouNinflBoundary NSg0 CaseRegI (Def|Poss)?   # Dir/LR

#Plural 
NounDecOldOchNominativeStem  NZero NouNinflBoundary NPlReg CaseRegU? (Def|Poss)? 

#regularized plural possessive ընկերնիս կիներնիս
NounDecOldOchNominativeStem NZero	 NouNinflBoundary NSg0|NPlReg  PossPl CaseRegPostPluralPoss Def|Poss
NounDecOldOchNominativeStem NZero  NouNinflBoundary NSg0|NPlReg  InsReg PossPl  Def|Poss
#irregular plural possessive ընկերոջնիս ընկերոջմովնիս ընկերոջովնիս կնոջնիս կնոջմովնիս կնոջոջնիս
NounDecOldOchObliqueStem NZero	 NouNinflBoundary  DatGenArchaicOch PossPl CaseRegPostPluralPoss Def|Poss  # Dir/LR
NounDecOldOchObliqueStem NZero  NouNinflBoundary  DatGenArchaicOch CaseBoundaryM? InsReg PossPl  Def|Poss  # Dir/LR


PATTERN NounDecOldOchNominativeStem
#Nominative stems ընկեր կին
NounFamilyOchFixed 
NounFamilyOchBase
PATTERN NounDecOldOchObliqueStem
#Oblique  stems ընկեր կն
NounFamilyOchFixed 
NounFamilyOchReduced


PATTERN NounDeclOldFamilyOAblaut
#These are nouns which can take -օ- in the dative
#The -օ- form is rather antiquated and with only a few words
#The -ոջ then triggers arbitrary reduction
#My cheat is to use separate reduced stems
#The obliques optionally take մ
#So this pattern   derives հայր հօր հօրէ հօրմէ հօրմով and regularizedform

#Singular caseless հայր
NounFamilyOBase NZero NouNinflBoundary NSg0 (Def|Poss)? 
#Singular archaic case with oblique forms հօր  հօրմէ 
NounFamilyOAblauted NZero NouNinflBoundary NSg0  DatGenArchaicEmpty (CaseBoundaryM CaseRegOblique)? (Def|Poss)? 
 #without nasal boundary հօրէ
NounFamilyOAblauted NZero NouNinflBoundary NSg0 DatGenArchaicEmpty CaseRegOblique (Def|Poss)?   # Dir/LR
#Singular regular case հայրէ
NounFamilyOBase  NZero NouNinflBoundary NSg0 CaseRegI (Def|Poss)?   # Dir/LR

#Plural հայրեր հայրերուն
NounFamilyOBase  NZero NouNinflBoundary NPlReg CaseRegU? (Def|Poss)? 

#regularized plural possessive եղբայրնիս եղբայրովնիս
NounFamilyOBase NZero	 NouNinflBoundary NSg0|NPlReg  PossPl CaseRegPostPluralPoss Def|Poss
NounFamilyOBase NZero  NouNinflBoundary NSg0|NPlReg  InsReg PossPl  Def|Poss
#irregular plural possessive եղբօրնիս եղբօրմովնիս եղբօրովնիս
NounFamilyOAblauted NZero	 NouNinflBoundary  DatGenArchaicEmpty PossPl CaseRegPostPluralPoss Def|Poss  # Dir/LR
NounFamilyOAblauted NZero  NouNinflBoundary  DatGenArchaicEmpty CaseBoundaryM? InsReg PossPl  Def|Poss  # Dir/LR

PATTERN NounDeclOldRandomOAblaut
# These are nouns which take ablaut -ա- in the dative
# տուն տան
# doesnt take ablaut in oblique տունէ, տնէ

#Singular caseless տուն
NounRandomOBase NZero NouNinflBoundary NSg0 (Def|Poss)? 
#Singular archaic case  տան
NounRandomOAblauted NZero NouNinflBoundary NSg0  DatGenArchaicEmpty   (Def|Poss)? 
#Singular regular case տունէ
NounRandomOBase  NZero NouNinflBoundary NSg0 DatGenRegI (Def|Poss)?   # Dir/LR
NounRandomOBase  NZero NouNinflBoundary NSg0 CaseRegOblique (Def|Poss)? 
#Optional reduction
NounRandomOBase  NZero NouNinflBoundaryRed NSg0 CaseRegI (Def|Poss)?   # Dir/LR


#Plural տուներ
NounRandomOBase  NZero NouNinflBoundary NPlReg CaseRegU? (Def|Poss)? 
#regularized plural possessive տուներնիս
NounRandomOBase NZero  NouNinflBoundary NSg0|NPlReg  PossPl CaseRegPostPluralPoss Def|Poss
NounRandomOBase NZero  NouNinflBoundary NSg0|NPlReg  InsReg PossPl  Def|Poss

#Optional reduction
NounRandomOBase  NZero NouNinflBoundaryRed NPlReg CaseRegU? (Def|Poss)?  # Dir/LR
NounRandomOBase NZero  NouNinflBoundaryRed NSg0|NPlReg  PossPl CaseRegPostPluralPoss Def|Poss  # Dir/LR
NounRandomOBase NZero  NouNinflBoundaryRed NSg0|NPlReg  InsReg PossPl  Def|Poss  # Dir/LR


PATTERN NounDeclCountryOj
# These are country nouns which take -ոյ- reduction in dative
# անգլիա անգլիոյ պոլիս պոլսոյ

#Singular caseless անգլիա
NounCountryBase NZero NouNinflBoundary NSg0 (Def|Poss)? 
#Singular archaic case  անգլիոյ, must add infl boundary to get glide deletion անգլիոն
NounCountryReduced NZero NouNinflBoundary NSg0  DatGenArchaicOj  NouNinflBoundary (Def|Poss)?   # Dir/LR
#Singular regular case անգլիայի
NounCountryBase  NZero NouNinflBoundary NSg0 CaseRegI (Def|Poss)?  

#Plural անգլիաներ
NounCountryBase  NZero NouNinflBoundary NPlReg CaseRegU? (Def|Poss)? 
#regularized plural possessive տուներնիս
NounCountryBase NZero  NouNinflBoundary NSg0|NPlReg  PossPl CaseRegPostPluralPoss Def|Poss
NounCountryBase NZero  NouNinflBoundary NSg0|NPlReg  InsReg PossPl  Def|Poss



PATTERN NounDeclFamilyNkForm
#These are family nouns which can take -նք in the nominative
# And նց in the genitive/dative
#With no other cases
#So this pattern only derives Վարդանանք Վարդանանց

NounFamilyNkForm NNkSuffix NZero NouNinflBoundary NSg0  (Def|Poss)? 
NounFamilyNkForm NZero NouNinflBoundary NSg0  NNkSuffixCaseNc  (Def|Poss)? 
 
 
 
PATTERN NounDeclIrregReduce
#For nouns which supplete or have single-lexeme paradigms
#տղայ
#regular caseless and non-dative case տղայէ
NounIrregBoyBase NZero NouNinflBoundary NSg0 CaseRegOblique? (Def|Poss)? 
#regular dative case տղայի
NounIrregBoyBase NZero NouNinflBoundary NSg0 DatGenRegI (Def|Poss)?  # Dir/LR
#irregular dative case տղու
NounIrregBoyReduced NZero NouNinflBoundary NSg0 DatGenRegU (Def|Poss)? 

#regularized plural and reguarlized case տղաներու տղաքներու
NounIrregBoyBase NZero  NouNinflBoundary NPlIrregK? NPlReg CaseRegU? (Def|Poss)?   # Dir/LR
#Irregular plural and irregular case տղաք տղոց տղոցմէ
NounIrregBoyBase NZero NouNinflBoundary NPlIrregK  (Def|Poss)? 
NounIrregBoyReduced NZero NouNinflBoundary PlDatGenIrregularOts (CaseBoundaryM CaseRegOblique)? (Def|Poss)? 

#regularized plural possessive տղայներնիս տղաքներնիս
NounIrregBoyBase NZero	 NouNinflBoundary NSg0|(NPlIrregK? NPlReg)  PossPl CaseRegPostPluralPoss Def|Poss  # Dir/LR
NounIrregBoyBase NZero  NouNinflBoundary NSg0|(NPlIrregK? NPlReg)  InsReg PossPl  Def|Poss  # Dir/LR
#irregular plural possessive տղաքնիս տղաքովնիս տղաքմովնիս
NounIrregBoyBase NZero	 NouNinflBoundary NPlIrregK  PossPl CaseRegPostPluralPoss Def|Poss
NounIrregBoyBase NZero  NouNinflBoundary NPlIrregK CaseBoundaryM? InsReg PossPl  Def|Poss

#մարդ
#regular caseless and non-dative case մարդ
NounIrregMan NZero NouNinflBoundary NSg0 CaseRegOblique? (Def|Poss)? 
#regular dative case մարդի
NounIrregMan NZero NouNinflBoundary NSg0 DatGenRegI (Def|Poss)?  # Dir/LR
#irregular dative case մարդու
NounIrregMan NZero NouNinflBoundary NSg0 DatGenRegU (Def|Poss)? 

#regularized plural and reguarlized case մարդերու
NounIrregMan NZero  NouNinflBoundary NPlReg CaseRegU? (Def|Poss)?   # Dir/LR
#Irregular plural and irregular case մարդիկ մարդոց մարդոցմէ
NounIrregMan NZero NouNinflBoundary NPlIrregIg  (Def|Poss)? 
NounIrregMan NZero NouNinflBoundary PlDatGenIrregularOts (CaseBoundaryM CaseRegOblique)? (Def|Poss)? 

#regularized plural possessive մարդերնիս
NounIrregMan NZero	 NouNinflBoundary NSg0|NPlReg  PossPl CaseRegPostPluralPoss Def|Poss  # Dir/LR
NounIrregMan NZero  NouNinflBoundary NSg0|NPlReg  InsReg PossPl  Def|Poss	 # Dir/LR
#irregular plural possessive մարդիկնիս մարդոցնիս մարդոցովնիս մարդոցմովնիս
NounIrregMan NZero	 NouNinflBoundary NPlIrregIg  PossPl CaseRegPostPluralPoss Def|Poss
NounIrregMan NZero  NouNinflBoundary  PlDatGenIrregularOts CaseBoundaryM? InsReg PossPl  Def|Poss

#տիկին
#regular caseless  տիկին
NounIrregWomanBase NZero NouNinflBoundary NSg0 (Def|Poss)? 
#regular dative case and oblique տիկինի տիկինէ
NounIrregWomanBase NZero NouNinflBoundary NSg0 CaseRegI (Def|Poss)?  # Dir/LR
#irregular dative case տիկնոջ and oblique տիկնոջմէ
NounIrregWomanReduced NZero NouNinflBoundary NSg0 DatGenArchaicOch (CaseBoundaryM CaseRegOblique)? (Def|Poss)? 

#regularized plural and reguarlized case տիկիններու
NounIrregWomanBase NZero  NouNinflBoundary NPlReg CaseRegU? (Def|Poss)? 
#Irregular plural and irregular case տիկնայք տինանց  տիկնանցմէ տիկնանցէ
NounIrregWomanReduced NZero NouNinflBoundary [<pl>:այք]  (Def|Poss)? 
NounIrregWomanReduced NZero NouNinflBoundary [<dat_gen>:անց] (CaseBoundaryM CaseRegOblique)? (Def|Poss)? 

#regularized plural possessive տիկիննիս տիկիններնիս
NounIrregWomanBase NZero	 NouNinflBoundary NSg0|NPlReg  PossPl CaseRegPostPluralPoss Def|Poss
NounIrregWomanBase NZero  NouNinflBoundary NSg0|NPlReg  InsReg PossPl  Def|Poss
#irregular plural possessive տիկնայքնիս տիկնացնիս տիկնանցմովնիս
NounIrregWomanReduced NZero NouNinflBoundary [<pl>:այք]  PossPl CaseRegPostPluralPoss Def|Poss
NounIrregWomanReduced NZero NouNinflBoundary [<dat_gen>:անց] CaseBoundaryM? InsReg PossPl Def|Poss





# աստուած աստծոյ աստուծոյ աստուծմէ
#Singular 
NounIrregGod NZero NouNinflBoundary NSg0   (Def|Poss)? 
#Regular case
NounIrregGod NZero NouNinflBoundary NSg0 CaseRegI (Def|Poss)? 
# Archaic dative case  աստծոյ աստուծոյ
#Have to force a boundary after ոյ to trigger glide deletion
NounIrregGodNoVowel NZero NouNinflBoundary NSg0 DatGenArchaicOj NouNinflBoundary (Def|Poss)? 
NounIrregGodReducedU NZero NouNinflBoundary NSg0 DatGenArchaicOj NouNinflBoundary (Def|Poss)? 
# Archaic oblique case with nasal   աստուծմէ
NounIrregGodReducedU NZero NouNinflBoundary NSg0 CaseBoundaryM CaseRegOblique (Def|Poss)? 

#Plural
NounIrregGod NZero  NouNinflBoundary NPlReg CaseRegU? (Def|Poss)? 

#Plural possessive (separate line for instrumental)
NounIrregGod NZero	 NouNinflBoundary NSg0|NPlReg  PossPl CaseRegPostPluralPoss Def|Poss
NounIrregGod NZero  NouNinflBoundary NSg0|NPlReg  InsReg PossPl  Def|Poss


# սէր սիրոյ
#Singular 
NounLoveBase NZero NouNinflBoundary NSg0  (Def|Poss)? 
#regular case 
NounLoveBase NZero NouNinflBoundary NSg0 CaseRegI (Def|Poss)?  
# Archaic dative case  սիրոյ 
#Have to force a boundary after ոյ to trigger glide deletion
NounLoveReduced NZero NouNinflBoundary NSg0 DatGenArchaicOj NouNinflBoundary (Def|Poss)?   # Dir/LR

#Plural
NounLoveBase NZero  NouNinflBoundary NPlReg CaseRegU? (Def|Poss)? 
#Plural possessive (separate line for instrumental)
NounLoveBase NZero	 NouNinflBoundary NSg0|NPlReg  PossPl CaseRegPostPluralPoss Def|Poss
NounLoveBase NZero  NouNinflBoundary NSg0|NPlReg  InsReg PossPl  Def|Poss


#աղջիկ for dative աղջկան աղջկայ
#Singular 
NounGirlBase NZero NouNinflBoundary NSg0 (Def|Poss)? 
# regular case աղջիկի
NounGirlBase NZero NouNinflBoundary NSg0 DatGenRegI (Def|Poss)? 
NounGirlBase NZero NouNinflBoundary NSg0 CaseRegOblique (Def|Poss)?  # Dir/LR
#Reduced աղջկայ աղջկան -- not sure if nasal is always definite TO DO
NounGirlReduced NZero NouNinflBoundary NSg0 DatGenArchaicAnAj NouNinflBoundary (Def|Poss)?  
# Reduced and oblique աղջկանէ
NounGirlReduced NZero NouNinflBoundary NSg0 DatGenArchaicAn CaseRegOblique (Def|Poss)?   # Dir/LR

#Plural
NounGirlBase NZero  NouNinflBoundary NPlReg CaseRegU? (Def|Poss)? 
#Plural possessive (separate line for instrumental)
NounGirlBase NZero	 NouNinflBoundary NSg0|NPlReg  PossPl CaseRegPostPluralPoss Def|Poss
NounGirlBase NZero  NouNinflBoundary NSg0|NPlReg  InsReg PossPl  Def|Poss





#####################
####### Case patterns

PATTERN CaseRegI
#NomAccReg
DatGenRegI
AblReg
InsReg

PATTERN CaseRegOblique
AblReg
InsReg

PATTERN CaseRegU
#I allow the regular ի to be an optional form
#This overgeneralized for infintivals and reduced irregulars like ամսի
#NomAccReg
DatGenRegI # Dir/LR
DatGenRegU
AblReg
InsReg

PATTERN CaseRegPostPluralPoss
#NomAccReg 	
DatGenRegI
AblReg




PATTERNS

#Relevant functional morphemes
Indf


##############
##############
##############
#### Noun morphemes
##############
##############

LEXICON NZero
<n>:

LEXICON NUtjunSuffix
:թիւն

#ում suffix reduces to մ before ան
LEXICON NUmSuffixBase
:ում
LEXICON NUmSuffixReduced
:մ


#The suffix նք when part of a nominal suffix like family names
LEXICON NNkSuffix
:նք
#And its dative/genitive form
LEXICON NNkSuffixCaseNc
<dat_gen>:նց

###### Other POS morphemes
LEXICON AdjZero
<adj>:
LEXICON AdvZero
<adv>:
LEXICON SubstZero
<subst>:


#Number marking
LEXICON NSg0
<sg>:
LEXICON NPlReg
#This is the correct plural that counts syllables
<pl>:{Plն}եր 
#But for plural variation and errors:
#Compounds can vary in the choice of plural suffix
#Some monosyllables optionally surface with -ներ  because of possessive plural readings in Eastern
<pl>:ներ		# Dir/LR
<pl>:եր		# Dir/LR

LEXICON NPlVary
#This is for monosyllabic words which can take both plurals
<pl>:ներ		# Dir/LR
<pl>:եր		

LEXICON NPlNerOnly
<pl>:ներ	

LEXICON NPlIrregK
<pl>:ք

LEXICON NPlIrregIg
<pl>:իկ

#Boundary between noun and inflection
LEXICON NouNinflBoundary
:{Ninfl}
#Used for reduction and other boundary rules
LEXICON NouNinflBoundaryRed
:{NinflRed}
#Used for forced reduction before plural -ner 
LEXICON NouNinflBoundaryRedForcedNer
:{NinflRedForcedNer}

#Case marking
#LEXICON NomAccReg
#<nom>:
#<acc>:
LEXICON DatGenRegI
<dat_gen>:ի
<dat_gen>:ու # In case of unexpected irregularization
LEXICON DatGenRegU
<dat_gen>:ու
<dat_gen>:ի # In case of regularization
LEXICON AblReg
<abl>:է
LEXICON InsReg
<ins>:ով

LEXICON CaseUtjunArchaic
<dat_gen>:թեան
<abl>:թենէ
<ins>:թեամբ

LEXICON CaseTimeVan
<dat_gen>:ուան
<dat_gen>:ուայ 
<abl>:ուընէ
<abl>:ուանէ # Dir/LR
<ins>:ուընով
<ins>:ուանով # Dir/LR

LEXICON DatGenArchaicAn
<dat_gen>:ան

LEXICON DatGenArchaicAnAj
<dat_gen>:ան
<dat_gen>:այ


LEXICON DatGenArchaicOch
<dat_gen>:ոջ

LEXICON DatGenArchaicOj
<dat_gen>:ոյ


LEXICON DatGenArchaicEmpty #used for հօր
<dat_gen>:

#irregular marker of plural and dative genitive like it մարդոց
LEXICON PlDatGenIrregularOts
<pl><dat_gen>:ոց


#Some irregulars take regular oblique case after մ
#Like մարդոցմէ
LEXICON CaseBoundaryM
:մ

#Definite/possessive marking
LEXICON Def
<def>:{defն}
<def>:ն # Dir/LR for pre-clitic forms

LEXICON Poss
<px1sg>:ս
<px2sg>:դ

LEXICON PossPl
# Spurious plural -er- is added before -ni 
#after monosyllable and before plural possessive 
<pl_poss>:{SpurPlե}{SpurPlր}ն{PossPlի}  #The ի is deleted in vowel hiatus

#Functional morphemes
LEXICON Indf
մը<enc><idf>:մը
մը<enc><idf>:մըն

#####  Nouns
LEXICON NounReg
գիրք:գիրք
պատ:պատ
փայտ:փայտ
կապիկ:կապիկ
տուփ:տուփ
կամուրճ:կամուրճ
արքայ:արքայ
բայ:բայ
հայ:հայ
լիբանանահայ:լիբանանահայ{mono}

#Adjectives and adverbs
LEXICON AdjReg
մեծ:մեծ
ուրախ:ուրախ
LEXICON AdvReg
ուրախօրէն:ուրախօրէն

#Monosyllabic nouns which arbitrary take -ner 
#When forming their possessive plural, I have to manually add the ն before the possessive plural
LEXICON NounIrregMonoNerOld   #Some prescriptively take -ner, but in real life, they take -er
բեռ:բեռ  #Taken from Hagopian 350
դուռ:դուռ
եզ:եզ
թոռ:թոռ
հարս:հարս
մատ:մատ
մուկ:մուկ
ձուկ:ձուկ
ձեռ:ձեռ
ոտ:ոտ
ռուս:ռուս
# մահ:մահ # removed because the word has too many other irregular forms + the ner form is obsolete


LEXICON NounIrregMonoNerStill  #Only very few both descriptively and presriptively  take -ner
լեռ:լեռ
LEXICON NounRisingClusters
մեղր:մեղր

LEXICON NounIrregU
#These words take ու in the dative
#I allow them to optionally take ի 
#This don't have high vowels that can reduce
հայ:հայ #Taken from Hagopian 351
ձի:ձի
մանչ:մանչ
չին:չին
չար:չար
գահ:գահ
պահ:պահ
դար:դար
ծով:ծով
կով:կով
հաւ:հաւ
էշ:էշ
արջ:արջ
թի:թի
արեւ:արեւ

#Some nouns optionally take irregular inflection
#When they do, they reduce
LEXICON NounIrregUReducePoly
ամիս:ամիս
գլուխ:գլուխ

#List of all words that take the ութիւն suffix
#The suffix utjun cannot be part of the stem
LEXICON NounUtjunSuffix
ուրախութիւն:ուրախու

#List of all words that take the -ում suffix
#The suffix ում takes an optional irregular -ան
#The suffix ում cannot be part of the stem
LEXICON NounUmSuffix
շարժում:շարժ
անկում:անկ



#Time words which take the ուան declension
LEXICON NounTimeVan
մահ:մահ
օր:օր
ժամանակ:ժամանակ
գիշեր:գիշեր
ցերեկ:ցերեկ
վաղը:վաղ
երեկ:երեկ
ատեն:ատեն
ամառ:ամառ
ձմեռ:ձմեռ

LEXICON NounTimeVanU
#words which take the -van declension class, but can also do dative -u
ժամ:ժամ

#Time words which reduce a final vowel or substring in inflected forms
LEXICON NounTimeVanReducedBase
ամիս:ամիս
իրիկուն:իրիկուն
առտու:առտու
հեռու:հեռու
LEXICON NounTimeVanReducedReduced
ամիս:ամս
իրիկուն:իրիկ
առտու:առտ
հեռու:հեռ

#Words which take -van and -u and reduce
LEXICON NounTimeVanUReducedBase
շաբաթ:շաբաթ
տարի:տարի
LEXICON NounTimeVanUReducedReduced
շաբաթ:շաբթ
տարի:տար

#Adverbs which take -van when nominalized
LEXICON NounTimeVanAdv
այսօր:այսօր
երբ:երբ
առաջ:առաջ
անգամ:անգամ
LEXICON NounTimeVanHima
հիմա:հիմա #This word takes the time suffixes

#These irregulars only take an optional irregular dative ան, 
#while everything else is regular
#So I'll only make a lexicon for their reduced form for the dative
LEXICON NounOldAnForm
գարուն:գարն
աշուն:աշն
ամառ:ամռ
ձմեռ:ձմռ
մանուկ:մանկ
կնիկ:կնկ
լեռ:լեր
դուռ:դր

#These irregulars  take an optional irregular dative ոջ, 
#The oblique forms can take a մ 
#I classify into nouns that never reduce, those that do reduce, and the reduced form
LEXICON NounFamilyOchFixed
ընկեր:ընկեր
աներ:աներ
տալ:տալ
տագր:տագր
ներ:ներ
դասընկեր:դասընկեր

LEXICON NounFamilyOchBase
կին:կին
քոյր:քոյր
սկեսուր:սկեսուր
մօրաքոյր:մօրաքոյր{mono}
LEXICON NounFamilyOchReduced
կին:կն
քոյր:քր
սկեսուր:սկեսր
մօրաքոյր:մօրաքր

#Words like հայր հօր
LEXICON NounFamilyOBase
հայր:հայր
մայր:մայր
եղբայր:եղբայր
տիրամայր:տիրամայր{mono}
կնքահայր:կնքահայր{mono}
LEXICON NounFamilyOAblauted
հայր:հօր
մայր:մօր
եղբայր:եղբօր
տիրամայր:տիրամօր
կնքահայր:կնքահօր

#Words like տուն տան
LEXICON NounRandomOBase
տուն:տուն
շուն:շուն
դրամատուն:դրամատուն{mono}
ծովաշուն:ծովաշուն{mono}
LEXICON NounRandomOAblauted
տուն:տան
շուն:շան
դրամատուն:դրամատան
ծովաշուն:ծովաշան





#Family names which take the նք suffix in nominative
#And նց in dative/genitive with no other cases
#I don't include the suffix in the stem
LEXICON NounFamilyNkForm
Վարդանանք:Վարդանա



#These are nouns which supplete or have irregular reduction in their declensions
#The noun տղայ suppletes to տղու for dative/genitive and takes irregular plural
LEXICON NounIrregBoyBase
տղայ:տղայ
LEXICON NounIrregBoyReduced
տղայ:տղ
LEXICON NounIrregMan
մարդ:մարդ
LEXICON NounIrregWomanBase
տիկին:տիկին
տանտիկին:տանտիկին
LEXICON NounIrregWomanReduced #I make hard-coded allomorphs տիկնայք տիկնանց inside the PATTTERns
տիկին:տիկն
տանտիկին:տանտիկն

#The noun տարի suppletes to տարու for dative/genitive
LEXICON NounIrregUReduceYearBase
տարի:տարի
LEXICON NounIrregUReduceYearReduced
տարի:տար

#The noun ասուած suppletes to աստծոյ աստուծոյ  for dative/genitive
LEXICON NounIrregGod
Աստուած:աստուած
LEXICON NounIrregGodReducedU
Աստուած:աստուծ
LEXICON NounIrregGodNoVowel
Աստուած:աստծ

#The noun սէր reduces to սիրոյ  for dative/genitive
LEXICON NounLoveBase
սէր:սէր
LEXICON NounLoveReduced
սէր:սիր

#The noun աղջիկ reduces to աղջկա աղջկան  for dative/genitive
LEXICON NounGirlBase
աղջիկ:աղջիկ
LEXICON NounGirlReduced
աղջիկ:աղջկ


#The country names անգլիա changes to անգլիոյ  for dative/genitive
LEXICON NounCountryBase
Անգլիա:Անգլիա
Կիլիկիա:Կիլիկիա
Պոլիս:Պոլիս
LEXICON NounCountryReduced
Անգլիա:Անգլի
Կիլիկիա:Կիլիկի
Պոլիս:Պոլս


LEXICON IrregularBucket
#Words that are just fossilized forms
հոգւոյ<n><dat_gen>:հոգւոյ 

LEXICON Name
Յովսէփ:Յովսէփ
#! Resources:
#! http://wiki.apertium.org/wiki/Starting_a_new_language_with_HFST#Lexicon
#! http://wiki.apertium.org/wiki/Lttoolbox_and_lexc
#! http://wiki.apertium.org/wiki/Apertium-specific_conventions_for_lexc
#! https://kitwiki.csc.fi/twiki/bin/view/KitWiki/HfstHome
#! https://github.com/hfst/
#! http://www.cis.upenn.edu/~cis639/docs/lexc.html
#! http://wiki.apertium.org/wiki/Hfst



