 
#!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
#!!!  M O R P H O T A C T I C S  !!!
#!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

# I don't use zero affixes for nominative or accusative
# I don't separate syncretic genitive and dative
# Not making եան declension because obsolete
#Not making եաց class From Hagopian grammar

########################################
########################################
########################################
### NOUN PARADIGMS
########################################
########################################
########################################
########################################

# TO DO: add attributive morphology but it's really hard to know what its distribution is: Dum-Tragut 2009 page 553, and it's mostly limited to spoken speech
#TO DO: remove repetition in output like ամիսը by reorganizing declensions based on Number

#All the main declension systems
PATTERNS
OpenClassRegularNounDeclCanBeBare
OpenClassRegularNounDeclCantBeBare
NounDeclIrregularU
InfinitivalDecl


#suffix-based declensions
NounDeclUtjunSuffix
NounDeclUmSuffix

#Full-declension patterns
NounDeclIrregMonoWithN
NounDeclIrregularTimeVan
NounDeclOldOch
NounDeclOldFamilyOAblaut


# Dative+oblique only patterns
NounDeclOldAnForm
NounDeclFamilyNkForm
NounDeclOldRandomOAblaut
NounDeclCountry

#Irregular words
NounDeclIrregReduce
NounDeclIrregReducePoly


##########################
##########################

PATTERN OpenClassRegularNounDeclCanBeBare
#This is for bucket of nouns that get similar enough inflection
# This includes regular nouns, multi-POS nouns, and names that are nominalized even when bare
# postpositions
#This excludes irregular U, and infinitivals
#This excludes adverbs/adjectives and participles that are nominalized when non-bare


OpenClassRegularNounDeclCanBeBareStem NSg0 RegNounCaseIDeclensionWithOptDef
OpenClassRegularNounDeclCanBeBareStem RegPluralAndPluralPossessiveDeclension

PATTERN OpenClassRegularNounDeclCanBeBareStem
NounReg NZero NouNinflBoundary
NounReg NZero NouNinflBoundaryRed # Dir/LR
NounOtherPOSBase NouNinflBoundary
NounOtherPOSBase NouNinflBoundaryRed  # Dir/LR
DeclinablePostposition [<subs>:ին]? NouNinflBoundary

# For names or proper nouns. Don't have the NouNInflBoundary to block glide deletion
#Country names  are in separate category because some of them take -ia suffix

NameBaseNotCountry NouNinflBoundary


PATTERN NameBaseNotCountry
NameMaleGiven [<n><np><ant><m>:]
NameFemaleGiven [<n><np><ant><f>:]
NameMaleFemaleGiven [<n><np><ant><f_m>:]
NameMaleGivenLocation [<n><np><ant_m_top>:]
NameMaleFemaleGivenLocation [<n><np><ant_f_m_top>:]
NameFemaleGivenLocation [<n><np><ant_f_top>:]

NameFamily  [<n><np><pat>:]
NameGivenUnclassified [<n><np><ant>:]
NameNotGivenCountry [<n><np><al>:]

PATTERN NounOtherPOSBase
AdvNounReg [<adv_noun>:]
NounAdjReg [<noun_adj>:]



PATTERN RegNounCaseIDeclensionWithOptDef # singular
#This is for singular case/def inflection where both case and def are optional
#This is like regular nouns
CaseRegI? (Def|Poss)?
AttributiveGenIDef
AttributiveInst

PATTERN CaseDefUDeclension
#This is case/def slots where dative is -u
#Used by plurals and by irregular nouns
CaseRegU? (Def|Poss)?
AttributiveGenUDef
AttributiveInst


PATTERN RegNounPluralFormDeclension # plural
NPlReg CaseDefUDeclension


PATTERN PluralPossessiveDeclension # plural possessive
PossPl CaseRegPostPluralPoss Def|Poss
InsReg PossPl  Def|Poss

PATTERN RegPluralAndPluralPossessiveDeclension
# plural
RegNounPluralFormDeclension
# plural possessive
NSg0|NPlReg PluralPossessiveDeclension
#Not doing combo of plural possessive and of attributive because weird

 

##########################
##########################
PATTERN OpenClassRegularNounDeclCantBeBare
#This is for bucket of words that get regular nominal inflection when not bare
# This includes  adverbs/adjectives and participles that are nominalized when non-bare
# interjctions, nubmers

OpenClassRegularNounDeclCantBeBareStem NSg0 RegNounCaseIDeclensionWithReqDef
OpenClassRegularNounDeclCantBeBareStem RegPluralAndPluralPossessiveDeclension

#For bookkeeping, I also have bare adjectives/adverbs here without any substantivization
AdjAdvBaseDegreed


PATTERN OpenClassRegularNounDeclCantBeBareStem
ParticipleDeclStem
AdjAdvDeclStem
DeclDeclinableInterjection SubstZero NouNinflBoundary
NumericalBase SubstZero  NouNinflBoundary
NumericalBase SubstZero  NouNinflBoundaryRed # Dir/LR
CardinalFraction SubstZero  NouNinflBoundary
CardinalFraction SubstZero NouNinflBoundaryRed # Dir/LR


PATTERN ParticipleDeclStem
ParticipleInflectable SubstZero NouNinflBoundary
(:Neg ParticipleInflectable Neg:) SubstZero NouNinflBoundary

PATTERN ParticipleInflectable
PerfectPart
SubjectPart
ProspectivePart

PATTERN AdjAdvDeclStem
AdjAdvBaseDegreed SubstZero NouNinflBoundary
AdjAdvBaseDegreed SubstZero NouNinflBoundaryRed # Dir/LR


PATTERN AdjAdvBaseDegreed
#Adj with or without superlative prefix
AdjAdvBaseSimple
:AdjSuper AdjAdvBaseSimple AdjSuper:

PATTERN AdjAdvBaseSimple
#Bare adjectives
AdjReg AdjZero
#Bare adverbs
AdvReg AdvZero
#Multi-POS
AdvAdjReg 	[<adv_adj>:] # Dir/LR
AdvAdjOren 	[<adv_adj>:] #Dir/LR
AdvAdjReg 	NouNinflBoundaryRed [<adv_adj>:] # Dir/LR
AdvAdjOren 	NouNinflBoundaryRed [<adv_adj>:] #Dir/LR

#Derived adverbs
AdjReg AdvOren
AdjReg NouNinflBoundaryRed AdvOren # Dir/LR
AdvAdjReg [<adj>:] AdvOren
AdvAdjReg [<adj>:] NouNinflBoundaryRed AdvOren # Dir/LR
NounAdjReg [<n_adj>:] AdvOren
NounAdjReg [<n_adj>:] NouNinflBoundaryRed AdvOren # Dir/LR





PATTERN RegNounCaseIDeclensionWithReqDef
#This is for singular case/def inflection where both either case or  def is required
#This is like substantivized nouns where the bare form is not interpreted as a noun
# without case
Def|Poss
# with case, with/without definite
CaseRegI (Def|Poss)?
# attributive
AttributiveGenIDef
AttributiveInst

##########################
##########################


PATTERN NounDeclIrregularU
#This is for nouns that take u-declension
#This don't have high vowels that can reduce
#This is a closet set of nouns
# Does not include infinitivals because infinitivals cant be bare
#Singular
UNounStemBoundary NSg0 CaseDefUDeclension
UNounStemBoundary NSg0 DatGenRegI (Def|Poss)?  # Dir/LR

NounIrregU NZero NouNinflBoundaryRed NSg0 AttributiveGenUDef|AttributiveInst  # Dir/LR

#Plural
UNounStemBoundary RegPluralAndPluralPossessiveDeclension
NounIrregU NZero NouNinflBoundaryRed NPlReg AttributiveGenUDef|AttributiveInst  # Dir/LR

PATTERN UNounStemBoundary
NounIrregU NZero NouNinflBoundary



##########################
##########################

PATTERN InfinitivalDecl
#For declined infinitivals, I assume they're not pluralized
#I don't add the reduction boundary as a safety precaution
#I make bare infinitival get analyezd as just a verb
#TO DO: I don't think they can take attributive, but I put them here just to be safe

#Singular caseless definite and attributive
InfinitivalDeclStem  NSg0 DeclUWithFixedDef
#Plural possessive
InfinitivalDeclStem   PluralPossessiveDeclension


PATTERN InfinitivalDeclStem
Infinitival
:Neg Infinitival Neg:


PATTERN DeclUWithFixedDef
#This is case-def where dative is -u and the noun can't be bare, like an infinitival
Def|Poss
CaseRegU (Def|Poss)?
AttributiveGenUDef
AttributiveInst



######################
######################



PATTERN NounTimeVanAdvStem
NounTimeVanAdv AdvZero

PATTERN NounTimeVanAdvNominalizedStem
NounTimeVanAdvStem SubstZero NouNinflBoundary

PATTERN NounTimeVanStem
NounTimeVan NZero NouNinflBoundary
NounTimeVanU NZero NouNinflBoundary

PATTERN NounDeclIrregularTimeVan
#optional normal case
#Have to add noun-boundary before definite too in order to trigger glide deletion
#ժամ takes the above and dative ու

NounTimeVanStem NSg0 CaseTimeVan? NouNinflBoundary (Def|Poss)? 
NounTimeVanStem NSg0 RegNounCaseIDeclensionWithOptDef  # Dir/LR
NounTimeVanU  NZero NouNinflBoundary NSg0 DatGenRegU (Def|Poss)? # Dir/LR
#sometimes see words like շատոնցմէ where get regular inflection but case-boundary M
NounTimeVanStem NSg0 CaseBoundaryM CaseRegOblique (Def|Poss)?  # Dir/LR


#Singular attributive
#The ուանէ form is preferably treated as attributive
NounTimeVanStem NSg0 AttributiveVan
NounTimeVanU 	 			NZero NouNinflBoundary NSg0 AttributiveGenUDef|AttributiveInst   # Dir/LR


#Plural
NounTimeVanStem RegPluralAndPluralPossessiveDeclension



#For irregulars among the time words
#For irregulars derived from adverbs like այսօր
#Bare adverb
NounTimeVanAdvStem
#Without case, but with definite
NounTimeVanAdvNominalizedStem NSg0 NouNinflBoundary (Def|Poss)
#With case
NounTimeVanAdvNominalizedStem NSg0 CaseTimeVan NouNinflBoundary (Def|Poss)? 
NounTimeVanAdvNominalizedStem NSg0 CaseRegI (Def|Poss)?  # Dir/LR
#with possible case-boundary m like շատոնցմէ
NounTimeVanAdvNominalizedStem NSg0 CaseBoundaryM CaseRegOblique (Def|Poss)?  # Dir/LR


#Attributive
NounTimeVanAdvNominalizedStem NSg0 AttributiveVan

#Plural
NounTimeVanAdvNominalizedStem RegPluralAndPluralPossessiveDeclension

#For irregular հիմակուան from հիմա
NounTimeVanHima AdvZero
NounTimeVanHima [:կ] AdvZero SubstZero NouNinflBoundary NSg0 CaseTimeVan NouNinflBoundary (Def|Poss)? 

#singular attributive
NounTimeVanHima [:կ] AdvZero SubstZero NouNinflBoundary NSg0 AttributiveVan


#Plural possessive (separate line for instrumental)
NounTimeVanHima [:կուան] AdvZero SubstZero NouNinflBoundary NSg0|NPlReg PluralPossessiveDeclension


#For reduced forms իրիկուն իրիկուան
#For reduced forms շաբաթ շաբթուան շաբթուն
# I don't think we ever find մ boundary here, even in older texts

NounTimeVanReducedBase|NounTimeVanUReducedBase NZero NouNinflBoundary NSg0 (Def|Poss)? 

NounTimeVanReducedReduced|NounTimeVanUReducedReduced NZero NouNinflBoundary NSg0 CaseTimeVan NouNinflBoundary (Def|Poss)?
NounTimeVanReducedBase|NounTimeVanUReducedBase NZero NouNinflBoundary NSg0 CaseRegI (Def|Poss)? # Dir/LR
NounTimeVanUReducedReduced NZero NouNinflBoundary NSg0 DatGenRegU (Def|Poss)? # Dir/LR

#attributive
NounTimeVanReducedReduced|NounTimeVanUReducedReduced NZero NouNinflBoundary NSg0 AttributiveVan
NounTimeVanReducedBase|NounTimeVanUReducedBase NZero NouNinflBoundary NSg0 AttributiveGenIDef|AttributiveInst # Dir/LR
NounTimeVanUReducedReduced NZero NouNinflBoundary NSg0 AttributiveGenUDef|AttributiveInst # Dir/LR

 
#Plural
NounTimeVanReducedBase|NounTimeVanUReducedBase NZero NouNinflBoundary RegPluralAndPluralPossessiveDeclension


#############################
#############################
#############################
#############################
#Other irregulars

PATTERN NounDeclIrregReducePoly
#For nouns which reduce when they take irregular inflection
#But don't reduce when take normal inflection
#right now just գլուխ because ամիս can go under the -van words
#I take the -u form as less prefered
#Singular
NounIrregUReducePoly NZero NSg0 NouNinflBoundary RegNounCaseIDeclensionWithOptDef
NounIrregUReducePoly NZero NSg0 NouNinflBoundaryRed RegNounCaseIDeclensionWithOptDef   # Dir/LR
NounIrregUReducePoly NZero NouNinflBoundaryRed NSg0 CaseDefUDeclension   # Dir/LR

#Because polysyllabic, no need for reduction boundary
#Plural
NounIrregUReducePoly NZero  NouNinflBoundary RegPluralAndPluralPossessiveDeclension



#############################
PATTERN NounDeclIrregMonoWithN
#These are for nouns which are monosyllabic but take ներ, whether descriptively or prescriptively

#For nouns which take ներ prescriptively but not in practice
#I give a special diacritic in the plural -ner so that it can trigger reduction
#Singular մուկ մուկէ մկնէ 
NounIrregMonoNerOld NZero NouNinflBoundary NSg0 RegNounCaseIDeclensionWithOptDef
NounIrregMonoNerOld NZero NouNinflBoundaryRedForcedNer NSg0 [:ն]? CaseRegI (Def|Poss)?   # Dir/LR

#attributive
NounIrregMonoNerOld NZero NouNinflBoundaryRedForcedNer NSg0 [:ն]? AttributiveGenIDef|AttributiveInst   # Dir/LR

#Plural
#մուկեր
NounIrregMonoNerOld NZero NouNinflBoundary NPlReg CaseDefUDeclension
#մկեր
NounIrregMonoNerOld NZero NouNinflBoundaryRed NPlReg CaseDefUDeclension  # Dir/LR
#մուկներ մկներ
NounIrregMonoNerOld NZero NouNinflBoundary|NouNinflBoundaryRedForcedNer NPlNerOnly CaseDefUDeclension    # Dir/LR


#Plural possessive (separate line for instrumental)
#I manually add the ն for singular forms before the plural possessive
#մուկերնիս
NounIrregMonoNerOld NZero NouNinflBoundary NSg0|NPlReg  PluralPossessiveDeclension

#մկերնիս
NounIrregMonoNerOld NZero NouNinflBoundaryRed NSg0|NPlReg  PluralPossessiveDeclension   # Dir/LR

#մուկներնիս, I have to force -er 
NounIrregMonoNerOld NZero NouNinflBoundary (NSg0 [:ներ])|NPlNerOnly  PossPl CaseRegPostPluralPoss Def|Poss  # Dir/LR
NounIrregMonoNerOld NZero NouNinflBoundary NSg0|NPlNerOnly  InsReg PossPl  Def|Poss   # Dir/LR

#մկներնիս
NounIrregMonoNerOld NZero NouNinflBoundaryRedForcedNer (NSg0 [:ներ])|NPlNerOnly  PossPl CaseRegPostPluralPoss Def|Poss  # Dir/LR
NounIrregMonoNerOld NZero NouNinflBoundaryRedForcedNer NSg0|NPlNerOnly  InsReg PossPl  Def|Poss   # Dir/LR


#For nouns which take ներ both prescriptively and in practice
#It's just լեռ
#Singular լեռէ լեռնէ
NounIrregMonoNerStill NZero NouNinflBoundary NSg0 CaseRegI? (Def|Poss)?
NounIrregMonoNerStill NZero NouNinflBoundary [:ն] NSg0 CaseRegI (Def|Poss)?  # Dir/LR

NounIrregMonoNerStill NZero NouNinflBoundary NSg0 AttributiveGenIDef|AttributiveInst
NounIrregMonoNerStill NZero NouNinflBoundary [:ն] NSg0 AttributiveGenIDef|AttributiveInst  # Dir/LR

#Plural
NounIrregMonoNerStill NZero NouNinflBoundary NPlNerOnly CaseDefUDeclension

#Plural possessive (separate line for instrumental)
#I manually add the ն for singular forms before the plural possessive
# Have to force եր inside the singular
NounIrregMonoNerStill NZero NouNinflBoundary (NSg0 [:ներ])|NPlNerOnly  PossPl CaseRegPostPluralPoss Def|Poss 
NounIrregMonoNerStill NZero NouNinflBoundary NSg0|NPlNerOnly  InsReg PossPl  Def|Poss 


################################
################################
################################
################################
#Suffixal irregulars

PATTERN NounDeclUtjunSuffix
#paradigm of suffix ութիւն
#Singular caseless
NounUtjunSuffix NUtjunSuffix NZero NouNinflBoundary NSg0 (Def|Poss)?  
#Singular archaic case
NounUtjunSuffix NZero NouNinflBoundary NSg0 CaseUtjunArchaic (Def|Poss)? 
#Singular regular case
NounUtjunSuffix NUtjunSuffix NZero NouNinflBoundary NSg0 CaseRegI (Def|Poss)?    # Dir/LR

#Plural 
NounUtjunSuffix NUtjunSuffix  NZero NouNinflBoundary NPlReg CaseRegU? (Def|Poss)? 
#Archaic -ութեանց for plural genitive/dat
NounUtjunSuffix NZero NouNinflBoundary CaseUtjunArchaicPlural   # Dir/LR
#Plural possessive (separate line for instrumental)
NounUtjunSuffix NUtjunSuffix  NZero	 NouNinflBoundary NSg0|NPlReg  PluralPossessiveDeclension


#attributive, arak included թեանիս
NounUtjunSuffix  NZero NouNinflBoundary NSg0 AttributiveUtjun
NounUtjunSuffix NUtjunSuffix  NZero NouNinflBoundary NPlReg AttributiveGenUDef|AttributiveInst

PATTERN NounDeclUmSuffix
#pattern for suffix ում

#Singular caseless
NounUmSuffix NUmSuffixBase NZero NouNinflBoundary NSg0 (Def|Poss)? 
#Singular archaic case
NounUmSuffix NUmSuffixReduced NZero NouNinflBoundary NSg0 DatGenArchaicAn (Def|Poss)? 
#Singular regular case
NounUmSuffix NUmSuffixBase NZero NouNinflBoundary NSg0 DatGenRegI (Def|Poss)?   # Dir/LR
NounUmSuffix NUmSuffixBase NZero NouNinflBoundary NSg0 CaseRegOblique (Def|Poss)?  
#Singular archaic oblique cases like ուսմանէ ուսմամբ
NounUmSuffix NUmSuffixReduced NZero NouNinflBoundary NSg0 DatGenArchaicAn CaseRegOblique (Def|Poss)? # Dir/LR
NounUmSuffix NUmSuffixReduced NZero NouNinflBoundary NSg0 InstArchaicAmp (Def|Poss)? # Dir/LR



#Plural (already know it's polysyllabic)
NounUmSuffix NUmSuffixBase NZero NouNinflBoundary NPlReg CaseRegU? (Def|Poss)? 

#Plural possessive (separate line for instrumental)
NounUmSuffix NUmSuffixBase  NZero NouNinflBoundary NSg0|NPlReg PluralPossessiveDeclension


#attributive,
NounUmSuffix  NZero NouNinflBoundary NSg0 AttributiveUm 
NounUmSuffix  NZero NouNinflBoundary NPlReg AttributiveUm 


######################
#####################
##### Archaic declensions on nouns

PATTERN NounDeclOldAnForm
#These are nouns which can take -ան in the dative
#The -an form is rather antiquated and with only a few words
#The -an then triggers arbitrary reduction
#My cheat is that this pattern only forms the dative
#The words should have a regular version in the regular list
#So this pattern only derives գարուն գարնան գարնանէ
# can also do unreduced nouns like կողմ կողման կողմանէ
# some can use այ when add indefinite suffx
# Can have ամբ forms in instrumental

NounOldAnForm NZero NouNinflBoundary NSg0 DatGenArchaicAn (Def|Poss)? 
NounOldAnForm NZero NouNinflBoundary NSg0 DatGenArchaicAn CaseRegOblique  (Def|Poss)?  # Dir/LR
NounOldAnForm NZero NouNinflBoundary NSg0 DatGenArchaicAnAj # Dir/LR
NounOldAnForm NZero NouNinflBoundary NSg0 InstArchaicAmp (Def|Poss)?  # Dir/LR



PATTERN NounDeclOldOch
#These are nouns which can take -ոջ in the dative
#The -ոջ form is rather antiquated and with only a few words
#The -ոջ then triggers arbitrary reduction
#My cheat is to use separate reduced stems
#The obliques optionally take մ
#So this pattern   derives կին կնոջ կնոջմէ կնոջէ and regularizedform

#Singular caseless ընկեր կին
NounDecOldOchNominativeStem NZero NouNinflBoundary NSg0 (Def|Poss)? 
#Singular archaic case with oblique forms ընկերոջ ընկերոջմէ կնոջ կնոջմէ
NounDecOldOchObliqueStem NZero NouNinflBoundary NSg0 DatGenArchaicOch (CaseBoundaryM CaseRegOblique)? (Def|Poss)? 
 #without nasal boundary ընկերոջէ կնոջէ
NounDecOldOchObliqueStem NZero NouNinflBoundary NSg0 DatGenArchaicOch CaseRegOblique (Def|Poss)?  # Dir/LR
#Singular regular case
NounDecOldOchNominativeStem  NZero NouNinflBoundary NSg0 CaseRegI (Def|Poss)?   # Dir/LR

#Plural 
NounDecOldOchNominativeStem  NZero NouNinflBoundary NPlReg CaseRegU? (Def|Poss)? 

#regularized plural possessive ընկերնիս կիներնիս
NounDecOldOchNominativeStem NZero NouNinflBoundary NSg0|NPlReg  PluralPossessiveDeclension
#irregular plural possessive ընկերոջնիս ընկերոջմովնիս ընկերոջովնիս կնոջնիս կնոջմովնիս կնոջոջնիս
NounDecOldOchObliqueStem NZero	 NouNinflBoundary  DatGenArchaicOch PossPl CaseRegPostPluralPoss Def|Poss  # Dir/LR
NounDecOldOchObliqueStem NZero  NouNinflBoundary  DatGenArchaicOch CaseBoundaryM? InsReg PossPl  Def|Poss  # Dir/LR


#Attributive
NounDecOldOchObliqueStem NZero NouNinflBoundary NSg0 AttributiveOch
NounDecOldOchNominativeStem  NZero NouNinflBoundary NSg0 AttributiveGenIDef|AttributiveInst   # Dir/LR
NounDecOldOchNominativeStem  NZero NouNinflBoundary NPlReg AttributiveGenUDef|AttributiveInst



PATTERN NounDecOldOchNominativeStem
#Nominative stems ընկեր կին
NounFamilyOchFixed 
NounFamilyOchBase
PATTERN NounDecOldOchObliqueStem
#Oblique  stems ընկեր կն
NounFamilyOchFixed 
NounFamilyOchReduced


PATTERN NounDeclOldFamilyOAblaut
#These are nouns which can take -օ- in the dative
#The -օ- form is rather antiquated and with only a few words
#The -ոջ then triggers arbitrary reduction
#My cheat is to use separate reduced stems
#The obliques optionally take մ
#So this pattern   derives հայր հօր հօրէ հօրմէ հօրմով and regularizedform

#Singular caseless հայր
NounFamilyOBase NZero NouNinflBoundary NSg0 (Def|Poss)? 
#Singular archaic case with oblique forms հօր  հօրմէ 
NounFamilyOAblauted NZero NouNinflBoundary NSg0  DatGenArchaicEmpty (CaseBoundaryM CaseRegOblique)? (Def|Poss)? 
 #without nasal boundary հօրէ
NounFamilyOAblauted NZero NouNinflBoundary NSg0 DatGenArchaicEmpty CaseRegOblique (Def|Poss)?   # Dir/LR
#Singular regular case հայրէ
NounFamilyOBase  NZero NouNinflBoundary NSg0 CaseRegI (Def|Poss)?   # Dir/LR

#Plural հայրեր հայրերուն
NounFamilyOBase  NZero NouNinflBoundary NPlReg CaseRegU? (Def|Poss)? 

#regularized plural possessive եղբայրնիս եղբայրովնիս
NounFamilyOBase NZero	 NouNinflBoundary NSg0|NPlReg  PossPl CaseRegPostPluralPoss Def|Poss
NounFamilyOBase NZero  NouNinflBoundary NSg0|NPlReg  InsReg PossPl  Def|Poss
#irregular plural possessive եղբօրնիս եղբօրմովնիս եղբօրովնիս
NounFamilyOAblauted NZero	 NouNinflBoundary  DatGenArchaicEmpty PossPl CaseRegPostPluralPoss Def|Poss  # Dir/LR
NounFamilyOAblauted NZero  NouNinflBoundary  DatGenArchaicEmpty CaseBoundaryM? InsReg PossPl  Def|Poss  # Dir/LR


#attributive, I don't think ablauted stem takes attributive
NounFamilyOBase NZero NouNinflBoundary NSg0 AttributiveGenIDef|AttributiveInst 
NounFamilyOBase  NZero NouNinflBoundary NPlReg AttributiveGenUDef|AttributiveInst 


PATTERN NounDeclOldRandomOAblaut
# These are nouns which take ablaut -ա- in the dative
# տուն տան
# doesnt take ablaut in oblique տունէ, տնէ

#Singular caseless տուն
NounRandomOBase NZero NouNinflBoundary NSg0 (Def|Poss)? 
#Singular archaic case  տան
NounRandomOAblauted NZero NouNinflBoundary NSg0  DatGenArchaicEmpty   (Def|Poss)? 
#Singular regular case տունէ
NounRandomOBase  NZero NouNinflBoundary NSg0 DatGenRegI (Def|Poss)?   # Dir/LR
NounRandomOBase  NZero NouNinflBoundary NSg0 CaseRegOblique (Def|Poss)? 
#Optional reduction
NounRandomOBase  NZero NouNinflBoundaryRed NSg0 CaseRegI (Def|Poss)?   # Dir/LR


#Plural տուներ
NounRandomOBase  NZero NouNinflBoundary NPlReg CaseRegU? (Def|Poss)? 
#regularized plural possessive տուներնիս
NounRandomOBase NZero  NouNinflBoundary NSg0|NPlReg  PossPl CaseRegPostPluralPoss Def|Poss
NounRandomOBase NZero  NouNinflBoundary NSg0|NPlReg  InsReg PossPl  Def|Poss

#Optional reduction
NounRandomOBase  NZero NouNinflBoundaryRed NPlReg CaseRegU? (Def|Poss)?  # Dir/LR
NounRandomOBase NZero  NouNinflBoundaryRed NSg0|NPlReg  PossPl CaseRegPostPluralPoss Def|Poss  # Dir/LR
NounRandomOBase NZero  NouNinflBoundaryRed NSg0|NPlReg  InsReg PossPl  Def|Poss  # Dir/LR

#attributive, doesnt take ablaut I think
NounRandomOBase NZero NouNinflBoundary NSg0 AttributiveGenIDef|AttributiveInst 
NounRandomOBase  NZero NouNinflBoundary NPlReg AttributiveGenUDef|AttributiveInst 



PATTERN NounDeclCountry
# These are country nouns which take -ոյ- reduction in dative
# անգլիա անգլիոյ պոլիս պոլսոյ
# also includes normal countries as separate list NounCountryUnreducible
# Add toponym feature
#Singular caseless անգլիա
NounCountryBase|NounCountryUnreducible  [<n><np><top>:] NouNinflBoundary NSg0 (Def|Poss)? 
#Singular archaic case  անգլիոյ, must add infl boundary to get glide deletion անգլիոն
NounCountryReduced  [<n><np><top>:] NouNinflBoundary NSg0 DatGenArchaicOj NouNinflBoundary (Def|Poss)?  
#Singular modern dative case
NounCountryBase  [<n><np><top>:] NouNinflBoundary NSg0 DatGenRegI (Def|Poss)?  # Dir/LR
NounCountryUnreducible  [<n><np><top>:] NouNinflBoundary NSg0 DatGenRegI (Def|Poss)? 
#Singular regular oblique case
NounCountryBase|NounCountryUnreducible  [<n><np><top>:] NouNinflBoundary NSg0 CaseRegOblique (Def|Poss)? 



#Plural անգլիաներ
NounCountryBase|NounCountryUnreducible [<n><np><top>:]  NouNinflBoundary NPlReg CaseRegU? (Def|Poss)? 
#regularized plural possessive տուներնիս
NounCountryBase|NounCountryUnreducible  [<n><np><top>:] NouNinflBoundary NSg0|NPlReg  PossPl CaseRegPostPluralPoss Def|Poss
NounCountryBase|NounCountryUnreducible  [<n><np><top>:] NouNinflBoundary NSg0|NPlReg  InsReg PossPl  Def|Poss


#attributive, i assume no ablaut in the attributive
NounCountryBase|NounCountryUnreducible  [<n><np><top>:] NouNinflBoundary NSg0 AttributiveGenIDef|AttributiveInst 
NounCountryBase|NounCountryUnreducible [<n><np><top>:]  NouNinflBoundary NPlReg AttributiveGenUDef|AttributiveInst 


PATTERN NounDeclFamilyNkForm
#These are family nouns which can take -նք in the nominative
# And նց in the genitive/dative
#With no other cases
#So this pattern only derives Վարդանանք Վարդանանց

NounFamilyNkForm NNkSuffix [<n><np><pat>:] NouNinflBoundary NSg0  (Def|Poss)? 
NounFamilyNkForm [<n><np><pat>:] NouNinflBoundary NSg0  NNkSuffixCaseNc  (Def|Poss)? 
 
 
 
PATTERN NounDeclIrregReduce
#For nouns which supplete or have single-lexeme paradigms
#տղայ
#regular caseless and non-dative case տղայէ
NounIrregBoyBase NZero NouNinflBoundary NSg0 CaseRegOblique? (Def|Poss)? 
#regular dative case տղայի
NounIrregBoyBase NZero NouNinflBoundary NSg0 DatGenRegI (Def|Poss)?  # Dir/LR
#irregular dative case տղու
NounIrregBoyReduced NZero NouNinflBoundary NSg0 DatGenRegU (Def|Poss)? 

#regularized plural and reguarlized case տղաներու տղաքներու
NounIrregBoyBase NZero  NouNinflBoundary NPlIrregK? NPlReg CaseRegU? (Def|Poss)?   # Dir/LR
#Irregular plural and irregular case տղաք տղոց տղոցմէ and տղայոց
NounIrregBoyBase NZero NouNinflBoundary NPlIrregK  (Def|Poss)? 
NounIrregBoyReduced NZero NouNinflBoundary PlDatGenIrregularOts (CaseBoundaryM CaseRegOblique)? (Def|Poss)? 
NounIrregBoyBase NZero NouNinflBoundary PlDatGenIrregularOts  (CaseBoundaryM CaseRegOblique)? (Def|Poss)?  # Dir/LR

#regularized plural possessive տղայներնիս տղաքներնիս
NounIrregBoyBase NZero	 NouNinflBoundary NSg0|(NPlIrregK? NPlReg)  PossPl CaseRegPostPluralPoss Def|Poss  # Dir/LR
NounIrregBoyBase NZero  NouNinflBoundary NSg0|(NPlIrregK? NPlReg)  InsReg PossPl  Def|Poss  # Dir/LR
#irregular plural possessive տղաքնիս տղաքովնիս տղաքմովնիս
NounIrregBoyBase NZero	 NouNinflBoundary NPlIrregK  PossPl CaseRegPostPluralPoss Def|Poss
NounIrregBoyBase NZero  NouNinflBoundary NPlIrregK CaseBoundaryM? InsReg PossPl  Def|Poss

#attributive

NounIrregBoyBase NZero NouNinflBoundary NSg0 AttributiveGenIDef # Dir/LR
NounIrregBoyBase NZero NouNinflBoundary NSg0 AttributiveInst 
NounIrregBoyReduced NZero NouNinflBoundary NSg0 AttributiveGenUDef  
NounIrregBoyBase NZero  NouNinflBoundary NPlIrregK? NPlReg AttributiveGenUDef|AttributiveInst    # Dir/LR
NounIrregBoyReduced NZero NouNinflBoundary AttributiveOts 


#մարդ
#regular caseless and non-dative case մարդ
NounIrregMan NZero NouNinflBoundary NSg0 CaseRegOblique? (Def|Poss)? 
#regular dative case մարդի
NounIrregMan NZero NouNinflBoundary NSg0 DatGenRegI (Def|Poss)?  # Dir/LR
#irregular dative case մարդու
NounIrregMan NZero NouNinflBoundary NSg0 DatGenRegU (Def|Poss)? 

#regularized plural and reguarlized case մարդերու
NounIrregMan NZero  NouNinflBoundary NPlReg CaseRegU? (Def|Poss)?   # Dir/LR
#Irregular plural and irregular case մարդիկ մարդոց մարդոցմէ
NounIrregMan NZero NouNinflBoundary NPlIrregIg  (Def|Poss)? 
NounIrregMan NZero NouNinflBoundary PlDatGenIrregularOts (CaseBoundaryM CaseRegOblique)? (Def|Poss)? 

#regularized plural possessive մարդերնիս
NounIrregMan NZero	 NouNinflBoundary NSg0|NPlReg  PossPl CaseRegPostPluralPoss Def|Poss  # Dir/LR
NounIrregMan NZero  NouNinflBoundary NSg0|NPlReg  InsReg PossPl  Def|Poss	 # Dir/LR
#irregular plural possessive մարդիկնիս մարդոցնիս մարդոցովնիս մարդոցմովնիս
NounIrregMan NZero	 NouNinflBoundary NPlIrregIg  PossPl CaseRegPostPluralPoss Def|Poss
NounIrregMan NZero  NouNinflBoundary  PlDatGenIrregularOts CaseBoundaryM? InsReg PossPl  Def|Poss

#attributive
NounIrregMan NZero NouNinflBoundary NSg0 AttributiveGenUDef|AttributiveInst  
NounIrregMan NZero NouNinflBoundary NSg0 AttributiveGenIDef  # Dir/LR
NounIrregMan NZero  NouNinflBoundary NPlReg AttributiveGenUDef|AttributiveInst    # Dir/LR
NounIrregMan NZero NouNinflBoundary  AttributiveOts 



#տիկին
#regular caseless  տիկին
NounIrregWomanBase NZero NouNinflBoundary NSg0 (Def|Poss)? 
#regular dative case and oblique տիկինի տիկինէ
NounIrregWomanBase NZero NouNinflBoundary NSg0 CaseRegI (Def|Poss)?  # Dir/LR
#irregular dative case տիկնոջ and oblique տիկնոջմէ
NounIrregWomanReduced NZero NouNinflBoundary NSg0 DatGenArchaicOch (CaseBoundaryM CaseRegOblique)? (Def|Poss)? 

#regularized plural and reguarlized case տիկիններու
NounIrregWomanBase NZero  NouNinflBoundary NPlReg CaseRegU? (Def|Poss)? 
#Irregular plural and irregular case տիկնայք տինանց  տիկնանցմէ տիկնանցէ
NounIrregWomanReduced NZero NouNinflBoundary [<pl>:այք]  (Def|Poss)? 
NounIrregWomanReduced NZero NouNinflBoundary [<dat_gen>:անց] (CaseBoundaryM CaseRegOblique)? (Def|Poss)? 

#regularized plural possessive տիկիննիս տիկիններնիս
NounIrregWomanBase NZero	NouNinflBoundary NSg0|NPlReg  PossPl CaseRegPostPluralPoss Def|Poss
NounIrregWomanBase NZero  	NouNinflBoundary NSg0|NPlReg  InsReg PossPl  Def|Poss
#irregular plural possessive տիկնայքնիս տիկնացնիս տիկնանցմովնիս
NounIrregWomanReduced NZero NouNinflBoundary [<pl>:այք]  PossPl CaseRegPostPluralPoss Def|Poss
NounIrregWomanReduced NZero NouNinflBoundary [<dat_gen>:անց] CaseBoundaryM? InsReg PossPl Def|Poss

#attributive, i dont know about attriburive for այք/անց
NounIrregWomanBase NZero NouNinflBoundary NSg0 AttributiveGenIDef|AttributiveInst   # Dir/LR
NounIrregWomanReduced NZero NouNinflBoundary NSg0 AttributiveOch
NounIrregWomanBase NZero  NouNinflBoundary NPlReg AttributiveGenUDef|AttributiveInst


# աստուած աստծոյ աստուծոյ աստուծմէ
#Singular 
NounIrregGod NZero NouNinflBoundary NSg0   (Def|Poss)? 
#Regular case
NounIrregGod NZero NouNinflBoundary NSg0 CaseRegI (Def|Poss)?  # Dir/LR
# Archaic dative case  աստծոյ աստուծոյ աստուծոյն
#Have to variably force a boundary after ոյ to trigger glide deletion
NounIrregGodNoVowel NZero NouNinflBoundary NSg0 DatGenArchaicOj NouNinflBoundary? (Def|Poss)?  #Dir/LR
NounIrregGodReducedU NZero NouNinflBoundary NSg0 DatGenArchaicOj NouNinflBoundary (Def|Poss)? 
NounIrregGodReducedU NZero NouNinflBoundary NSg0 DatGenArchaicOj Def|Poss #Dir/LR
# Archaic oblique case with nasal   աստուծմէ
NounIrregGodReducedU NZero NouNinflBoundary NSg0 CaseBoundaryM CaseRegOblique (Def|Poss)? 

#Plural
NounIrregGod NZero  NouNinflBoundary NPlReg CaseRegU? (Def|Poss)? 

#Plural possessive (separate line for instrumental)
NounIrregGod NZero	 NouNinflBoundary NSg0|NPlReg  PossPl CaseRegPostPluralPoss Def|Poss
NounIrregGod NZero  NouNinflBoundary NSg0|NPlReg  InsReg PossPl  Def|Poss

#don't think can form attributive irregularly
NounIrregGod NZero NouNinflBoundary NSg0 AttributiveGenIDef|AttributiveInst
NounIrregGod NZero  NouNinflBoundary NPlReg AttributiveGenUDef|AttributiveInst

# սէր սիրոյ հոգի հոգւոյն, սիրով մարմին մարմնոյ
#Regular forms are derived elsewhere by having a duplicat entry in RegNoun
#These entries here are treated as optional ones

# Archaic dative case  սիրոյ սիրոյն
#Can't have  boundary after ոյ to block glide deletion + force ն definite
NounIrregArchaicOj NZero NouNinflBoundary NSg0 DatGenArchaicOj ([<def>:ն]|Poss)?   # Dir/LR
#սիրով սիրովն
NounIrregArchaicOj NZero NouNinflBoundary NSg0 InsReg ([<def>:ն]|Poss)?   # Dir/LR

  
#Words that supplete or reduce midvowel, and take  dative -u
#ճամբայ ճամբուն ճամբայի ճամբէն , էշ էշու իշու էշէ իշէ
#Only have irregulars, while regualrs are formed elsewhere as the norm
NounSuppleteDativeU NZero NouNinflBoundary NSg0 DatGenRegU (Def|Poss)?   # Dir/LR
NounSuppleteDativeU NZero NouNinflBoundary NSg0 CaseRegOblique (Def|Poss)?   # Dir/LR
#Plural possessive 
NounSuppleteDativeU NZero NouNinflBoundary NSg0 DatGenRegI (Def|Poss)?  # Dir/LR

#attributive
NounSuppleteDativeU NZero NouNinflBoundary NSg0 AttributiveGenUDef # Dir/LR
NounSuppleteDativeU NZero NouNinflBoundary NSg0 AttributiveInst   # Dir/LR


 
#For a bunch of words with frozen classical case-slots
# կայսր կայսեր
#Regular form is norm, so I only list irregular
#Regular form is derived in other declensions
IrregFrozenCase (Def|Poss)? 



# for words with optional syncope of non-high vowel in declined forms like բերան բերնի
#I only show syncopated forms as optional. Unsyncopated should be part of regular forms
NounSyncopeReduced NZero NouNinflBoundary NSg0 CaseRegI (Def|Poss)?    # Dir/LR
#attributive
NounSyncopeReduced NZero NouNinflBoundary NSg0 AttributiveGenIDef    # Dir/LR


############ Attributive
#I don't think the gen-def for attributive replace def with a possessive

PATTERN AttributiveGenIDef
DatGenRegI  Def|Poss [+NP:] AttributiveDefinitePostInflection
PATTERN AttributiveGenUDef
DatGenRegU  Def|Poss [+NP:] AttributiveDefinitePostInflection
PATTERN AttributiveInst
InsReg  [+NP:] AttributiveInstrumentalPostInflection
PATTERN AttributiveVan
#special ones for the Van forms
[<dat_gen><def>:ուան]   [+NP:] AttributiveDefinitePostInflection
[<ins>:ուընով]  [+NP:] AttributiveInstrumentalPostInflection
PATTERN AttributiveUm
#special ones for the um forms
NUmSuffixReduced DatGenArchaicAn  [+NP:] AttributiveDefinitePostInflection #Dir/LR
NUmSuffixBase DatGenRegI  Def|Poss   [+NP:] AttributiveDefinitePostInflection
NUmSuffixBase InsReg [+NP:] AttributiveInstrumentalPostInflection #Dir/LR
PATTERN AttributiveUtjun
# archaic forms like թեանէն
CaseUtjunArchaic   [+NP:] AttributiveDefinitePostInflection #Dir/LR 
NUtjunSuffix AttributiveGenIDef|AttributiveInst

PATTERN AttributiveOch
#special ones for the och forms
DatGenArchaicOch [+NP:] AttributiveDefinitePostInflection
DatGenArchaicOch CaseBoundaryM InsReg  [+NP:] AttributiveInstrumentalPostInflection
DatGenArchaicOch InsReg  [+NP:] AttributiveInstrumentalPostInflection # Dir/LR
PATTERN AttributiveOts
#special ones for the ots forms
PlDatGenIrregularOts   [+NP:] AttributiveDefinitePostInflection
PlDatGenIrregularOts CaseBoundaryM InsReg   [+NP:] AttributiveInstrumentalPostInflection
PlDatGenIrregularOts  InsReg [+NP:] AttributiveInstrumentalPostInflection # Dir/LR
PATTERN AttributiveAn
#special ones for the an forms like աղջիկ
DatGenArchaicAn  [+NP:] AttributiveDefinitePostInflection #Dir/LR
DatGenArchaicAn InsReg [+NP:] AttributiveInstrumentalPostInflection #Dir/LR


PATTERN AttributiveDefinitePostInflection
#This is for declension of an attributive noun
#Pre-inflection, an attributive noun ends in genitive-definite or in instrumental
#This is for the definit-ending attributive
#It has to take some overt inflection later
NSg0 CaseRegI (Def|Poss)?
NSg0 (Def|Poss)
NPlReg CaseRegU? (Def|Poss)? 

PATTERN AttributiveInstrumentalPostInflection
#This is for the instrumental-ending attributive;
#It can't just take definite because then the same meaning as un-attributive
#It has to take some overt inflection later
NSg0 CaseRegI (Def|Poss)?
NPlReg CaseRegU? (Def|Poss)? 

#####################
####### Case patterns

PATTERN CaseRegI
#NomAccReg
DatGenRegI
AblReg
InsReg
LocReg # Dir/LR

PATTERN CaseRegOblique
AblReg
InsReg
LocReg # Dir/LR

PATTERN CaseRegU
#I allow the regular ի to be an optional form
#This overgeneralized for infintivals and reduced irregulars like ամսի
#NomAccReg
DatGenRegU
AblReg
InsReg
LocReg # Dir/LR
PATTERN CaseRegPostPluralPoss
#NomAccReg 	
DatGenRegI
AblReg
LocReg # Dir/LR



PATTERNS

#Relevant functional morphemes
Indf

##############
##############
##############
#### Adjective morphemes
##############
##############
LEXICON AdvOren
<adv>:օրէն
LEXICON AdjSuper
<sup>:ամէնա # Dir/LR common misspelling
<sup>:ամենա


##############
##############
##############
#### Noun morphemes
##############
##############

LEXICON NZero
<n>:

LEXICON NUtjunSuffix
:ութիւն

#ում suffix reduces to մ before ան
LEXICON NUmSuffixBase
:ում
LEXICON NUmSuffixReduced
:մ


#The suffix նք when part of a nominal suffix like family names
LEXICON NNkSuffix
:նք
#And its dative/genitive form
LEXICON NNkSuffixCaseNc
<dat_gen>:նց

###### Other POS morphemes
LEXICON AdjZero
<adj>:
LEXICON AdvZero
<adv>:
LEXICON SubstZero
<subst>:


#Number marking
LEXICON NSg0
<sg>:
LEXICON NPlReg
#This is the correct plural that counts syllables
<pl>:{Plն}եր 
#But for plural variation and errors:
#Compounds can vary in the choice of plural suffix
#Some monosyllables optionally surface with -ներ  because of possessive plural readings in Eastern
#Some final CC-clusters variably take -ner or -er depending on a schwa
<pl>:ներ		# Dir/LR
<pl>:եր		# Dir/LR

 
LEXICON NPlNerOnly
<pl>:ներ	

LEXICON NPlIrregK
<pl>:ք

LEXICON NPlIrregIg
<pl>:իկ

#Boundary between noun and inflection
LEXICON NouNinflBoundary
:{Ninfl}
#Used for reduction and other boundary rules
LEXICON NouNinflBoundaryRed
:{NinflRed}
#Used for forced reduction before plural -ner 
LEXICON NouNinflBoundaryRedForcedNer
:{NinflRedForcedNer}

#Case marking
#LEXICON NomAccReg
#<nom>:
#<acc>:
LEXICON DatGenRegI
<dat_gen>:ի
<dat_gen>:ու #Dir/LR In case of unexpected irregularization
LEXICON DatGenRegU
<dat_gen>:ու
<dat_gen>:ի # Dir/LR In case of regularization
LEXICON AblReg
<abl>:է
<abl>:ից # Dir/LR because of code-switching
LEXICON InsReg
<ins>:ով
LEXICON LocReg
# Locative doesn't exist in WA but can find it in EA code-switching
<loc>:ում


LEXICON CaseUtjunArchaic
<dat_gen>:ութեան
<abl>:ութենէ
<abl>:ութենից # Dir/LR because of code-switching
<ins>:ութեամբ


LEXICON CaseUtjunArchaicPlural
<pl><dat_gen>:ութեանց

LEXICON CaseTimeVan
<dat_gen>:ուան # Dir/LR
<dat_gen>:ուայ 
<abl>:ուընէ
<abl>:ւընէ  # Dir/LR
<abl>:ուանէ # Dir/LR
<abl>:ուանից # Dir/LR because of code-switching
<ins>:ուընով 
<ins>:ւընով # Dir/LR
<ins>:ուանով # Dir/LR
<ins>:ուամբ # Dir/LR

LEXICON DatGenArchaicAn
<dat_gen>:ան
LEXICON DatGenArchaicAnAj
<dat_gen>:այ 


LEXICON InstArchaicAmp
<ins>:ամբ


LEXICON DatGenArchaicOch
<dat_gen>:ոջ

LEXICON DatGenArchaicOj
<dat_gen>:ոյ


LEXICON DatGenArchaicEmpty #used for հօր
<dat_gen>:

#irregular marker of plural and dative genitive like it մարդոց
LEXICON PlDatGenIrregularOts
<pl><dat_gen>:ոց


#Some irregulars take regular oblique case after մ
#Like մարդոցմէ
LEXICON CaseBoundaryM
:մ

#Definite/possessive marking
LEXICON Def
<def>:{defն}
<def>:ն # Dir/LR for pre-clitic forms

LEXICON Poss
<px1sg>:ս
<px1sg>:ըս # Dir/LR  -- in case of typos because EANC has this rule
<px2sg>:դ
<px2sg>:ըդ # Dir/LR  -- in case of typos because EANC has this rule

LEXICON PossPl
# Spurious plural -er- is added before -ni 
#after monosyllable and before plural possessive 
<pl_poss>:{SpurPlե}{SpurPlր}ն{PossPlի}  #The ի is deleted in vowel hiatus

#Functional morphemes
LEXICON Indf
մը<enc><idf>:մը
մը<enc><idf>:մըն
մը<enc><idf>:մ՚

#####  Nouns
LEXICON NounReg
գիրք:գիրք
պատ:պատ
փայտ:փայտ
կապիկ:կապիկ
տուփ:տուփ
կամուրճ:կամուրճ
արքայ:արքայ
բայ:բայ
հայ:հայ
լիբանանահայ:լիբանանահայ{mono}

#Adjectives and adverbs
LEXICON AdjReg
մեծ:մեծ
ուրախ:ուրախ
LEXICON AdvReg
ուրախօրէն:ուրախօրէն

#Monosyllabic nouns which arbitrary take -ner 
#When forming their possessive plural, I have to manually add the ն before the possessive plural
LEXICON NounIrregMonoNerOld   #Some prescriptively take -ner, but in real life, they take -er
բեռ:բեռ  #Taken from Hagopian 350
#Larger list is in noun_lex_irreg


LEXICON NounIrregMonoNerStill  #Only very few both descriptively and presriptively  take -ner
#Larger list is in noun_lex_irreg
լեռ:լեռ

LEXICON NounIrregU
#These words take ու in the dative
#I allow them to optionally take ի 
#This don't have high vowels that can reduce
#Larger list in noun_lex_irreg
հայ:հայ #Taken from Hagopian 351

#Some nouns optionally take irregular inflection
#When they do, they reduce
LEXICON NounIrregUReducePoly
#Larger list in noun_lex_irreg
գլուխ:գլուխ

#List of all words that take the ութիւն suffix
#The suffix utjun cannot be part of the stem
LEXICON NounUtjunSuffix
ուրախութիւն:ուրախ

#List of all words that take the -ում suffix
#The suffix ում takes an optional irregular -ան
#The suffix ում cannot be part of the stem
LEXICON NounUmSuffix
շարժում:շարժ
անկում:անկ



#Time words which take the ուան declension
LEXICON NounTimeVan
#Larger list in noun_lex_irreg
մահ:մահ

LEXICON NounTimeVanU
#words which take the -van declension class, but can also do dative -u
#Larger list in noun_lex_irreg
ժամ:ժամ

#Time words which reduce a final vowel or substring in inflected forms
LEXICON NounTimeVanReducedBase
#Larger list in noun_lex_irreg
իրիկուն:իրիկուն
LEXICON NounTimeVanReducedReduced
#Larger list in noun_lex_irreg
իրիկուն:իրիկ

#Words which take -van and -u and reduce
LEXICON NounTimeVanUReducedBase
#Larger list in noun_lex_irreg
շաբաթ:շաբաթ
ամիս:ամիս
LEXICON NounTimeVanUReducedReduced
#Larger list in noun_lex_irreg
ամիս:ամս
շաբաթ:շաբթ

#Adverbs which take -van when nominalized
LEXICON NounTimeVanAdv
#Larger list in noun_lex_irreg
այսօր:այսօր
հիմակ:հիմակ # this is a very rare word, more often just հիմա

LEXICON NounTimeVanHima
հիմա:հիմա #This word takes the time suffixes

#These irregulars only take an optional irregular dative ան, and its inflected like -անէ 
#while everything else is regular
# Some have ամբ form too
#My cheat is to only use the reduced stem here
#Each of these words should have a duplicate entry in the regular list, else in time list
LEXICON NounOldAnForm
#Larger list in noun_lex_irreg
գարուն:գարն


#These irregulars  take an optional irregular dative ոջ, 
#The oblique forms can take a մ 
#I classify into nouns that never reduce, those that do reduce, and the reduced form
LEXICON NounFamilyOchFixed
#Larger list in noun_lex_irreg
ընկեր:ընկեր

LEXICON NounFamilyOchBase
#Larger list in noun_lex_irreg
կին:կին
LEXICON NounFamilyOchReduced
կին:կն

#Words like հայր հօր
LEXICON NounFamilyOBase
#Larger list in noun_lex_irreg
հայր:հայր
LEXICON NounFamilyOAblauted
հայր:հօր

#Words like տուն տան
LEXICON NounRandomOBase
#Larger list in noun_lex_irreg
տուն:տուն
LEXICON NounRandomOAblauted
տուն:տան
 




#Family names which take the նք suffix in nominative
#And նց in dative/genitive with no other cases
#I don't include the suffix in the stem
LEXICON NounFamilyNkForm
Վարդանանք:Վարդանա

#These are nouns which supplete or have irregular reduction in their declensions
#The noun տղայ suppletes to տղու for dative/genitive and takes irregular plural
LEXICON NounIrregBoyBase
տղայ:տղայ
LEXICON NounIrregBoyReduced
տղայ:տղ
LEXICON NounIrregMan
մարդ:մարդ
այծամարդ:այծամարդ{mono}
անտառամարդ:անտառամարդ{mono}
աստուածամարդ:աստուածամարդ{mono}
գերմարդ:գերմարդ{mono}
էրիկմարդ:էրիկմարդ{mono}
կիսամարդ:կիսամարդ{mono}
մանկամարդ:մանկամարդ{mono}
սկզբնամարդ:սկզբնամարդ{mono}
վիշապամարդ:վիշապամարդ{mono}
տղամարդ:տղամարդ{mono}
քրջամարդ:քրջամարդ{mono}
օձամարդ:օձամարդ{mono}

LEXICON NounIrregWomanBase
տիկին:տիկին
տանտիկին:տանտիկին
LEXICON NounIrregWomanReduced #I make hard-coded allomorphs տիկնայք տիկնանց inside the PATTTERns
տիկին:տիկն
տանտիկին:տանտիկն



#Words that supplete/syncopate and take -u 
#Like  ճամբայ suppletes to  ճամբու for dative/genitive, and էշ էշու իշու
# Make sure there's a regular version
LEXICON NounSuppleteDativeU
ճամբայ:ճամբ
էշ:իշ 
էշ:էշ 
կէս:կիս



#The noun աստուած suppletes to աստծոյ աստուծոյ  for dative/genitive
LEXICON NounIrregGod
աստուած:աստուած
LEXICON NounIrregGodReducedU
աստուած:աստուծ
LEXICON NounIrregGodNoVowel
աստուած:աստծ

#The noun սէր reduces to սիրոյ  for dative/genitive
# make sure this word has duplicate in regular list
# this list only has the reduced form as seen in archaic cases
LEXICON NounIrregArchaicOj
#Larger list in noun_lex_irreg
սէր:սիր

#The noun բերանի reduces to բերնի բերնէ by irregular syncope of non-high vowel
LEXICON NounSyncopeReduced
բերան:բերն
զաւակ:զաւկ
ճակատ:ճակտ
պատիւ:պատու 
հաշիւ:հաշու

#For nouns which are regular except for fossilized archaic case-forms
#I only list irregulars, and i let the regular versions be in the normal list
#LEXICON IrregFrozenCase
#դուստր<n><sg><dat_gen>:դստեր
#I put the wordin the lex_irreg list

#The country names անգլիա changes to անգլիոյ  for dative/genitive
#Regular nouns which never shift are in separate group
LEXICON NounCountryBase
Անգլիա:Անգլիա
Կիլիկիա:Կիլիկիա
Պոլիս:Պոլիս
LEXICON NounCountryReduced
Անգլիա:Անգլի
Կիլիկիա:Կիլիկի
Պոլիս:Պոլս
LEXICON NounCountryUnreducible
Հայաստան:Հայաստան


LEXICON NameMaleGiven
Յովսէփ:Յովսէփ
LEXICON NameFemaleGiven
Մարիամ:Մարիամ
LEXICON NameGivenUnclassified
#Given names that were taken from un-gendered lists
Ամալյա:Ամալյա
LEXICON NameFamily
Տէօվլէթեան:Տէօվլէթեան
Յովսէփեան:Յովսէփեան

#This is just for quick use o# f just the noun-paradigm lexd
# LEXICON NameMaleGivenLocation
# Վան:Վան
# LEXICON NameMaleFemaleGivenLocation
# Վան:Վան
# LEXICON AdvAdjReg
# կրկնակի:կրկնակի
# LEXICON AdvAdjOren
# կրկնակի:կրկնակի
# LEXICON AdvNounReg
# կրկնակի:կրկնակի
# LEXICON NounAdjReg
# կրկնակի:կրկնակի
# LEXICON Infinitival
# կրկնակի:կրկնակի
# LEXICON Neg
# կրկնակի:կրկնակի
# LEXICON NameMaleFemaleGiven 
# կրկնակի:կրկնակի
# LEXICON NameFemaleGivenLocation
# կրկնակի:կրկնակի
#LEXICON PerfectPart
# կրկնակի:կրկնակի
#LEXICON SubjectPart
# կրկնակի:կրկնակի
#LEXICON ProspectivePart
# կրկնակի:կրկնակի


#! Resources:
#! https://wiki.apertium.org/wiki/Starting_a_new_language_with_HFST#Lexicon
#! https://wiki.apertium.org/wiki/Lttoolbox_and_lexc
#! https://wiki.apertium.org/wiki/Apertium-specific_conventions_for_lexc
#! https://kitwiki.csc.fi/twiki/bin/view/KitWiki/HfstHome
#! https://github.com/hfst/
#! http://www.cis.upenn.edu/~cis639/docs/lexc.html
#! https://wiki.apertium.org/wiki/Hfst



