 
#!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
#!!!  M O R P H O T A C T I C S  !!!
#!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

# I don't use zero affixes for nominative or accusative
# I don't separate syncretic genitive and dative
# Not making եան declension because obsolete
#Not making եաց class From Hagopian grammar

########################################
########################################
########################################
### NOUN PARADIGMS
########################################
########################################
########################################
########################################

# TO DO: add attributive morphology but it's really hard to know what its distribution is: Dum-Tragut 2009 page 553, and it's mostly limited to spoken speech
#TO DO: remove repetition in output like ամիսը by reorganizing declensions based on Number

#All the main declension systems
PATTERNS
NounDeclRegular
NounDeclRisingClusters
InfinitivalDecl
AdjAdvDeclRegular
NounOtherPOSDeclRegular
NameDecl
ParticipleDeclension

#suffix-based declensions
NounDeclUtjunSuffix
NounDeclUmSuffix

#Full-declension patterns
NounDeclIrregMonoWithN
NounDeclRegularU
NounDeclIrregularTimeVan
NounDeclOldOch
NounDeclOldFamilyOAblaut

#Dative only patterns
NounDeclOldAnForm
NounDeclFamilyNkForm
NounDeclOldRandomOAblaut
NounDeclCountry

#Irregular words
NounDeclIrregReduce
NounDeclIrregReducePoly


 
PATTERN NounDeclRegular
#Singular
NounReg NZero NouNinflBoundary NSg0 CaseRegI? (Def|Poss)? 
NounReg NZero NouNinflBoundaryRed NSg0 CaseRegI? (Def|Poss)?  # Dir/LR

#Singular attributive
NounReg NZero NouNinflBoundary NSg0 AttributiveGenIDef|AttributiveInst
NounReg NZero NouNinflBoundaryRed NSg0 AttributiveGenIDef|AttributiveInst  # Dir/LR

#Plural
NounReg NZero NouNinflBoundary NPlReg CaseRegU? (Def|Poss)? 
NounReg NZero NouNinflBoundaryRed NPlReg CaseRegU? (Def|Poss)?  # Dir/LR

#Plural attributive
NounReg NZero NouNinflBoundary NPlReg AttributiveGenUDef|AttributiveInst
NounReg NZero NouNinflBoundaryRed NPlReg AttributiveGenUDef|AttributiveInst  # Dir/LR

#Plural possessive (separate line for instrumental)
NounReg NZero NouNinflBoundary NSg0|NPlReg  	PossPl CaseRegPostPluralPoss Def|Poss
NounReg NZero NouNinflBoundaryRed NSg0|NPlReg  	PossPl CaseRegPostPluralPoss Def|Poss  # Dir/LR
NounReg NZero NouNinflBoundary NSg0|NPlReg  	InsReg PossPl  Def|Poss
NounReg NZero NouNinflBoundaryRed NSg0|NPlReg  	InsReg PossPl  Def|Poss  # Dir/LR

#Not doing combo of plural possessive and of attributive because weird

PATTERN NounDeclRegularU
#This is for verbs that take ու in the genitive/dative
#This don't have high vowels that can reduce
#Singular
NounIrregU NZero NouNinflBoundary NSg0 CaseRegU? (Def|Poss)? 
NounIrregU NZero NouNinflBoundary NSg0 DatGenRegI (Def|Poss)?  # Dir/LR

NounIrregU NZero NouNinflBoundary NSg0 AttributiveGenUDef|AttributiveInst
NounIrregU NZero NouNinflBoundaryRed NSg0 AttributiveGenUDef|AttributiveInst  # Dir/LR

#Plural
NounIrregU NZero NouNinflBoundary NPlReg CaseRegU? (Def|Poss)? 

NounIrregU NZero NouNinflBoundary NPlReg AttributiveGenUDef|AttributiveInst
NounIrregU NZero NouNinflBoundaryRed NPlReg AttributiveGenUDef|AttributiveInst  # Dir/LR

#Plural possessive (separate line for instrumental)
NounIrregU NZero NouNinflBoundary NSg0|NPlReg  PossPl CaseRegPostPluralPoss Def|Poss
NounIrregU NZero NouNinflBoundary NSg0|NPlReg  InsReg PossPl  Def|Poss

PATTERN NameDecl
# For names or proper nouns. Don't have the NouNInflBoundary to block glide deletion
#COuntry names that take -ia suffix are in separate category
#Singular
NameBase   CaseRegI? (Def|Poss)? 

NameBase AttributiveGenIDef|AttributiveInst

#Plural
NameBase    NPlReg CaseRegU? (Def|Poss)? 

NameBase 	NPlReg AttributiveGenUDef|AttributiveInst


#Plural possessive (separate line for instrumental)
NameBase   NPlReg?  PossPl CaseRegPostPluralPoss Def|Poss
NameBase   NPlReg?  InsReg PossPl  Def|Poss

PATTERN NameBase
NameMaleGiven [<n><np><ant><m>:]
NameFemaleGiven [<n><np><ant><f>:]
NameMaleFemaleGiven [<n><np><ant><f_m>:]
NameMaleGivenLocation [<n><np><ant_m_top>:]
NameMaleFemaleGivenLocation [<n><np><ant_f_m_top>:]
NameFemaleGivenLocation [<n><np><ant_f_top>:]

NameFamily  [<n><np><pat>:]
NameGivenUnclassified [<n><np><ant>:]

PATTERN NounDeclIrregularTimeVan
#optional normal case
#Have to add noun-boundary before definite too in order to trigger glide deletion
#ժամ takes the above and dative ու

NounTimeVan|NounTimeVanU 	NZero NouNinflBoundary NSg0 CaseTimeVan? NouNinflBoundary (Def|Poss)? 
NounTimeVan|NounTimeVanU	NZero NouNinflBoundary NSg0 CaseRegI (Def|Poss)?  # Dir/LR
NounTimeVanU 	 			NZero NouNinflBoundary NSg0 DatGenRegU (Def|Poss)? # Dir/LR

#Singular attributive
#The ուանէ form is preferably treated as attributive
NounTimeVan|NounTimeVanU 	NZero NouNinflBoundary NSg0 AttributiveVan
NounTimeVan|NounTimeVanU	NZero NouNinflBoundary NSg0 AttributiveGenIDef|AttributiveInst   # Dir/LR
NounTimeVanU 	 			NZero NouNinflBoundary NSg0 AttributiveGenUDef|AttributiveInst   # Dir/LR


#Plural
NounTimeVan|NounTimeVanU NZero NouNinflBoundary NPlReg CaseRegU? (Def|Poss)?

NounTimeVan|NounTimeVanU NZero NouNinflBoundary NPlReg AttributiveGenUDef|AttributiveInst
 
#Plural possessive (separate line for instrumental)
NounTimeVan|NounTimeVanU NZero NouNinflBoundary NSg0|NPlReg  PossPl CaseRegPostPluralPoss Def|Poss
NounTimeVan|NounTimeVanU NZero NouNinflBoundary NSg0|NPlReg  InsReg PossPl  Def|Poss



#For irregulars among the time words
#For irregulars derived from adverbs like այսօր
#Bare adverb
NounTimeVanAdv AdvZero
#Without case, but with definite
NounTimeVanAdv AdvZero SubstZero NouNinflBoundary NSg0 NouNinflBoundary (Def|Poss)
#With case
NounTimeVanAdv AdvZero SubstZero NouNinflBoundary NSg0 CaseTimeVan NouNinflBoundary (Def|Poss)? 
NounTimeVanAdv AdvZero SubstZero NouNinflBoundary NSg0 CaseRegI (Def|Poss)?  # Dir/LR

#Attributive
NounTimeVanAdv AdvZero SubstZero NouNinflBoundary NSg0 AttributiveVan

#Plural
NounTimeVanAdv AdvZero SubstZero NouNinflBoundary NPlReg CaseRegU? (Def|Poss)? 

NounTimeVanAdv AdvZero SubstZero NouNinflBoundary NPlReg AttributiveGenUDef|AttributiveInst 

#Plural possessive (separate line for instrumental)
NounTimeVanAdv AdvZero SubstZero NouNinflBoundary NSg0|NPlReg  PossPl CaseRegPostPluralPoss Def|Poss
NounTimeVanAdv AdvZero SubstZero NouNinflBoundary NSg0|NPlReg  InsReg PossPl  Def|Poss

#For irregular հիմակուան from հիմա
NounTimeVanHima AdvZero
NounTimeVanHima [:կ] AdvZero SubstZero NouNinflBoundary NSg0 CaseTimeVan NouNinflBoundary (Def|Poss)? 

#singular attributive
NounTimeVanHima [:կ] AdvZero SubstZero NouNinflBoundary NSg0 AttributiveVan


#Plural possessive (separate line for instrumental)
NounTimeVanHima [:կուան] AdvZero SubstZero NouNinflBoundary NSg0|NPlReg  PossPl CaseRegPostPluralPoss Def|Poss
NounTimeVanHima [:կուան] AdvZero SubstZero NouNinflBoundary NSg0|NPlReg  InsReg PossPl  Def|Poss


#For reduced forms իրիկուն իրիկուան
#For reduced forms շաբաթ շաբթուան շաբթուն
NounTimeVanReducedBase|NounTimeVanUReducedBase NZero NouNinflBoundary NSg0 (Def|Poss)? 

NounTimeVanReducedReduced|NounTimeVanUReducedReduced NZero NouNinflBoundary NSg0 CaseTimeVan NouNinflBoundary (Def|Poss)?
NounTimeVanReducedBase|NounTimeVanUReducedBase NZero NouNinflBoundary NSg0 CaseRegI (Def|Poss)? # Dir/LR
NounTimeVanUReducedReduced NZero NouNinflBoundary NSg0 DatGenRegU (Def|Poss)? # Dir/LR

#attributive
NounTimeVanReducedReduced|NounTimeVanUReducedReduced NZero NouNinflBoundary NSg0 AttributiveVan
NounTimeVanReducedBase|NounTimeVanUReducedBase NZero NouNinflBoundary NSg0 AttributiveGenIDef|AttributiveInst # Dir/LR
NounTimeVanUReducedReduced NZero NouNinflBoundary NSg0 AttributiveGenUDef|AttributiveInst # Dir/LR

 
#Plural
NounTimeVanReducedBase|NounTimeVanUReducedBase NZero NouNinflBoundary NPlReg CaseRegU? (Def|Poss)? 

NounTimeVanReducedBase|NounTimeVanUReducedBase NZero NouNinflBoundary NPlReg AttributiveGenUDef|AttributiveInst

#Plural possessive (separate line for instrumental)
NounTimeVanReducedBase|NounTimeVanUReducedBase NZero NouNinflBoundary NSg0|NPlReg  PossPl CaseRegPostPluralPoss Def|Poss
NounTimeVanReducedBase|NounTimeVanUReducedBase NZero NouNinflBoundary NSg0|NPlReg  InsReg PossPl  Def|Poss
	

PATTERN NounDeclIrregReducePoly
#For nouns which reduce when they take irregular inflection
#But don't reduce when take normal inflection
#right now just գլուխ because ամիս can go under the -van words
#I take the -u form as less prefered
#Singular
NounIrregUReducePoly NZero NSg0 NouNinflBoundary CaseRegI? (Def|Poss)? 
NounIrregUReducePoly NZero NSg0 NouNinflBoundaryRed CaseRegI? (Def|Poss)?   # Dir/LR
NounIrregUReducePoly NZero NouNinflBoundaryRed NSg0 CaseRegU (Def|Poss)?   # Dir/LR

#attributive
NounIrregUReducePoly NZero NSg0 NouNinflBoundary AttributiveGenIDef|AttributiveInst
NounIrregUReducePoly NZero NSg0 NouNinflBoundaryRed AttributiveGenIDef|AttributiveInst   # Dir/LR
NounIrregUReducePoly NZero NouNinflBoundaryRed NSg0 AttributiveGenUDef|AttributiveInst   # Dir/LR

#Because polysyllabic, no need for reduction boundary
#Plural
NounIrregUReducePoly NZero  NouNinflBoundary NPlReg CaseRegU? (Def|Poss)? 

NounIrregUReducePoly NZero  NouNinflBoundary NPlReg AttributiveGenUDef|AttributiveInst 

#Plural possessive (separate line for instrumental)
NounIrregUReducePoly NZero  NouNinflBoundary NSg0|NPlReg  PossPl CaseRegPostPluralPoss Def|Poss
NounIrregUReducePoly NZero  NouNinflBoundary NSg0|NPlReg  InsReg PossPl  Def|Poss




PATTERN InfinitivalDecl
#For declined infinitivals, I assume they're not pluralized
#I don't add the reduction boundary as a safety precaution
#I make bare infinitival get analyezd as just a verb
#TO DO: I don't think they can take attributive, but I put them here just to be safe

#Singular caseless definite
Infinitival  NSg0 (Def|Poss)
Infinitival  NSg0 CaseRegU (Def|Poss)?
#Plural possessive (separate line for instrumental)
Infinitival   PossPl CaseRegPostPluralPoss Def|Poss
Infinitival   InsReg PossPl  Def|Poss

#Negation forms
#Singular caseless definite
:Neg Infinitival Neg: NSg0 (Def|Poss)
:Neg Infinitival Neg:  NSg0 CaseRegU (Def|Poss)?
#Plural possessive (separate line for instrumental)
:Neg Infinitival Neg:   PossPl CaseRegPostPluralPoss Def|Poss
:Neg Infinitival Neg:   InsReg PossPl  Def|Poss

#Attributive
Infinitival  NSg0 AttributiveGenUDef|AttributiveInst 
:Neg Infinitival Neg:  NSg0 AttributiveGenUDef|AttributiveInst 

PATTERN ParticipleDeclension
#Subject and perfect participle can take nominal inflection
ParticipleInflectable SubstZero NouNinflBoundary NSg0  (Def|Poss)
#With case, with/without definite
ParticipleInflectable SubstZero NouNinflBoundary NSg0 CaseRegI (Def|Poss)? 


#Plural
ParticipleInflectable SubstZero NouNinflBoundary NPlReg CaseRegU? (Def|Poss)? 
#Plural possessive (separate line for instrumental)
ParticipleInflectable SubstZero NouNinflBoundary NSg0|NPlReg  PossPl CaseRegPostPluralPoss Def|Poss
ParticipleInflectable SubstZero NouNinflBoundary NSg0|NPlReg  InsReg PossPl  Def|Poss

#Negation forms
:Neg ParticipleInflectable Neg: SubstZero NouNinflBoundary NSg0  (Def|Poss)
#With case, with/without definite
:Neg ParticipleInflectable Neg:  SubstZero NouNinflBoundary NSg0 CaseRegI (Def|Poss)? 

#Plural
:Neg ParticipleInflectable Neg:  SubstZero NouNinflBoundary NPlReg CaseRegU? (Def|Poss)? 
#Plural possessive (separate line for instrumental)
:Neg ParticipleInflectable Neg:  SubstZero NouNinflBoundary NSg0|NPlReg  PossPl CaseRegPostPluralPoss Def|Poss
:Neg ParticipleInflectable Neg:  SubstZero NouNinflBoundary NSg0|NPlReg  InsReg PossPl  Def|Poss

## attributive
ParticipleInflectable SubstZero NouNinflBoundary NSg0  AttributiveGenIDef|AttributiveInst 
ParticipleInflectable SubstZero NouNinflBoundary NPlReg AttributiveGenUDef|AttributiveInst 
:Neg ParticipleInflectable Neg: SubstZero NouNinflBoundary NSg0  AttributiveGenIDef|AttributiveInst 
:Neg ParticipleInflectable Neg:  SubstZero NouNinflBoundary NPlReg AttributiveGenUDef|AttributiveInst 


PATTERN ParticipleInflectable
PerfectPart
SubjectPart
ProspectivePart

PATTERN AdjAdvDeclRegular
#Exploit this for definite-marked adverbs like ընդամէնը. would be nice to remove the subst feature from them TO DO

#Used as normal adjective/adverb
AdjAdvBaseDegreed

#Singular definite
#Without case
AdjAdvBaseDegreed SubstZero NouNinflBoundary NSg0  (Def|Poss)
AdjAdvBaseDegreed SubstZero NouNinflBoundaryRed NSg0  (Def|Poss) # Dir/LR
#With case, with/without definite
AdjAdvBaseDegreed SubstZero NouNinflBoundary NSg0 CaseRegI (Def|Poss)? 
AdjAdvBaseDegreed SubstZero NouNinflBoundaryRed NSg0 CaseRegI (Def|Poss)?  # Dir/LR

#attributive
AdjAdvBaseDegreed SubstZero NouNinflBoundary NSg0  AttributiveGenIDef|AttributiveInst 
AdjAdvBaseDegreed SubstZero NouNinflBoundaryRed NSg0 AttributiveGenIDef|AttributiveInst  # Dir/LR


#Plural
AdjAdvBaseDegreed SubstZero NouNinflBoundary NPlReg CaseRegU? (Def|Poss)? 
AdjAdvBaseDegreed SubstZero NouNinflBoundaryRed NPlReg CaseRegU? (Def|Poss)?  # Dir/LR

AdjAdvBaseDegreed SubstZero NouNinflBoundary NPlReg AttributiveGenUDef|AttributiveInst 
AdjAdvBaseDegreed SubstZero NouNinflBoundaryRed NPlReg AttributiveGenUDef|AttributiveInst  # Dir/LR

#Plural possessive (separate line for instrumental)
AdjAdvBaseDegreed SubstZero NouNinflBoundary NSg0|NPlReg  PossPl CaseRegPostPluralPoss Def|Poss
AdjAdvBaseDegreed SubstZero NouNinflBoundaryRed NSg0|NPlReg  PossPl CaseRegPostPluralPoss Def|Poss  # Dir/LR
AdjAdvBaseDegreed SubstZero NouNinflBoundary NSg0|NPlReg  InsReg PossPl  Def|Poss
AdjAdvBaseDegreed SubstZero NouNinflBoundaryRed NSg0|NPlReg  InsReg PossPl  Def|Poss  # Dir/LR


PATTERN AdjAdvBaseDegreed
#Adj with or without superlative prefix
AdjAdvBaseSimple
:AdjSuper AdjAdvBaseSimple AdjSuper:

PATTERN AdjAdvBaseSimple
#Bare adjectives
AdjReg AdjZero
#Bare adverbs
AdvReg AdvZero
#Multi-POS
AdvAdjReg 	[<adv_adj>:] # Dir/LR
AdvAdjOren 	[<adv_adj>:] #Dir/LR
AdvAdjReg 	NouNinflBoundaryRed [<adv_adj>:] # Dir/LR
AdvAdjOren 	NouNinflBoundaryRed [<adv_adj>:] #Dir/LR

#Derived adverbs
AdjReg AdvOren
AdjReg NouNinflBoundaryRed AdvOren # Dir/LR
AdvAdjReg [<adj>:] AdvOren
AdvAdjReg [<adj>:] NouNinflBoundaryRed AdvOren # Dir/LR
NounAdjReg [<n_adj>:] AdvOren
NounAdjReg [<n_adj>:] NouNinflBoundaryRed AdvOren # Dir/LR


PATTERN NounOtherPOSDeclRegular

#Singular
NounOtherPOSBase NouNinflBoundary NSg0 CaseRegI? (Def|Poss)?
NounOtherPOSBase NouNinflBoundaryRed NSg0 CaseRegI? (Def|Poss)? # Dir/LR

#attributive
NounOtherPOSBase NouNinflBoundary NSg0 AttributiveGenIDef|AttributiveInst
NounOtherPOSBase NouNinflBoundaryRed NSg0 AttributiveGenIDef|AttributiveInst # Dir/LR

#Plural
NounOtherPOSBase NouNinflBoundary NPlReg CaseRegU? (Def|Poss)? 
NounOtherPOSBase NouNinflBoundaryRed NPlReg CaseRegU? (Def|Poss)?  # Dir/LR

#attributive
NounOtherPOSBase NouNinflBoundary NPlReg AttributiveGenUDef|AttributiveInst
NounOtherPOSBase NouNinflBoundaryRed NPlReg AttributiveGenUDef|AttributiveInst  # Dir/LR

#Plural possessive (separate line for instrumental)
NounOtherPOSBase NouNinflBoundary NSg0|NPlReg  PossPl CaseRegPostPluralPoss Def|Poss
NounOtherPOSBase NouNinflBoundaryRed NSg0|NPlReg  PossPl CaseRegPostPluralPoss Def|Poss  # Dir/LR
NounOtherPOSBase NouNinflBoundary NSg0|NPlReg  InsReg PossPl  Def|Poss
NounOtherPOSBase NouNinflBoundaryRed NSg0|NPlReg  InsReg PossPl  Def|Poss  # Dir/LR

PATTERN NounOtherPOSBase
AdvNounReg [<adv_noun>:]
NounAdjReg [<noun_adj>:]

PATTERN NounDeclRisingClusters
#This is for nouns like մեղր which can variably take եր ներ
#Because of speaker-variation in syllabification

NounRisingClusters NZero NouNinflBoundary NSg0 CaseRegI? (Def|Poss)?
NounRisingClusters NZero NouNinflBoundaryRed NSg0 CaseRegI? (Def|Poss)?   # Dir/LR

NounRisingClusters NZero NouNinflBoundary NPlVary CaseRegU? (Def|Poss)? 
NounRisingClusters NZero NouNinflBoundaryRed NPlVary CaseRegU? (Def|Poss)?    # Dir/LR

NounRisingClusters NZero NouNinflBoundary NSg0|NPlVary  PossPl CaseRegPostPluralPoss Def|Poss
NounRisingClusters NZero NouNinflBoundaryRed NSg0|NPlVary  PossPl CaseRegPostPluralPoss Def|Poss   # Dir/LR
NounRisingClusters NZero NouNinflBoundary NSg0|NPlVary  InsReg PossPl  Def|Poss
NounRisingClusters NZero NouNinflBoundaryRed NSg0|NPlVary  InsReg PossPl  Def|Poss   # Dir/LR

#attributive
NounRisingClusters NZero NouNinflBoundary NSg0 AttributiveGenIDef|AttributiveInst
NounRisingClusters NZero NouNinflBoundaryRed NSg0 AttributiveGenIDef|AttributiveInst   # Dir/LR

NounRisingClusters NZero NouNinflBoundary NPlVary AttributiveGenUDef|AttributiveInst
NounRisingClusters NZero NouNinflBoundaryRed NPlVary AttributiveGenUDef|AttributiveInst # Dir/LR



PATTERN NounDeclIrregMonoWithN
#These are for nouns which are monosyllabic but take ներ, whether descriptively or prescriptively

#For nouns which take ներ prescriptively but not in practice
#I give a special diacritic in the plural -ner so that it can trigger reduction
#Singular մուկ մուկէ մկնէ 
NounIrregMonoNerOld NZero NouNinflBoundary NSg0 CaseRegI? (Def|Poss)?
NounIrregMonoNerOld NZero NouNinflBoundaryRedForcedNer NSg0 [:ն]? CaseRegI (Def|Poss)?   # Dir/LR

#attributive
NounIrregMonoNerOld NZero NouNinflBoundary NSg0 AttributiveGenIDef|AttributiveInst
NounIrregMonoNerOld NZero NouNinflBoundaryRedForcedNer NSg0 [:ն]? AttributiveGenIDef|AttributiveInst   # Dir/LR

#Plural
#մուկեր
NounIrregMonoNerOld NZero NouNinflBoundary NPlReg CaseRegU? (Def|Poss)? 
#մկեր
NounIrregMonoNerOld NZero NouNinflBoundaryRed NPlReg CaseRegU? (Def|Poss)?  # Dir/LR
#մուկներ մկներ
NounIrregMonoNerOld NZero NouNinflBoundary|NouNinflBoundaryRedForcedNer NPlNerOnly CaseRegU? (Def|Poss)?    # Dir/LR

#attributive
NounIrregMonoNerOld NZero NouNinflBoundary NPlReg AttributiveGenUDef|AttributiveInst
NounIrregMonoNerOld NZero NouNinflBoundaryRed NPlReg AttributiveGenUDef|AttributiveInst  # Dir/LR
NounIrregMonoNerOld NZero NouNinflBoundary|NouNinflBoundaryRedForcedNer NPlNerOnly AttributiveGenUDef|AttributiveInst    # Dir/LR


#Plural possessive (separate line for instrumental)
#I manually add the ն for singular forms before the plural possessive
#մուկերնիս
NounIrregMonoNerOld NZero NouNinflBoundary NSg0|NPlReg  PossPl CaseRegPostPluralPoss Def|Poss 
NounIrregMonoNerOld NZero NouNinflBoundary NSg0|NPlReg  InsReg PossPl  Def|Poss

#մկերնիս
NounIrregMonoNerOld NZero NouNinflBoundaryRed NSg0|NPlReg  PossPl CaseRegPostPluralPoss Def|Poss    # Dir/LR
NounIrregMonoNerOld NZero NouNinflBoundaryRed NSg0|NPlReg  InsReg PossPl  Def|Poss   # Dir/LR

#մուկներնիս, I have to force -er 
NounIrregMonoNerOld NZero NouNinflBoundary (NSg0 [:ներ])|NPlNerOnly  PossPl CaseRegPostPluralPoss Def|Poss  # Dir/LR
NounIrregMonoNerOld NZero NouNinflBoundary NSg0|NPlNerOnly  InsReg PossPl  Def|Poss   # Dir/LR

#մկներնիս
NounIrregMonoNerOld NZero NouNinflBoundaryRedForcedNer (NSg0 [:ներ])|NPlNerOnly  PossPl CaseRegPostPluralPoss Def|Poss  # Dir/LR
NounIrregMonoNerOld NZero NouNinflBoundaryRedForcedNer NSg0|NPlNerOnly  InsReg PossPl  Def|Poss   # Dir/LR


#For nouns which take ներ both prescriptively and in practice
#It's just լեռ
#Singular լեռէ լեռնէ
NounIrregMonoNerStill NZero NouNinflBoundary NSg0 CaseRegI? (Def|Poss)?
NounIrregMonoNerStill NZero NouNinflBoundary [:ն] NSg0 CaseRegI (Def|Poss)?  # Dir/LR

NounIrregMonoNerStill NZero NouNinflBoundary NSg0 AttributiveGenIDef|AttributiveInst
NounIrregMonoNerStill NZero NouNinflBoundary [:ն] NSg0 AttributiveGenIDef|AttributiveInst  # Dir/LR

#Plural
NounIrregMonoNerStill NZero NouNinflBoundary NPlNerOnly CaseRegU? (Def|Poss)? 

NounIrregMonoNerStill NZero NouNinflBoundary NPlNerOnly AttributiveGenUDef|AttributiveInst

#Plural possessive (separate line for instrumental)
#I manually add the ն for singular forms before the plural possessive
# Have to force եր inside the singular
NounIrregMonoNerStill NZero NouNinflBoundary (NSg0 [:ներ])|NPlNerOnly  PossPl CaseRegPostPluralPoss Def|Poss 
NounIrregMonoNerStill NZero NouNinflBoundary NSg0|NPlNerOnly  InsReg PossPl  Def|Poss 


PATTERN NounDeclUtjunSuffix
#paradigm of suffix թիւն
#Singular caseless
NounUtjunSuffix NUtjunSuffix NZero NouNinflBoundary NSg0 (Def|Poss)?  
#Singular archaic case
NounUtjunSuffix NZero NouNinflBoundary NSg0 CaseUtjunArchaic (Def|Poss)? 
#Singular regular case
NounUtjunSuffix NUtjunSuffix NZero NouNinflBoundary NSg0 CaseRegI (Def|Poss)?    # Dir/LR

#Plural 
NounUtjunSuffix NUtjunSuffix  NZero NouNinflBoundary NPlReg CaseRegU? (Def|Poss)? 
#Archaic -թեանց for plural genitive/dat
NounUtjunSuffix NZero NouNinflBoundary CaseUtjunArchaicPlural   # Dir/LR
#Plural possessive (separate line for instrumental)
NounUtjunSuffix NUtjunSuffix  NZero	 NouNinflBoundary NSg0|NPlReg  PossPl CaseRegPostPluralPoss Def|Poss
NounUtjunSuffix NUtjunSuffix  NZero  NouNinflBoundary NSg0|NPlReg  InsReg PossPl  Def|Poss


#attributive, arak included թեանիս
NounUtjunSuffix  NZero NouNinflBoundary NSg0 AttributiveUtjun
NounUtjunSuffix NUtjunSuffix  NZero NouNinflBoundary NPlReg AttributiveGenUDef|AttributiveInst


PATTERN NounDeclOldAnForm
#These are nouns which can take -ան in the dative
#The -an form is rather antiquated and with only a few words
#The -an then triggers arbitrary reduction
#My cheat is that this pattern only forms the dative
#The words should have a regular version in the regular list
#So this pattern only derives գարուն գարնան

NounOldAnForm NZero NouNinflBoundary NSg0 DatGenArchaicAn  (Def|Poss)? 

PATTERN NounDeclUmSuffix
#pattern for suffix ում

#Singular caseless
NounUmSuffix NUmSuffixBase NZero NouNinflBoundary NSg0 (Def|Poss)? 
#Singular archaic case
NounUmSuffix NUmSuffixReduced NZero NouNinflBoundary NSg0 DatGenArchaicAn (Def|Poss)? 
#Singular regular case
NounUmSuffix NUmSuffixBase NZero NouNinflBoundary NSg0 DatGenRegI (Def|Poss)?   # Dir/LR
NounUmSuffix NUmSuffixBase NZero NouNinflBoundary NSg0 CaseRegOblique (Def|Poss)?  
#Singular archaic oblique cases like ուսմանէ ուսմամբ
NounUmSuffix NUmSuffixReduced NZero NouNinflBoundary NSg0 DatGenArchaicAn AblReg (Def|Poss)? # Dir/LR
NounUmSuffix NUmSuffixReduced NZero NouNinflBoundary NSg0 InstArchaicAmp (Def|Poss)? # Dir/LR



#Plural (already know it's polysyllabic)
NounUmSuffix NUmSuffixBase NZero NouNinflBoundary NPlReg CaseRegU? (Def|Poss)? 

#Plural possessive (separate line for instrumental)
NounUmSuffix NUmSuffixBase  NZero	 NouNinflBoundary NSg0|NPlReg  PossPl CaseRegPostPluralPoss Def|Poss
NounUmSuffix NUmSuffixBase  NZero  NouNinflBoundary NSg0|NPlReg  InsReg PossPl  Def|Poss


#attributive,
NounUmSuffix  NZero NouNinflBoundary NSg0 AttributiveUm 
NounUmSuffix  NZero NouNinflBoundary NPlReg AttributiveUm 


PATTERN NounDeclOldOch
#These are nouns which can take -ոջ in the dative
#The -ոջ form is rather antiquated and with only a few words
#The -ոջ then triggers arbitrary reduction
#My cheat is to use separate reduced stems
#The obliques optionally take մ
#So this pattern   derives կին կնոջ կնոջմէ կնոջէ and regularizedform

#Singular caseless ընկեր կին
NounDecOldOchNominativeStem NZero NouNinflBoundary NSg0 (Def|Poss)? 
#Singular archaic case with oblique forms ընկերոջ ընկերոջմէ կնոջ կնոջմէ
NounDecOldOchObliqueStem NZero NouNinflBoundary NSg0 DatGenArchaicOch (CaseBoundaryM CaseRegOblique)? (Def|Poss)? 
 #without nasal boundary ընկերոջէ կնոջէ
NounDecOldOchObliqueStem NZero NouNinflBoundary NSg0 DatGenArchaicOch CaseRegOblique (Def|Poss)?  # Dir/LR
#Singular regular case
NounDecOldOchNominativeStem  NZero NouNinflBoundary NSg0 CaseRegI (Def|Poss)?   # Dir/LR

#Plural 
NounDecOldOchNominativeStem  NZero NouNinflBoundary NPlReg CaseRegU? (Def|Poss)? 

#regularized plural possessive ընկերնիս կիներնիս
NounDecOldOchNominativeStem NZero	 NouNinflBoundary NSg0|NPlReg  PossPl CaseRegPostPluralPoss Def|Poss
NounDecOldOchNominativeStem NZero  NouNinflBoundary NSg0|NPlReg  InsReg PossPl  Def|Poss
#irregular plural possessive ընկերոջնիս ընկերոջմովնիս ընկերոջովնիս կնոջնիս կնոջմովնիս կնոջոջնիս
NounDecOldOchObliqueStem NZero	 NouNinflBoundary  DatGenArchaicOch PossPl CaseRegPostPluralPoss Def|Poss  # Dir/LR
NounDecOldOchObliqueStem NZero  NouNinflBoundary  DatGenArchaicOch CaseBoundaryM? InsReg PossPl  Def|Poss  # Dir/LR


#Attributive
NounDecOldOchObliqueStem NZero NouNinflBoundary NSg0 AttributiveOch
NounDecOldOchNominativeStem  NZero NouNinflBoundary NSg0 AttributiveGenIDef|AttributiveInst   # Dir/LR
NounDecOldOchNominativeStem  NZero NouNinflBoundary NPlReg AttributiveGenUDef|AttributiveInst



PATTERN NounDecOldOchNominativeStem
#Nominative stems ընկեր կին
NounFamilyOchFixed 
NounFamilyOchBase
PATTERN NounDecOldOchObliqueStem
#Oblique  stems ընկեր կն
NounFamilyOchFixed 
NounFamilyOchReduced


PATTERN NounDeclOldFamilyOAblaut
#These are nouns which can take -օ- in the dative
#The -օ- form is rather antiquated and with only a few words
#The -ոջ then triggers arbitrary reduction
#My cheat is to use separate reduced stems
#The obliques optionally take մ
#So this pattern   derives հայր հօր հօրէ հօրմէ հօրմով and regularizedform

#Singular caseless հայր
NounFamilyOBase NZero NouNinflBoundary NSg0 (Def|Poss)? 
#Singular archaic case with oblique forms հօր  հօրմէ 
NounFamilyOAblauted NZero NouNinflBoundary NSg0  DatGenArchaicEmpty (CaseBoundaryM CaseRegOblique)? (Def|Poss)? 
 #without nasal boundary հօրէ
NounFamilyOAblauted NZero NouNinflBoundary NSg0 DatGenArchaicEmpty CaseRegOblique (Def|Poss)?   # Dir/LR
#Singular regular case հայրէ
NounFamilyOBase  NZero NouNinflBoundary NSg0 CaseRegI (Def|Poss)?   # Dir/LR

#Plural հայրեր հայրերուն
NounFamilyOBase  NZero NouNinflBoundary NPlReg CaseRegU? (Def|Poss)? 

#regularized plural possessive եղբայրնիս եղբայրովնիս
NounFamilyOBase NZero	 NouNinflBoundary NSg0|NPlReg  PossPl CaseRegPostPluralPoss Def|Poss
NounFamilyOBase NZero  NouNinflBoundary NSg0|NPlReg  InsReg PossPl  Def|Poss
#irregular plural possessive եղբօրնիս եղբօրմովնիս եղբօրովնիս
NounFamilyOAblauted NZero	 NouNinflBoundary  DatGenArchaicEmpty PossPl CaseRegPostPluralPoss Def|Poss  # Dir/LR
NounFamilyOAblauted NZero  NouNinflBoundary  DatGenArchaicEmpty CaseBoundaryM? InsReg PossPl  Def|Poss  # Dir/LR


#attributive, I don't think ablauted stem takes attributive
NounFamilyOBase NZero NouNinflBoundary NSg0 AttributiveGenIDef|AttributiveInst 
NounFamilyOBase  NZero NouNinflBoundary NPlReg AttributiveGenUDef|AttributiveInst 


PATTERN NounDeclOldRandomOAblaut
# These are nouns which take ablaut -ա- in the dative
# տուն տան
# doesnt take ablaut in oblique տունէ, տնէ

#Singular caseless տուն
NounRandomOBase NZero NouNinflBoundary NSg0 (Def|Poss)? 
#Singular archaic case  տան
NounRandomOAblauted NZero NouNinflBoundary NSg0  DatGenArchaicEmpty   (Def|Poss)? 
#Singular regular case տունէ
NounRandomOBase  NZero NouNinflBoundary NSg0 DatGenRegI (Def|Poss)?   # Dir/LR
NounRandomOBase  NZero NouNinflBoundary NSg0 CaseRegOblique (Def|Poss)? 
#Optional reduction
NounRandomOBase  NZero NouNinflBoundaryRed NSg0 CaseRegI (Def|Poss)?   # Dir/LR


#Plural տուներ
NounRandomOBase  NZero NouNinflBoundary NPlReg CaseRegU? (Def|Poss)? 
#regularized plural possessive տուներնիս
NounRandomOBase NZero  NouNinflBoundary NSg0|NPlReg  PossPl CaseRegPostPluralPoss Def|Poss
NounRandomOBase NZero  NouNinflBoundary NSg0|NPlReg  InsReg PossPl  Def|Poss

#Optional reduction
NounRandomOBase  NZero NouNinflBoundaryRed NPlReg CaseRegU? (Def|Poss)?  # Dir/LR
NounRandomOBase NZero  NouNinflBoundaryRed NSg0|NPlReg  PossPl CaseRegPostPluralPoss Def|Poss  # Dir/LR
NounRandomOBase NZero  NouNinflBoundaryRed NSg0|NPlReg  InsReg PossPl  Def|Poss  # Dir/LR

#attributive, doesnt take ablaut I think
NounRandomOBase NZero NouNinflBoundary NSg0 AttributiveGenIDef|AttributiveInst 
NounRandomOBase  NZero NouNinflBoundary NPlReg AttributiveGenUDef|AttributiveInst 



PATTERN NounDeclCountry
# These are country nouns which take -ոյ- reduction in dative
# անգլիա անգլիոյ պոլիս պոլսոյ
#also includes normal countries as separate list NounCountryUnreducible
# Add toponym feature
#Singular caseless անգլիա
NounCountryBase|NounCountryUnreducible  [<n><np><top>:] NouNinflBoundary NSg0 (Def|Poss)? 
#Singular archaic case  անգլիոյ, must add infl boundary to get glide deletion անգլիոն
NounCountryReduced  [<n><np><top>:] NouNinflBoundary NSg0 DatGenArchaicOj NouNinflBoundary (Def|Poss)?  
#Singular modern dative case
NounCountryBase  [<n><np><top>:] NouNinflBoundary NSg0 DatGenRegI (Def|Poss)?  # Dir/LR
NounCountryUnreducible  [<n><np><top>:] NouNinflBoundary NSg0 DatGenRegI (Def|Poss)? 
#Singular regular oblique case
NounCountryBase|NounCountryUnreducible  [<n><np><top>:] NouNinflBoundary NSg0 CaseRegOblique (Def|Poss)? 



#Plural անգլիաներ
NounCountryBase|NounCountryUnreducible [<n><np><top>:]  NouNinflBoundary NPlReg CaseRegU? (Def|Poss)? 
#regularized plural possessive տուներնիս
NounCountryBase|NounCountryUnreducible  [<n><np><top>:] NouNinflBoundary NSg0|NPlReg  PossPl CaseRegPostPluralPoss Def|Poss
NounCountryBase|NounCountryUnreducible  [<n><np><top>:] NouNinflBoundary NSg0|NPlReg  InsReg PossPl  Def|Poss


#attributive, i assume no ablaut in the attributive
NounCountryBase|NounCountryUnreducible  [<n><np><top>:] NouNinflBoundary NSg0 AttributiveGenIDef|AttributiveInst 
NounCountryBase|NounCountryUnreducible [<n><np><top>:]  NouNinflBoundary NPlReg AttributiveGenUDef|AttributiveInst 


PATTERN NounDeclFamilyNkForm
#These are family nouns which can take -նք in the nominative
# And նց in the genitive/dative
#With no other cases
#So this pattern only derives Վարդանանք Վարդանանց

NounFamilyNkForm NNkSuffix [<n><np><pat>:] NouNinflBoundary NSg0  (Def|Poss)? 
NounFamilyNkForm [<n><np><pat>:] NouNinflBoundary NSg0  NNkSuffixCaseNc  (Def|Poss)? 
 
 
 
PATTERN NounDeclIrregReduce
#For nouns which supplete or have single-lexeme paradigms
#տղայ
#regular caseless and non-dative case տղայէ
NounIrregBoyBase NZero NouNinflBoundary NSg0 CaseRegOblique? (Def|Poss)? 
#regular dative case տղայի
NounIrregBoyBase NZero NouNinflBoundary NSg0 DatGenRegI (Def|Poss)?  # Dir/LR
#irregular dative case տղու
NounIrregBoyReduced NZero NouNinflBoundary NSg0 DatGenRegU (Def|Poss)? 

#regularized plural and reguarlized case տղաներու տղաքներու
NounIrregBoyBase NZero  NouNinflBoundary NPlIrregK? NPlReg CaseRegU? (Def|Poss)?   # Dir/LR
#Irregular plural and irregular case տղաք տղոց տղոցմէ
NounIrregBoyBase NZero NouNinflBoundary NPlIrregK  (Def|Poss)? 
NounIrregBoyReduced NZero NouNinflBoundary PlDatGenIrregularOts (CaseBoundaryM CaseRegOblique)? (Def|Poss)? 

#regularized plural possessive տղայներնիս տղաքներնիս
NounIrregBoyBase NZero	 NouNinflBoundary NSg0|(NPlIrregK? NPlReg)  PossPl CaseRegPostPluralPoss Def|Poss  # Dir/LR
NounIrregBoyBase NZero  NouNinflBoundary NSg0|(NPlIrregK? NPlReg)  InsReg PossPl  Def|Poss  # Dir/LR
#irregular plural possessive տղաքնիս տղաքովնիս տղաքմովնիս
NounIrregBoyBase NZero	 NouNinflBoundary NPlIrregK  PossPl CaseRegPostPluralPoss Def|Poss
NounIrregBoyBase NZero  NouNinflBoundary NPlIrregK CaseBoundaryM? InsReg PossPl  Def|Poss

#attributive

NounIrregBoyBase NZero NouNinflBoundary NSg0 AttributiveGenIDef # Dir/LR
NounIrregBoyBase NZero NouNinflBoundary NSg0 AttributiveInst 
NounIrregBoyReduced NZero NouNinflBoundary NSg0 AttributiveGenUDef  
NounIrregBoyBase NZero  NouNinflBoundary NPlIrregK? NPlReg AttributiveGenUDef|AttributiveInst    # Dir/LR
NounIrregBoyReduced NZero NouNinflBoundary AttributiveOts 


#մարդ
#regular caseless and non-dative case մարդ
NounIrregMan NZero NouNinflBoundary NSg0 CaseRegOblique? (Def|Poss)? 
#regular dative case մարդի
NounIrregMan NZero NouNinflBoundary NSg0 DatGenRegI (Def|Poss)?  # Dir/LR
#irregular dative case մարդու
NounIrregMan NZero NouNinflBoundary NSg0 DatGenRegU (Def|Poss)? 

#regularized plural and reguarlized case մարդերու
NounIrregMan NZero  NouNinflBoundary NPlReg CaseRegU? (Def|Poss)?   # Dir/LR
#Irregular plural and irregular case մարդիկ մարդոց մարդոցմէ
NounIrregMan NZero NouNinflBoundary NPlIrregIg  (Def|Poss)? 
NounIrregMan NZero NouNinflBoundary PlDatGenIrregularOts (CaseBoundaryM CaseRegOblique)? (Def|Poss)? 

#regularized plural possessive մարդերնիս
NounIrregMan NZero	 NouNinflBoundary NSg0|NPlReg  PossPl CaseRegPostPluralPoss Def|Poss  # Dir/LR
NounIrregMan NZero  NouNinflBoundary NSg0|NPlReg  InsReg PossPl  Def|Poss	 # Dir/LR
#irregular plural possessive մարդիկնիս մարդոցնիս մարդոցովնիս մարդոցմովնիս
NounIrregMan NZero	 NouNinflBoundary NPlIrregIg  PossPl CaseRegPostPluralPoss Def|Poss
NounIrregMan NZero  NouNinflBoundary  PlDatGenIrregularOts CaseBoundaryM? InsReg PossPl  Def|Poss

#attributive
NounIrregMan NZero NouNinflBoundary NSg0 AttributiveGenUDef|AttributiveInst  
NounIrregMan NZero NouNinflBoundary NSg0 AttributiveGenIDef  # Dir/LR
NounIrregMan NZero  NouNinflBoundary NPlReg AttributiveGenUDef|AttributiveInst    # Dir/LR
NounIrregMan NZero NouNinflBoundary  AttributiveOts 



#տիկին
#regular caseless  տիկին
NounIrregWomanBase NZero NouNinflBoundary NSg0 (Def|Poss)? 
#regular dative case and oblique տիկինի տիկինէ
NounIrregWomanBase NZero NouNinflBoundary NSg0 CaseRegI (Def|Poss)?  # Dir/LR
#irregular dative case տիկնոջ and oblique տիկնոջմէ
NounIrregWomanReduced NZero NouNinflBoundary NSg0 DatGenArchaicOch (CaseBoundaryM CaseRegOblique)? (Def|Poss)? 

#regularized plural and reguarlized case տիկիններու
NounIrregWomanBase NZero  NouNinflBoundary NPlReg CaseRegU? (Def|Poss)? 
#Irregular plural and irregular case տիկնայք տինանց  տիկնանցմէ տիկնանցէ
NounIrregWomanReduced NZero NouNinflBoundary [<pl>:այք]  (Def|Poss)? 
NounIrregWomanReduced NZero NouNinflBoundary [<dat_gen>:անց] (CaseBoundaryM CaseRegOblique)? (Def|Poss)? 

#regularized plural possessive տիկիննիս տիկիններնիս
NounIrregWomanBase NZero	NouNinflBoundary NSg0|NPlReg  PossPl CaseRegPostPluralPoss Def|Poss
NounIrregWomanBase NZero  	NouNinflBoundary NSg0|NPlReg  InsReg PossPl  Def|Poss
#irregular plural possessive տիկնայքնիս տիկնացնիս տիկնանցմովնիս
NounIrregWomanReduced NZero NouNinflBoundary [<pl>:այք]  PossPl CaseRegPostPluralPoss Def|Poss
NounIrregWomanReduced NZero NouNinflBoundary [<dat_gen>:անց] CaseBoundaryM? InsReg PossPl Def|Poss

#attributive, i dont know about attriburive for այք/անց
NounIrregWomanBase NZero NouNinflBoundary NSg0 AttributiveGenIDef|AttributiveInst   # Dir/LR
NounIrregWomanReduced NZero NouNinflBoundary NSg0 AttributiveOch
NounIrregWomanBase NZero  NouNinflBoundary NPlReg AttributiveGenUDef|AttributiveInst


# աստուած աստծոյ աստուծոյ աստուծմէ
#Singular 
NounIrregGod NZero NouNinflBoundary NSg0   (Def|Poss)? 
#Regular case
NounIrregGod NZero NouNinflBoundary NSg0 CaseRegI (Def|Poss)?  # Dir/LR
# Archaic dative case  աստծոյ աստուծոյ աստուծոյն
#Have to variably force a boundary after ոյ to trigger glide deletion
NounIrregGodNoVowel NZero NouNinflBoundary NSg0 DatGenArchaicOj NouNinflBoundary? (Def|Poss)?  #Dir/LR
NounIrregGodReducedU NZero NouNinflBoundary NSg0 DatGenArchaicOj NouNinflBoundary (Def|Poss)? 
NounIrregGodReducedU NZero NouNinflBoundary NSg0 DatGenArchaicOj Def|Poss #Dir/LR
# Archaic oblique case with nasal   աստուծմէ
NounIrregGodReducedU NZero NouNinflBoundary NSg0 CaseBoundaryM CaseRegOblique (Def|Poss)? 

#Plural
NounIrregGod NZero  NouNinflBoundary NPlReg CaseRegU? (Def|Poss)? 

#Plural possessive (separate line for instrumental)
NounIrregGod NZero	 NouNinflBoundary NSg0|NPlReg  PossPl CaseRegPostPluralPoss Def|Poss
NounIrregGod NZero  NouNinflBoundary NSg0|NPlReg  InsReg PossPl  Def|Poss

#don't think can form attributive irregularly
NounIrregGod NZero NouNinflBoundary NSg0 AttributiveGenIDef|AttributiveInst
NounIrregGod NZero  NouNinflBoundary NPlReg AttributiveGenUDef|AttributiveInst

# սէր սիրոյ հոգի հոգւոյն, սիրով մարմին մարմնոյ
#Singular 
NounLoveBase NZero NouNinflBoundary NSg0  (Def|Poss)? 
#regular case 
NounLoveBase NZero NouNinflBoundary NSg0 CaseRegI (Def|Poss)?  
#in case of high vowel reduction
NounLoveBase NZero NouNinflBoundaryRed NSg0 CaseRegI? (Def|Poss)?  # Dir/LR

# Archaic dative case  սիրոյ սիրոյն
#Can't have  boundary after ոյ to block glide deletion + force ն definite
NounLoveReduced NZero NouNinflBoundary NSg0 DatGenArchaicOj ([<def>:ն]|Poss)?   # Dir/LR
#սիրով սիրովն
NounLoveReduced NZero NouNinflBoundary NSg0 InsReg ([<def>:ն]|Poss)?   # Dir/LR

#Plural
NounLoveBase NZero  NouNinflBoundary NPlReg CaseRegU? (Def|Poss)? 
#Plural possessive (separate line for instrumental)
NounLoveBase NZero	 NouNinflBoundary NSg0|NPlReg  PossPl CaseRegPostPluralPoss Def|Poss
NounLoveBase NZero  NouNinflBoundary NSg0|NPlReg  InsReg PossPl  Def|Poss

#attributive, just regular
NounLoveBase NZero NouNinflBoundary NSg0 AttributiveGenIDef|AttributiveInst
NounLoveBase NZero  NouNinflBoundary NPlReg AttributiveGenUDef|AttributiveInst

#աղջիկ for dative աղջկան աղջկայ
#Singular 
NounGirlBase NZero NouNinflBoundary NSg0 (Def|Poss)? 
# regular case աղջիկի
NounGirlBase NZero NouNinflBoundary NSg0 DatGenRegI (Def|Poss)?    # Dir/LR
NounGirlBase NZero NouNinflBoundary NSg0 CaseRegOblique (Def|Poss)?
#Reduced աղջկայ աղջկան -- not sure if nasal is always definite TO DO
NounGirlReduced NZero NouNinflBoundary NSg0 DatGenArchaicAnAj NouNinflBoundary (Def|Poss)?  
# Reduced and oblique աղջկանէ
NounGirlReduced NZero NouNinflBoundary NSg0 DatGenArchaicAn CaseRegOblique (Def|Poss)?   # Dir/LR

#Plural
NounGirlBase NZero  NouNinflBoundary NPlReg CaseRegU? (Def|Poss)? 
#Plural possessive (separate line for instrumental)
NounGirlBase NZero	 NouNinflBoundary NSg0|NPlReg  PossPl CaseRegPostPluralPoss Def|Poss
NounGirlBase NZero  NouNinflBoundary NSg0|NPlReg  InsReg PossPl  Def|Poss

#attributive
NounGirlBase NZero NouNinflBoundary NSg0 AttributiveGenIDef  # Dir/LR
NounGirlBase NZero NouNinflBoundary NSg0 AttributiveInst
NounGirlBase NZero  NouNinflBoundary NPlReg AttributiveGenUDef|AttributiveInst


#ճամբայ ճամբուն ճամբայի ճամբէն , էշ էշու իշու էշէ իշէ

#Singular 
NounIrregRoadReduceBase NZero NouNinflBoundary NSg0 (Def|Poss)? 
# regular case ճամբայի
NounIrregRoadReduceBase NZero NouNinflBoundary NSg0 DatGenRegI (Def|Poss)?  # Dir/LR
NounIrregRoadReduceBase NZero NouNinflBoundary NSg0 CaseRegOblique (Def|Poss)? 
#Reduced ճամբու ճամբէն 
NounIrregRoadReduceReduced NZero NouNinflBoundary NSg0 DatGenRegU (Def|Poss)?  
NounIrregRoadReduceReduced NZero NouNinflBoundary NSg0 CaseRegOblique (Def|Poss)?   # Dir/LR

#Plural
NounIrregRoadReduceBase NZero  NouNinflBoundary NPlReg CaseRegU? (Def|Poss)? 
#Plural possessive (separate line for instrumental)
NounIrregRoadReduceBase NZero	 NouNinflBoundary NSg0|NPlReg  PossPl CaseRegPostPluralPoss Def|Poss
NounIrregRoadReduceBase NZero  NouNinflBoundary NSg0|NPlReg  InsReg PossPl  Def|Poss

NounIrregRoadReduceBase NZero NouNinflBoundary NSg0 DatGenRegI (Def|Poss)?  # Dir/LR

#attributive
NounIrregRoadReduceBase NZero NouNinflBoundary NSg0 AttributiveGenIDef  # Dir/LR
NounIrregRoadReduceBase NZero NouNinflBoundary NSg0 AttributiveInst 
NounIrregRoadReduceReduced NZero NouNinflBoundary NSg0 AttributiveGenUDef 
NounIrregRoadReduceReduced NZero NouNinflBoundary NSg0 AttributiveInst   # Dir/LR
NounIrregRoadReduceBase NZero  NouNinflBoundary NPlReg AttributiveGenUDef|AttributiveInst


#հաշիւ հաշուով
#Singular 
NounIrregCountReduceBase NZero NouNinflBoundary NSg0 (Def|Poss)? 
# regular case հաշիւիդ հաշւիդ 
NounIrregCountReduceBase NZero NouNinflBoundary NSg0 CaseRegI (Def|Poss)? 
NounIrregCountReduceBase NZero NouNinflBoundaryRed NSg0 CaseRegI? (Def|Poss)?  # Dir/LR

#Reduced հաշուի
NounIrregCountReduceReduced NZero NouNinflBoundary NSg0 CaseRegI (Def|Poss)?    # Dir/LR

#Plural
NounIrregCountReduceBase NZero  NouNinflBoundary NPlReg CaseRegU? (Def|Poss)? 
#Plural possessive (separate line for instrumental)
NounIrregCountReduceBase NZero	 NouNinflBoundary NSg0|NPlReg  PossPl CaseRegPostPluralPoss Def|Poss
NounIrregCountReduceBase NZero  NouNinflBoundary NSg0|NPlReg  InsReg PossPl  Def|Poss

#attributive
NounIrregCountReduceBase NZero NouNinflBoundary NSg0 AttributiveGenIDef|AttributiveInst
NounIrregCountReduceBase NZero NouNinflBoundaryRed NSg0 AttributiveGenIDef|AttributiveInst # Dir/LR
NounIrregCountReduceReduced NZero NouNinflBoundary NSg0 AttributiveGenIDef    # Dir/LR
NounIrregCountReduceBase NZero  NouNinflBoundary NPlReg AttributiveGenUDef|AttributiveInst


#For a bunch of words with frozen classical case-slots
# անուն անունի անուան անուամբ
#Regular form is norm
IrregFrozenBase NZero NouNinflBoundary NSg0 CaseRegI? (Def|Poss)? 
IrregFrozenBase NZero NouNinflBoundaryRed NSg0 CaseRegI? (Def|Poss)?  # Dir/LR
#frozen, i pushed the infletion feature inside the lexical entry
IrregFrozenCase (Def|Poss)? 

#Plural
IrregFrozenBase NZero  NouNinflBoundary NPlReg CaseRegU? (Def|Poss)? 
IrregFrozenBase NZero  NouNinflBoundaryRed NPlReg CaseRegU? (Def|Poss)? # Dir/LR
#Plural possessive (separate line for instrumental)
IrregFrozenBase NZero	 NouNinflBoundary NSg0|NPlReg  PossPl CaseRegPostPluralPoss Def|Poss
IrregFrozenBase NZero	 NouNinflBoundaryRed NSg0|NPlReg  PossPl CaseRegPostPluralPoss Def|Poss # Dir/LR
IrregFrozenBase NZero  NouNinflBoundary NSg0|NPlReg  InsReg PossPl  Def|Poss
IrregFrozenBase NZero  NouNinflBoundaryRed NSg0|NPlReg  InsReg PossPl  Def|Poss # Dir/LR

#attributive
IrregFrozenBase NZero NouNinflBoundary NSg0 AttributiveGenIDef|AttributiveInst
IrregFrozenBase NZero NouNinflBoundaryRed NSg0 AttributiveGenIDef|AttributiveInst  # Dir/LR
IrregFrozenBase NZero  NouNinflBoundary NPlReg AttributiveGenUDef|AttributiveInst
IrregFrozenBase NZero  NouNinflBoundaryRed NPlReg AttributiveGenUDef|AttributiveInst # Dir/LR


# for words with optional syncope of non-high vowel in declined forms like բերան բերնի
NounSyncopeBase
#Singular  բերանը
NounSyncopeBase NZero NouNinflBoundary NSg0 (Def|Poss)? 
# regular case բերանիդ  բերանէդ
NounSyncopeBase NZero NouNinflBoundary NSg0 CaseRegI (Def|Poss)? 
NounSyncopeBase NZero NouNinflBoundaryRed NSg0 CaseRegI? (Def|Poss)?  # Dir/LR

#Reduced բերնիդ բերնէդ
NounSyncopeReduced NZero NouNinflBoundary NSg0 CaseRegI (Def|Poss)?    # Dir/LR

#Plural բերաններ
NounSyncopeBase NZero  NouNinflBoundary NPlReg CaseRegU? (Def|Poss)? 
#Plural possessive (separate line for instrumental)
NounSyncopeBase NZero  NouNinflBoundary NSg0|NPlReg  PossPl CaseRegPostPluralPoss Def|Poss
NounSyncopeBase NZero  NouNinflBoundary NSg0|NPlReg  InsReg PossPl  Def|Poss

#attributive
NounSyncopeBase NZero NouNinflBoundary NSg0 AttributiveGenIDef|AttributiveInst
NounSyncopeBase NZero NouNinflBoundaryRed NSg0 AttributiveGenIDef|AttributiveInst # Dir/LR
NounSyncopeReduced NZero NouNinflBoundary NSg0 AttributiveGenIDef    # Dir/LR
NounSyncopeBase NZero  NouNinflBoundary NPlReg AttributiveGenUDef|AttributiveInst


############ Attributive
#I don't think the gen-def for attributive replace def with a possessive

PATTERN AttributiveGenIDef
DatGenRegI  Def [+NP:] AttributiveDefinitePostInflection
PATTERN AttributiveGenUDef
DatGenRegU  Def [+NP:] AttributiveDefinitePostInflection
PATTERN AttributiveInst
InsReg  [+NP:] AttributiveInstrumentalPostInflection
PATTERN AttributiveVan
#special ones for the Van forms
[<dat_gen><def>:ուան]   [+NP:] AttributiveDefinitePostInflection
[<ins>:ուընով]  [+NP:] AttributiveInstrumentalPostInflection
PATTERN AttributiveUm
#special ones for the um forms
NUmSuffixReduced DatGenArchaicAn  [+NP:] AttributiveDefinitePostInflection #Dir/LR
NUmSuffixBase DatGenRegI  Def   [+NP:] AttributiveDefinitePostInflection
NUmSuffixBase InsReg [+NP:] AttributiveInstrumentalPostInflection #Dir/LR
PATTERN AttributiveUtjun
# archaic forms like թեանէն
CaseUtjunArchaic   [+NP:] AttributiveDefinitePostInflection #Dir/LR 
NUtjunSuffix AttributiveGenIDef|AttributiveInst

PATTERN AttributiveOch
#special ones for the och forms
DatGenArchaicOch [+NP:] AttributiveDefinitePostInflection
DatGenArchaicOch CaseBoundaryM InsReg  [+NP:] AttributiveInstrumentalPostInflection
DatGenArchaicOch InsReg  [+NP:] AttributiveInstrumentalPostInflection # Dir/LR
PATTERN AttributiveOts
#special ones for the ots forms
PlDatGenIrregularOts   [+NP:] AttributiveDefinitePostInflection
PlDatGenIrregularOts CaseBoundaryM InsReg   [+NP:] AttributiveInstrumentalPostInflection
PlDatGenIrregularOts  InsReg [+NP:] AttributiveInstrumentalPostInflection # Dir/LR
PATTERN AttributiveAn
#special ones for the an forms like աղջիկ
DatGenArchaicAn  [+NP:] AttributiveDefinitePostInflection #Dir/LR
DatGenArchaicAn InsReg [+NP:] AttributiveInstrumentalPostInflection #Dir/LR


PATTERN AttributiveDefinitePostInflection
#This is for declension of an attributive noun
#Pre-inflection, an attributive noun ends in genitive-definite or in instrumental
#This is for the definit-ending attributive
#It has to take some overt inflection later
NSg0 CaseRegI (Def|Poss)?
NSg0 (Def|Poss)
NPlReg CaseRegU? (Def|Poss)? 

PATTERN AttributiveInstrumentalPostInflection
#This is for the instrumental-ending attributive;
#It can't just take definite because then the same meaning as un-attributive
#It has to take some overt inflection later
NSg0 CaseRegI (Def|Poss)?
NPlReg CaseRegU? (Def|Poss)? 

#####################
####### Case patterns

PATTERN CaseRegI
#NomAccReg
DatGenRegI
AblReg
InsReg

PATTERN CaseRegOblique
AblReg
InsReg

PATTERN CaseRegU
#I allow the regular ի to be an optional form
#This overgeneralized for infintivals and reduced irregulars like ամսի
#NomAccReg
DatGenRegU
AblReg
InsReg

PATTERN CaseRegPostPluralPoss
#NomAccReg 	
DatGenRegI
AblReg




PATTERNS

#Relevant functional morphemes
Indf

##############
##############
##############
#### Adjective morphemes
##############
##############
LEXICON AdvOren
<adv>:օրէն
LEXICON AdjSuper
<sup>:ամէնա # Dir/LR common misspelling
<sup>:ամենա


##############
##############
##############
#### Noun morphemes
##############
##############

LEXICON NZero
<n>:

LEXICON NUtjunSuffix
:թիւն

#ում suffix reduces to մ before ան
LEXICON NUmSuffixBase
:ում
LEXICON NUmSuffixReduced
:մ


#The suffix նք when part of a nominal suffix like family names
LEXICON NNkSuffix
:նք
#And its dative/genitive form
LEXICON NNkSuffixCaseNc
<dat_gen>:նց

###### Other POS morphemes
LEXICON AdjZero
<adj>:
LEXICON AdvZero
<adv>:
LEXICON SubstZero
<subst>:


#Number marking
LEXICON NSg0
<sg>:
LEXICON NPlReg
#This is the correct plural that counts syllables
<pl>:{Plն}եր 
#But for plural variation and errors:
#Compounds can vary in the choice of plural suffix
#Some monosyllables optionally surface with -ներ  because of possessive plural readings in Eastern
<pl>:ներ		# Dir/LR
<pl>:եր		# Dir/LR

LEXICON NPlVary
#This is for monosyllabic words which can take both plurals
<pl>:ներ		# Dir/LR
<pl>:եր		

LEXICON NPlNerOnly
<pl>:ներ	

LEXICON NPlIrregK
<pl>:ք

LEXICON NPlIrregIg
<pl>:իկ

#Boundary between noun and inflection
LEXICON NouNinflBoundary
:{Ninfl}
#Used for reduction and other boundary rules
LEXICON NouNinflBoundaryRed
:{NinflRed}
#Used for forced reduction before plural -ner 
LEXICON NouNinflBoundaryRedForcedNer
:{NinflRedForcedNer}

#Case marking
#LEXICON NomAccReg
#<nom>:
#<acc>:
LEXICON DatGenRegI
<dat_gen>:ի
<dat_gen>:ու #Dir/LR In case of unexpected irregularization
LEXICON DatGenRegU
<dat_gen>:ու
<dat_gen>:ի #Dir/LR In case of regularization
LEXICON AblReg
<abl>:է
LEXICON InsReg
<ins>:ով

LEXICON CaseUtjunArchaic
<dat_gen>:թեան
<abl>:թենէ
<ins>:թեամբ
LEXICON CaseUtjunArchaicPlural
<pl><dat_gen>:թեանց

LEXICON CaseTimeVan
<dat_gen>:ուան # Dir/LR
<dat_gen>:ուայ 
<abl>:ուընէ
<abl>:ուանէ # Dir/LR
<ins>:ուընով 
<ins>:ուանով # Dir/LR

LEXICON DatGenArchaicAn
<dat_gen>:ան

LEXICON DatGenArchaicAnAj
<dat_gen>:ան
<dat_gen>:այ

LEXICON InstArchaicAmp
<ins>:ամբ


LEXICON DatGenArchaicOch
<dat_gen>:ոջ

LEXICON DatGenArchaicOj
<dat_gen>:ոյ


LEXICON DatGenArchaicEmpty #used for հօր
<dat_gen>:

#irregular marker of plural and dative genitive like it մարդոց
LEXICON PlDatGenIrregularOts
<pl><dat_gen>:ոց


#Some irregulars take regular oblique case after մ
#Like մարդոցմէ
LEXICON CaseBoundaryM
:մ

#Definite/possessive marking
LEXICON Def
<def>:{defն}
<def>:ն # Dir/LR for pre-clitic forms

LEXICON Poss
<px1sg>:ս
<px1sg>:ըս # Dir/LR  -- in case of typos because EANC has this rule
<px2sg>:դ
<px2sg>:ըդ # Dir/LR  -- in case of typos because EANC has this rule

LEXICON PossPl
# Spurious plural -er- is added before -ni 
#after monosyllable and before plural possessive 
<pl_poss>:{SpurPlե}{SpurPlր}ն{PossPlի}  #The ի is deleted in vowel hiatus

#Functional morphemes
LEXICON Indf
մը<enc><idf>:մը
մը<enc><idf>:մըն

#####  Nouns
LEXICON NounReg
գիրք:գիրք
պատ:պատ
փայտ:փայտ
կապիկ:կապիկ
տուփ:տուփ
կամուրճ:կամուրճ
արքայ:արքայ
բայ:բայ
հայ:հայ
լիբանանահայ:լիբանանահայ{mono}

#Adjectives and adverbs
LEXICON AdjReg
մեծ:մեծ
ուրախ:ուրախ
LEXICON AdvReg
ուրախօրէն:ուրախօրէն

#Monosyllabic nouns which arbitrary take -ner 
#When forming their possessive plural, I have to manually add the ն before the possessive plural
LEXICON NounIrregMonoNerOld   #Some prescriptively take -ner, but in real life, they take -er
բեռ:բեռ  #Taken from Hagopian 350
դուռ:դուռ
եզ:եզ
թոռ:թոռ
հարս:հարս
մատ:մատ
մուկ:մուկ
ձուկ:ձուկ
ձեռ:ձեռ
ոտ:ոտ
ռուս:ռուս
# մահ:մահ # removed because the word has too many other irregular forms + the ner form is obsolete


LEXICON NounIrregMonoNerStill  #Only very few both descriptively and presriptively  take -ner
լեռ:լեռ
LEXICON NounRisingClusters
մեղր:մեղր

LEXICON NounIrregU
#These words take ու in the dative
#I allow them to optionally take ի 
#This don't have high vowels that can reduce
հայ:հայ #Taken from Hagopian 351
ձի:ձի
մանչ:մանչ
չին:չին
չար:չար
գահ:գահ
պահ:պահ
դար:դար
ծով:ծով
կով:կով
հաւ:հաւ
արջ:արջ
թի:թի
արեւ:արեւ

#Some nouns optionally take irregular inflection
#When they do, they reduce
LEXICON NounIrregUReducePoly
գլուխ:գլուխ

#List of all words that take the ութիւն suffix
#The suffix utjun cannot be part of the stem
LEXICON NounUtjunSuffix
ուրախութիւն:ուրախու

#List of all words that take the -ում suffix
#The suffix ում takes an optional irregular -ան
#The suffix ում cannot be part of the stem
LEXICON NounUmSuffix
շարժում:շարժ
անկում:անկ



#Time words which take the ուան declension
LEXICON NounTimeVan
մահ:մահ
օր:օր
ժամանակ:ժամանակ
գիշեր:գիշեր
ցերեկ:ցերեկ
վաղը:վաղ
երեկ:երեկ
ատեն:ատեն
ամառ:ամառ
ձմեռ:ձմեռ

LEXICON NounTimeVanU
#words which take the -van declension class, but can also do dative -u
ժամ:ժամ

#Time words which reduce a final vowel or substring in inflected forms
LEXICON NounTimeVanReducedBase
իրիկուն:իրիկուն
առտու:առտու
հեռու:հեռու
տարի:տարի
LEXICON NounTimeVanReducedReduced
իրիկուն:իրիկ
առտու:առտ
հեռու:հեռ
տարի:տար

#Words which take -van and -u and reduce
LEXICON NounTimeVanUReducedBase
շաբաթ:շաբաթ
ամիս:ամիս
տարի:տարի # Dir/LR
LEXICON NounTimeVanUReducedReduced
ամիս:ամս
շաբաթ:շաբթ
տարի:տար # Dir/LR

#Adverbs which take -van when nominalized
LEXICON NounTimeVanAdv
այսօր:այսօր
երբ:երբ
առաջ:առաջ
անգամ:անգամ
հիմակ:հիմակ # this is a very rare word, more often just հիմա
LEXICON NounTimeVanHima
հիմա:հիմա #This word takes the time suffixes

#These irregulars only take an optional irregular dative ան, 
#while everything else is regular
#So I'll only make a lexicon for their reduced form for the dative
LEXICON NounOldAnForm
գարուն:գարն
աշուն:աշն
ամառ:ամռ
ձմեռ:ձմռ
մանուկ:մանկ
կնիկ:կնկ
լեռ:լեր
դուռ:դր

#These irregulars  take an optional irregular dative ոջ, 
#The oblique forms can take a մ 
#I classify into nouns that never reduce, those that do reduce, and the reduced form
LEXICON NounFamilyOchFixed
ընկեր:ընկեր
աներ:աներ
տալ:տալ
տագր:տագր
ներ:ներ
դասընկեր:դասընկեր

LEXICON NounFamilyOchBase
կին:կին
քոյր:քոյր
սկեսուր:սկեսուր
մօրաքոյր:մօրաքոյր{mono}
տէր:տէր
LEXICON NounFamilyOchReduced
կին:կն
քոյր:քր
տէր:տիր
տէր:տէր # Dir/LR
սկեսուր:սկեսր
մօրաքոյր:մօրաքր

#Words like հայր հօր
LEXICON NounFamilyOBase
հայր:հայր
մայր:մայր
եղբայր:եղբայր
տիրամայր:տիրամայր{mono}
կնքահայր:կնքահայր{mono}
LEXICON NounFamilyOAblauted
հայր:հօր
մայր:մօր
եղբայր:եղբօր
տիրամայր:տիրամօր
կնքահայր:կնքահօր

#Words like տուն տան
LEXICON NounRandomOBase
տուն:տուն
շուն:շուն
դրամատուն:դրամատուն{mono}
ծովաշուն:ծովաշուն{mono}
LEXICON NounRandomOAblauted
տուն:տան
շուն:շան
դրամատուն:դրամատան
ծովաշուն:ծովաշան





#Family names which take the նք suffix in nominative
#And նց in dative/genitive with no other cases
#I don't include the suffix in the stem
LEXICON NounFamilyNkForm
Վարդանանք:Վարդանա



#These are nouns which supplete or have irregular reduction in their declensions
#The noun տղայ suppletes to տղու for dative/genitive and takes irregular plural
LEXICON NounIrregBoyBase
տղայ:տղայ
LEXICON NounIrregBoyReduced
տղայ:տղ
LEXICON NounIrregMan
մարդ:մարդ
LEXICON NounIrregWomanBase
տիկին:տիկին
տանտիկին:տանտիկին
LEXICON NounIrregWomanReduced #I make hard-coded allomorphs տիկնայք տիկնանց inside the PATTTERns
տիկին:տիկն
տանտիկին:տանտիկն

#The noun ճամբայ suppletes to  ճամբու for dative/genitive, and էշ էշու իշու
LEXICON NounIrregRoadReduceBase
ճամբայ:ճամբայ
էշ:էշ
LEXICON NounIrregRoadReduceReduced
ճամբայ:ճամբ
էշ:իշ # Dir L/R
էշ:էշ # Dir L/R

#The noun հաշիւ suppletes to հաշուով in irregulary reduced form
LEXICON NounIrregCountReduceBase
հաշիւ:հաշիւ
LEXICON NounIrregCountReduceReduced
հաշիւ:հաշու

#The noun աստուած suppletes to աստծոյ աստուծոյ  for dative/genitive
LEXICON NounIrregGod
աստուած:աստուած
LEXICON NounIrregGodReducedU
աստուած:աստուծ
LEXICON NounIrregGodNoVowel
աստուած:աստծ

#The noun սէր reduces to սիրոյ  for dative/genitive
LEXICON NounLoveBase
սէր:սէր
հոգի:հոգի
ամուսին:ամուսին
որդի:որդի
եկեղեցի:եկեղեցի
մարմին:մարմին
խնդիր:խնդիր
LEXICON NounLoveReduced
սէր:սիր
հոգի:հոգւ
ամուսին:ամուսն
որդի:որդւ
եկեղեցի:եկեղեցւ
մարմին:մարմն
խնդիր:խնդր
մարդ:մարդ  # Dir/LR  don't have unreduced form because taken care of by special declension

#The noun բերանի reduces to բերնի բերնէ by irregular syncope of non-high vowel
LEXICON NounSyncopeBase
բերան:բերան
LEXICON NounSyncopeReduced
բերան:բերն

#The noun աղջիկ reduces to աղջկա աղջկան  for dative/genitive
LEXICON NounGirlBase
աղջիկ:աղջիկ
LEXICON NounGirlReduced
աղջիկ:աղջկ

#For nouns which are regular except for fossilized archaic case-forms
LEXICON IrregFrozenBase
դուստր:դուստր
կայսր:կայսր
անուն:անուն
հիմն:հիմն
արիւն:արիւն
LEXICON IrregFrozenCase
դուստր<n><dat_gen>:դստեր
կայսր<n><dat_gen>:կայսեր
անուն<n><dat_gen>:անուան
անուն<n><ins>:անուամբ
հիմն<n><dat_gen>:հիման
արիւն<n><dat_gen>:արեան
արիւն<n><ins>:արեամբ

#The country names անգլիա changes to անգլիոյ  for dative/genitive
#Regular nouns which never shift are in separate group
LEXICON NounCountryBase
Անգլիա:Անգլիա
Կիլիկիա:Կիլիկիա
Պոլիս:Պոլիս
LEXICON NounCountryReduced
Անգլիա:Անգլի
Կիլիկիա:Կիլիկի
Պոլիս:Պոլս
LEXICON NounCountryUnreducible
Հայաստան:Հայաստան


LEXICON NameMaleGiven
Յովսէփ:Յովսէփ
LEXICON NameFemaleGiven
Մարիամ:Մարիամ
LEXICON NameGivenUnclassified
#Given names that were taken from un-gendered lists
Ամալյա:Ամալյա
LEXICON NameFamily
Տէօվլէթեան:Տէօվլէթեան
Յովսէփեան:Յովսէփեան

#This is just for quick use o# f just the noun-paradigm lexd
# LEXICON NameMaleGivenLocation
# Վան:Վան
# LEXICON NameMaleFemaleGivenLocation
# Վան:Վան
# LEXICON AdvAdjReg
# կրկնակի:կրկնակի
# LEXICON AdvAdjOren
# կրկնակի:կրկնակի
# LEXICON AdvNounReg
# կրկնակի:կրկնակի
# LEXICON NounAdjReg
# կրկնակի:կրկնակի
# LEXICON Infinitival
# կրկնակի:կրկնակի
# LEXICON NameMaleFemaleGiven 
# կրկնակի:կրկնակի
# LEXICON NameFemaleGivenLocation
# կրկնակի:կրկնակի


#! Resources:
#! https://wiki.apertium.org/wiki/Starting_a_new_language_with_HFST#Lexicon
#! https://wiki.apertium.org/wiki/Lttoolbox_and_lexc
#! https://wiki.apertium.org/wiki/Apertium-specific_conventions_for_lexc
#! https://kitwiki.csc.fi/twiki/bin/view/KitWiki/HfstHome
#! https://github.com/hfst/
#! http://www.cis.upenn.edu/~cis639/docs/lexc.html
#! https://wiki.apertium.org/wiki/Hfst



